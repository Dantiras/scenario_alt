label alt_day3_dv_lf:
    "Может, она там сейчас?"
    window hide
    scene bg ext_stage_big_night with dissolve
    "До эстрады оказалось ближе, чем я надеялся."
    "Во всяком случае, решимости набраться я так и не успел."
    "И не в последнюю очередь этому способствовала «взвейтесь кострами» в гитарном исполнении, которую я слышал чуть ли не от собственного домика."
    "Кто-то там сидел и играл."
    "Хотя почему «кто-то»? Я и так знаю достоверно."
    "Вопрос лишь в том, обрадуется ли она моей сомнительной компании."
    play music dv_guitar fadein 7
    "Я поднялся на сцену."
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_stage_big_night with dissolve
    $ lp_dv += 1
    "Она играла какую-то незнакомую, но крайне печальную мелодию."
    "Гитара в её руках пела и плакала, выдавая звук, который, казалось, неспособен выдать инструмент."
    "Алиса была полностью поглощена игрой, и на окружающих не обращала внимания, что и позволило мне подобраться поближе и сесть "
    if alt_day2_loki_minijack and alt_day2_rendezvous != 3:
        extend "на то же место, где она сидела вчера."
    else:
        extend "на самое удобное место."
    "Она всё играла и играла, а я сидел и боялся пошевелиться, спугнуть волшебство момента."
    "Наконец в воздух ввинтился последний аккорд, и девочка, прижав струны ладонью, резко выдохнула, будто спустили надувной мячик — вся как-то сжавшись и ссутулившись."
    play ambience ambience_camp_center_evening fadein 2
    show dv guilty pioneer2 at center with dissolve
    if alt_day2_date == 132:
        dv "Вечера."
        "Я кивнул."
        dv "Зачем пришёл?"
        "Я пожал плечами:"
        me "Ну не на танцы же идти. Обезьян я могу посмотреть и в зоопарке."
        stop music fadeout 3
        show dv smile pioneer2 with dspr
        "Алиса прихлопнула ладонью струны и улыбнулась."
        dv "Какое тёплое отношение!"
        me "Ну… Любить мне их не за что, им меня тоже. Так что лучше я пойду посмотрю то, что мне действительно интересно."
        show dv grin pioneer2 with dspr
        dv "Ну и как оно?"
        me "Ты здорово играешь. Сбацаешь ещё что-нибудь?"
        dv "Нет, концерт закончен."
    else:
        dv "Ты пришёл."
        stop music fadeout 5
        "Спокойно заметила она."
        me "Да."
        "Я решил отвечать в том же стиле."
        show dv sad pioneer2 at center with dissolve
        dv "Зачем?"
        me "Ты звала."
        dv "И только?"
        me "Мне достаточно и этого."
        "Я вытянул ноги на скамейку перед собой и опёрся руками на заднюю, расслабляясь в импровизированном «кресле»."
        me "Ты обещала сыграть песню собственного сочинения."
        show dv shy pioneer2 at center with dissolve
        dv "Ты её только что слышал."
    "Безучастно ответила она."
    me "Что ж…"
    "Я вздохнул и поднялся."
    me "Раз концерт окончен…"
    "Я развернулся и собрался было уходить, когда в спину вдруг прилетело удивительно жалобное:"
    return

label alt_day3_rockstar:
    show dv guilty pioneer2 at center with dissolve
    dv "Подожди…"
    me "Да?"
    "Я обернулся."
    me "Ты что-то хотела?"
    if alt_day3_dv2_event or alt_day2_date == 132:
        show dv shy pioneer2 with dspr
        dv "Не хочешь попробовать сыграть?"
        play music music_list["get_to_know_me_better"] fadein 5
        me "Да я как-то в музыке не очень…" # может вилку для Локи?
        dv "Да тут легко, вот смотри."
        "Кажется, здесь был какой-то собственный интерес."
        "Не знаю, что она задумала — но мне это что-то не очень нравится. Может, уйти, пока не поздно?"
        "Я заколебался, глядя на неё. От Двачевской пока что без исключений мне были только одни неприятности."
        "Так может, стоит разорвать порочный круг? Сойти с поезда, пока он не разогнался?"
        th "Или?"
        dv "Эй!"
        me "Что?"
        dv "Не засыпай."
        me "Да я не сплю, я…"
        dv "Медитируешь с открытыми глазами. Так что, будешь пробовать?"
        me "Сыграть?"
        dv "Ррррр. Да, сыграть. Будешь?"
        me "Я не знаю. Но ты покажи тогда, для начала."
        "Она кивнула и зажала первый аккорд."
        "…"
        window hide
        scene cg d3_dv_scene_2 with fade
        "В ночной воздух поплыли струнные переборы."
        th "Сейчас, когда она так вот держит струны…"
        me "Вроде, ничего сложного."
        dv "Ну так. Ты, главное, первый аккорд возьми, а там дальше терция вниз, ну и догадаться несложно."
        $ alt_day3_dv_guitar_lesson = True
        "Да, действительно, побежал и догадался. Человек, который гитару в руках держал ровно полтора раза в жизни."
        me "Эх… Ну ладно."
        window hide
        scene cg d3_dv_scene_1
        with dissolve
        "Первый аккорд деревянными пальцами."
        "И удар по струнам пальцами-сосисками."
        "Похоже, что даже гитара требует постоянной практики."
        dv "Ох…"
        me "Да ты не охай, погоди. Сейчас я."
        dv "Давай-давай."
        "Она тихо хихикнула."
        me "Просто давно уже не играл."
        "Я как будто извинялся за что-то. Что за бред!"
        me "Пробуем."
        "Деревянные пальцы деревянно ударили по струнам, деревянно съехали с лада."
        "Гитара издала звук, больше походящий на мяуканье голодной кошки."
        me "У тебя что, гитара расстроенная?"
        dv "Это ты меня сейчас расстроишь. Дай инструмент!"
        window hide
        scene cg d3_dv_scene_2
        with dissolve
        th "Ну да, легко доказывать собственное превосходство в том, где ты заведомо сильнее."
        "Она отобрала гитару и бегло наиграла последовательность аккордов."
        "Видимо, пожалев мою и без того здорово пошатнувшуюся самооценку, она не стала играть соло, которое мучала как раз в тот момент, когда я подошёл."
        me "Надеюсь, хоть кто-то из нас получает удовольствие от процесса."
        dv "Если ты перестанешь насиловать гитару и сделаешь всё по-человечески, мы оба удовольствие получим."
        "Она сыграла ещё раз и вопросительно посмотрела на меня."
        dv "Всё понял?"
        me "Эээ…"
        window hide
        scene cg d3_dv_scene_1
        with dissolve
        "Вполне логично было ожидать, что ничего у меня не получилось."
        "Одним голым энтузиазмом практику не заменишь. Здесь тупо нужны набитые руки, иначе ничего не получится."
        th "Самореализуется за мой счёт."
        "С неожиданной злостью подумал я и, забыв о том, что делаю вообще, даже изобразил некое подобие того, что показывала Алиса."
        stop music fadeout 3
        th "Внезапно."
        window hide
        $ persistent.sprite_time = "night"
        scene bg ext_stage_normal_night
        with dissolve
        play ambience ambience_camp_center_night fadein 2
        show dv normal pioneer at center 
        with dissolve
        dv "Не надорвался?"
        "Я процедил непечатное себе под нос."
        "А Алиса, расслышав окончание тирады, от души расхохоталась."
        show dv laugh pioneer at center 
        with dspr
        dv "Даже не думай. Со своей гитарой такие вещи вытворять я не позволю."
        me "Ага. {w}Ты предпочитаешь куда более жуткие вещи."
        show dv smile pioneer with dspr
        dv "Да ничего же страшного. Ты оказался не так безнадёжен как я думала."
        dv "Мы ещё сделаем из тебя гитариста."
        me "Правда? О, спасибо, всю жизнь мечтал, трепещу от нетерпения."
        show dv normal pioneer
        with dspr
        dv "Не торопись. Тебе ещё руки выпрямить надо."
        "Похоже, Двачевская обладала встроенным иммунитетом к сарказму."
        dv "И это будет самым сложным!"
        me "Самым сложным будет заставить меня повторить этот опыт ещё раз."
        "Я поднялся и отряхнул штаны."
        show dv laugh pioneer
        with dspr
        dv "Уже сдаёшься? А мне только показалось, что ты не слабак."
        "Её по-детски наивные попытки постоянно взять окружающих «на понт», похоже, были очевидны только мне."
        "И сначала они меня веселили."
        "Теперь ничего кроме раздражения не вызывают."
        th "Похоже, меня сюда позвали только и исключительно в качестве жертвы для насмешек."
        me "Бывай. Пойду обтекать."
        show dv surprise pioneer with dspr
        dv "Что?"
        me "Ну, от потока помоев, который на меня здесь вылили."
        show dv laugh pioneer
        with dspr
        dv "Ты обиделся, что ли? На себя надо обижаться за то, что руки-крюки."
        me "Если последняя фраза была заменой извинению — то крайне хреновой."
        me "Спокойной ночи. Уж лучше бы я позориться на танцы пошёл. Там надо мной хотя бы издеваться никто не собирался."
        "А что там дальше-то? Красивый жест сделан, уходя-уходи?"
        "Дальше опыт подсказывал добавить обидчика в блэк-листы и заигнорить по всем возможным каналам."
        "Жаль, в реале это невозможно. Вот уж кого я бы с удовольствием выключил из собственной жизни."
        show dv grin pioneer
        with dspr
        dv "Ты и правда обиделся?"
        "Похоже, до неё дошло, что она и в самом деле сказала что-то не то."
        me "А как ты догадалась?"
        "Я в очередной раз скатился в сарказм — совершенно бесполезный против этой девочки."
        dv "Ты расстроился и куда-то сбегать собираешься."
        th "Вот это дедукция, Шерлок!"
        me "Надеюсь, хотя бы тебе этот тупой вечер доставил удовольствие."
        hide dv
        with dissolve
        "Я развернулся и двинулся вон с места аутодафе."
        "На сегодня с меня хватит."
        "Алиса что-то говорила мне в спину, но тратить время и нервы на неё я больше не собирался."
        me "Ты — худшее, что встречалось мне в этой жизни."
        "Прошипел я себе под нос."
    window hide
    return
        
label alt_day3_dv_reunion:
    dv "Зачем ты… так."
    "Кажется, её смутило моё поведение. И всё же облегчать жизнь человеку, за меня всё решившему, я не собирался."
    me "Мы о чём сейчас говорим?"
    "Вежливо поинтересовался я."
    me "Если о музыке, то ты обещала сыграть песню."
    me "Ты сыграла, я выслушал, и мы, довольные друг другом, разбегаемся по норам. Спасибо за тёплый вечер."
    dv "Нет."
    show dv sad pioneer2 at center with dissolve
    "Из-за опустившейся темноты, её лицо было почти невозможно разглядеть, приходилось ориентироваться на один только силуэт." 
    dv "Я о нашем разговоре."
    me "Ага. {w=.3}Дай угадаю, ты считаешь, что поступила правильно, и я потом тебе ещё спасибо за это скажу?"
    show dv normal pioneer2 with dissolve
    dv "Нет."
    "Она снова помотала головой, а её голос опустился до шёпота."
    dv "Я… {w}Я хотела сказать, что мне жаль."
    me "Жаль потраченного времени?"
    me "На этот счёт можешь не беспокоиться, я больше не стану докучать тебе."
    "Я сам не знаю, что за бес в меня вселился, но это был определённо крайне жестокий и ядовитый бес."
    "Алиса стерпела и это. Просто вздрогнула и съёжилась, как тогда, у клуба."
    dreamgirl "Ты что творишь, придурок?!"
    dreamgirl "Девочка у тебя прощения просит, а ты нос воротишь?"
    dreamgirl "Или у тебя запасных вариантов избыток?"
    th "Ты мне не совесть. Молчи."
    dv "Нет, я…"
    me "Ты…"
    "Поощрительно продолжил я."
    "Без поощрения явно было не обойтись — девочке приходилось прикладывать огромные усилия к тому, чтобы просто продолжать разговор."
    "Но этого всё равно было недостаточно."
    dv " … извиняюсь за всё что наговорила тебе."
    "Было заметно, как трудно ей даются извинения."
    dv "Я была неправа."
    me "Да нет, ты была совершенно права. {w}Нам не стоило портить прекрасную дружбу всей этой романтической дребеденью."
    me "Твой единственный прокол  — в Лене: мы с ней за три дня и парой фраз не перебросились."
    show dv shy pioneer2 with dissolve
    if not alt_day1_un_ignored:
        dv "А как же тогда первый день?"
        "Её голос опасно балансировал между слезами и шёпотом. Хотя вряд ли такая девочка, как Алиса, станет рыдать. Слишком сильная, слишком самостоятельная."
        dv "Я же видела, как ты её окучивал."
        "И здесь я не выдержал."
        me "Окучивал?" with hpunch
        me "Это теперь так называется?"
        show blinking
        me "Алис… Только один вопрос {w} — у вас здесь поговорка про собаку на сене в ходу?"
        show dv guilty pioneer2 with dissolve
    "Она промолчала."
    "Похоже, дальнейший разговор лишен смысла."
    me "Это всё, что я хотел узнать."
    hide dv with dissolve
    window hide
    $ persistent.sprite_time = "sunset"
    scene bg ext_houses_night_7dl
    with dissolve
    play ambience ambience_camp_center_evening fadein 3
    "Я окончательно развернулся и ушёл, оставляя Алису наедине с её дурацкими мыслями, идиотской ревностью и ничем не обоснованными претензиями." 
    "А в голове продолжали крутиться гитарные переборы…"
    $ persistent.sprite_time = "night"
    play music deadman fadein 3
    "Ночной лагерь всё время разный."
    "В первый день он был тёплым, немного пугающим и в чём-то даже томным."
    "Во второй я будто проснулся, и всё было такое яркое-яркое…"
    "Сегодня же ко всему приплетается чуть терпкий хинный вкус полыни-разочарования. Из-за деревьев слышна была музыка, под неё танцевали пионеры, что-то крича."
    "На аллеях уже видны были парочки, у колонки водопровода уже стояла очередь из нескольких мальчиков, наплясавшихся так, что рубашки пропотели насквозь."
    "В порядке живой очереди они ставили голову под поток ледяной воды, в восторге ухали и отскакивали."
    "Ноги почему-то вывели меня к складу."
    scene bg ext_warehouse_night_7dl with dissolve
    if alt_day1_el_followed == 1:
        "Губы сами сложились в улыбку, когда я вспомнил, как мы с Электроником прятались здесь от гнева Двачевской."
    "Я прогонял в памяти всё, что нас связывало, всё, что мы пережили и могли бы вспомнить вместе."
    "А ночь между тем вступила в полные права, над головой распахнулось бездонное чёрное небо."
    "Над головой стояли звёзды, сегодня какие-то особенно блеклые и невыразительные…"
    "…а я вдруг понял, что меня, в сущности, готовы отпустить."
    "Видимо, не оправдал чьих-то надежд, видимо, я и правда безнадёжен."
    "Мне стоит лишь внятно выразить просьбу — и я проснусь у себя дома, носом в клавиатуру."
    "По левую руку всходила луна, и я, обернувшись в её сторону, набрал полную грудь воздуха."
    "И заговорил:"
    scene bg ext_warehouse_blurry_7dl
    "Реальность вздрогнула, впитывая мои слова."
    me "Пожалуйста… Пожалуйста. Я очень хочу домой. Дайте мне уй…"
    window hide
    scene black
    $ prolog_time()
    with fade2
    if loki:
        show blink
        scene bg ext_winterpark_7dl 
        with dissolve
        $ renpy.pause(1.5)
        "За свою глупость я заплатил сполна."
        "Как жаль, что некому предъявить оплаченный счёт."
        "Холод забрался под кожу, и сил хватило только на то, чтобы тихо улыбнуться."
        "Я дурак. И я очень виноват."
        "Надеюсь, в следующей жизни у меня будет шанс всё исправить."
        window hide
        play sound aunl
        $ persistent.alt_deep = True
        show acm_logo_me_deep with moveinright:
            pos (1600, 1020)
        $ renpy.pause(4.4, hard=True)
        with vpunch
    elif herc:
        play music herc_death fadein 5
        scene black with fade
        "Просто однажды дверь в лето открылась и выпустила наружу того, кто оказался безнадежен даже по меркам страны снов."
        "И чем бы ни оказалось слепое пятно в моей голове — уверен, это всё равно лучше, чем несбывшаяся мечта."
        "Несбывшаяся рыжая мечта с огромными янтарными глазами."
        "Пора домой."
        scene bg int_store_7dl with fade
        "Ты принимаешь правила игры. Ты забываешь о том, кто ты есть и что ты такое."
        "Ради чего?"
        window hide
        play sound makarych fadein 0
        "Меня ждёт мой холодный город, стылые улицы и нахохленное безразличие бегущих куда-то прохожих-птиц."
        scene black with fade
        "Я не знаю другого. Ведь это мой дом."
        "А этому путешествию пора прекращаться."
        "Прекрасному путешествию."
        "Длиной в три дня лета."
        window hide
        play sound sfx_bodyfall_1
        stop sound_loop fadeout 0
        play sound aunl
        $ persistent.alt_deep = True
        show acm_logo_me_deep with moveinright:
            pos (1600, 1020)
        $ renpy.pause(4.4, hard=True)
        with vpunch
    else:
        scene bg int_intro_liaz_7dl with fade
        stop ambience fadeout 2
        "Всё это — просто морозный сон закемарившего над колесом неудачника."
        "Почти песня."
        "Почти примостившийся на стенке гусар."
        play sound_loop sfx_bus_interior_moving fadein 0
        $ renpy.pause(2)
        window hide
        scene black with fade
        $ volume(0.0, "music")
        stop sound_loop fadeout 0
        "Впереди уже можно было разглядеть громаду моста."
        "Пришла в голову мысль, что было бы любопытно — а что случилось бы, если бы автобус упал вниз?"
        "Не страшно, не жутко, не интересно. Просто вяло любопытно."
        window hide
        play sound aunl
        $ persistent.alt_deep = True
        show acm_logo_me_deep with moveinright:
            pos (1600, 1020)
        $ renpy.pause(4.4, hard=True)
        with vpunch
    window hide
    return

label alt_day3_leave1:
    "Я открыл глаза."
    $ persistent.d3_deep = True
    $ renpy.pause(1.0, hard=True)
    window hide
    scene black
    show gameover
    with gopr
    return

label alt_day3_dv_stayhere1:
    $ alt_day3_dv_evening = True
    if alt_day_binder != 1 and persistent.dv_7dl_good_ussr:
        play sound tousche
        play sound2 sfx_concert_applause
        show acm_logo_me_qte with moveinright:
            pos (1600, 1020)
        $ renpy.pause(9.4, hard=True)
    $ persistent.alt_qte = True
    $ night_time()
    with vpunch
    stop music
    scene bg ext_warehouse_blurry_7dl with flash
    $ volume(1.0, "music")
    stop music fadeout 4
    play music music_list["what_do_you_think_of_me"] fadein 3
    "Ледяные объятья неохотно отпустили меня."
    "Прямо сквозь рубашку на плечо капнуло чем-то горячим."
    window hide
    scene bg ext_warehouse_night_7dl with dissolve
    "Я не успел испугаться, и терпеливо ждал, что будет дальше."
    me "Кто-то решил об меня тормознуть?"
    "Лениво бросил я через плечо, а оттуда слышались лишь шмыганье носом и прерывистое дыхание."
    "Наконец я смог обернуться."
    show dv cry pioneer2 at center with dissolve
    "Алиса."
    "Плачущая Алиса."
    "Думаю, теперь она меня закопает как свидетеля собственного позора."
    show dv cry pioneer2 close with dissolve
    "Но вместо того, чтобы отпрянуть, она прижалась ещё сильнее."
    me "Что случилось?"
    "Нейтрально поинтересовался я. {w}И всё-таки голос немного запнулся, и эта запинка сказала ждущему уху куда больше, чем все на свете слова." 
    dv "Я н-не…"
    "Её колотило истерикой или чем-то в этом духе, я не слишком силён в гашении женских истерик, поэтому всё, что мне оставалось — это наметить некое подобие объятий. И ждать."
    "Только не очень крепких. А то опять подумает что."
    me "Что?"
    "Ровно спросил я."
    show dv guilty pioneer2 close with dissolve
    dv "Прости. Пожалуйста."
    "Тут она меня, конечно, удивила."
    me "За что?"
    me "Ты не сказала ничего, что шло бы вразрез с твоими убеждениями."
    me "А с собой надо в мире жить. {w}Это я точно знаю."
    dv "Неправда… Я хочу, но я боюсь."
    me "Боишься того, что можешь почувствовать что-то? {w}Ну так это пройденный этап — ты уже показала, что я у тебя ничего кроме раздражения не вызываю."
    show dv sad pioneer2 close with dspr
    dv "Это неправда."
    "Она замотала головой."
    dv "Ты сам не знаешь, что ты несёшь."
    me "Несу я ровно то, что мне сегодня либо сказали прямо, либо дали понять."
    me "Я ведь не совсем тормоз."
    "Я усмехнулся."
    me "Хотя, ты, конечно, считаешь иначе."
    dv "Я боюсь, что однажды проснусь, а тебя нет."
    "Кажется ей удалось успокоиться, во всяком случае, настолько, чтобы её отпустила трясучка."
    show dv guilty pioneer2 close with dissolve
    "Я позволил себе улыбнуться."
    me "Если бояться, что твои игрушки кто-то отнимет, рано или поздно это произойдёт."
    me "И раз уж ты успокоилась — пошли умоемся, а дальше думать будем."
    dv "Л-ладно."
    stop ambience fadeout 2
    "Она шмыгнула носом и в потёмках, пока никто нас не видел, взяла под руку."
    window hide
    scene cg d5_dv_island with dissolve
    play ambience ambience_forest_night fadein 3
    "Такого развития событий я не ожидал!"
    "После всех сегодняшних мальчишеств. После этой дурацкой беседы, из которой я понял только то, что ценнее в качестве союзника и товарища по играм, чем…"
    "И вдруг такие знаки внимания!"
    "Разумеется, я не заблуждался на собственный счёт — в конце концов, мне ведь однажды уже было семнадцать."
    "Привлекательность моя колеблется между нулем и отрицательными значениями."
    "Так что я чисто физически неспособен пробудить какие-то чувства."
    "Во всяком случае, в семнадцать лет точно не был способен."
    "Я попробовал освободить руку, но Алиса только усугубила всё, прижавшись ещё и всем телом."
    "Кровь бросилась в лицо, когда сквозь тонкую рубашку я ощутил жар её тела."
    "Не знаю, насколько ей было удобно идти, но у меня вдруг ноги будто деревянными стали."
    window hide
    scene bg ext_washstand_night_7dl
    with dissolve
    play music music_list["sweet_darkness"] fadein 3
    play ambience ambience_forest_night fadein 3
    us "Тили-тили-тесто, жених и невеста!" 
    "Донеслось от умывальников."
    show us laugh dress with dissolve
    "В темноте стояла невысокая фигурка, впрочем по двум хвостам-ракетам я её узнал."
    me "Ульянка, ты чего в потёмках бродишь?"
    "Не отреагировал на дразнилку я."
    "Алиса тоже промолчала, кажется, доверив мне бразды правления."
    us "Хотела убедиться, что вы и в самом деле тили-тесто."
    show us normal dress with dissolve
    us "Зачем вам это надо, я не понимаю, вы друг друга терпеть не можете. Но, кажется, друг без друга вам ещё хуже."
    hide us with dissolve
    play sound sfx_open_water_sink
    play sound_loop sfx_water_sink_stream fadein 1
    "Она помахала рукой и скрылась в темноте, давая мне возможность умыть зарёванную Двачевскую."
    "Вода была ледяная, как и всегда, а я тоже бросил пару горстей воды в лицо, чтобы немного остудить пылающие щёки."
    "После того, что учудила Алиса…"
    "…Ульянка нас благословила, ну надо же!"
    stop sound_loop
    play sound sfx_close_water_sink
    show dv normal pioneer with dissolve
    dv "Я всё."
    "Её ещё немного колотило после сценки у склада, плюс холодная вода. А мне и согреть её было нечем…"
    "Во всяком случае, она расправила рубашку в бессильной попытке согреться."
    me "Хорошо."
    "Я нашёл её руку."
    me "Пошли тогда."
    "Я потянул её за собой, уводя к домикам."
    scene bg ext_houses_night_7dl with dissolve
    show dv surprise pioneer with dissolve
    dv "Что? Куда?"
    me "Для начала — ко мне в домик."
    show dv shy pioneer with dissolve
    dv "Что, прямо так сразу?"
    "Я пожал плечами."
    me "А чего тянуть? Лично мне там удобнее всего."
    dv "Ну… Не знаю… А если Ольга…"
    "Она смутилась."
    me "Да что она, не понимает, что ли. Дело-то молодое."
    "Я понизил голос."
    me "Да не бойся ты. Мы быстро. Раз — и готово!"
    show dv shocked pioneer with dissolve
    dv "…"
    me "Ну да! Зная тебя, у нас всё быстро получится."
    "Кажется, я смутил её достаточно для того, чтобы чувствовать себя отомщённым."
    me "И вообще, на сон грядущий делать такие вещи доктора не рекомендуют. Говорят, вредно."
    me "Но ты же с ужина ничего не ела. Пошли!"
    show dv shy pioneer with dissolve
    "То, как она вспыхнула, было заметно даже при свете луны."
    th "Какая прелесть."
    dreamgirl "Поздравляю, мальчик, ты только что сдал вступительный экзамен в школу доктора Виолетты."
    dv "Ну… Я у Ульянки немного конфет свистнула." 
    "Наконец справившись с собой, призналась она."
    window hide
    scene bg ext_house_of_mt_night with dissolve
    play sound sfx_open_dooor_campus_1
    pause(4)
    stop ambience fadeout 1
    scene bg int_house_of_mt_noitem_night
    $ renpy.pause (1)
    play ambience ambience_int_cabin_night fadein 1
    if alt_day1_dv_feed:
        me "Ну, а я тебе ужин приготовил, скромно, но что есть."
    "Я распахнул перед ней дверь, пропуская внутрь."
    if alt_day1_dv_feed:
        me "Булочки и кефир. Уж что нашлось."
    show dv smile pioneer far at center with dissolve
    dv "Теперь я тебе ещё больше должна."
    "Улыбнулась она, без обычного своего высокомерия — почти как тогда, на пляже."
    "Но уговаривать себя не заставила и подмела всё подчистую, уплетая с таким аппетитом, что и мне кусочек захотелось."
    "Не досталось — зубы клацнули в опасной близости от вытянутой руки" with vpunch
    extend ", и я поспешил спрятать конечность."
    "Я улыбнулся и устроился на кровати, наблюдая за тем, как девочка ест."
    "Ещё пять минут напряжённой работы челюстями — и она сыто отвалилась от стола."
    me "Вот и чудненько. А теперь поговорим."
    play music music_list["tried_to_bring_it_back"] fadein 3
    show dv grin pioneer at center with dissolve
    dv "О чём?"
    "Она подняла глаза, чувствуя себя не слишком уютно тет-а-тет с полузнакомым парнем в домике."
    me "Да-да, именно о том, о чём ты подумала. {w}О нас с тобой. {w}Как говорят американцы, о гёрлфрендах и бойфрендах."
    show dv shy pioneer with dissolve
    "Она покраснела, похоже, понимая меня несколько превратно, на что я поспешил заметить:"
    me "Не совсем об этом, хотя ход твоих мыслей мне нравится."
    dv "А что тогда…"
    "Она окончательно смутилась."
    me "Я буду чувствовать себя идиотом, но это, похоже, нужно для дела."
    "Я подполз к ней поближе и, взяв за руку, заглянул в глаза."
    me "Обычно это должно получаться спонтанно, но… Двачевская, что ты думаешь о том, чтобы стать моей девушкой?"
    show dv sad pioneer with dissolve
    "Она смутилась ещё сильнее."
    dv "Я… Не умею."
    me "Не умеешь? {w}Я тоже не умею. {w}Как-то не приходилось ещё ни разу быть чьей-то девушкой."
    dreamgirl "Кретин, что ты несёшь?!"
    th "Я решил, что немного комического контраста не повредит."
    dv "Да нет, я говорю, я раньше ни с кем не встречалась ещё."
    me "Я понял. {w=.2}Ну, мы все учились понемногу, чему-нибудь и как-нибудь."
    dv "Так а что делать-то надо?"
    me "Для начала перестать смущаться. А дальше…"
    "Я помолчал, думая, куда на самом деле дальше-то…"
    me "Наслаждаться жизнью и компанией друг друга. По мере сил."
    stop music fadeout 4
    "Наконец нашёлся я."
    play music music_list["farewell_to_the_past_edit"] fadein 5
    me "Пойдём прогуляемся, ночь какая хорошая."
    hide dv with dissolve2
    "Она не ответила."
    me "Алис?"
    "Я обернулся в её сторону. Мда…"
    "Похоже, я настолько скучный, что все существа женского пола в обозримом пространстве при моём приближении испытывают непреодолимое желание спать — начиная с Ольги,"
    if alt_day3_duty:
        extend " заканчивая Ульянкой и Алисой."
    else:
        extend " заканчивая Алисой."
    "Напереживавшись на год вперёд, молодой организм прибёг к самому простому способу восстановления после стресса — крепкому сну."
    "Улыбнувшись, я поднял её на руки и перенёс на кровать, а сам вышел на улицу, где и облюбовал себе один из шезлонгов, поворочался, устраиваясь поудобнее, и закрыл глаза."
    scene black with fade2
    "Через некоторое время ткань соседнего шезлонга скрипнула, принимая в свои объятья ещё кого-то, женский голос спросил, почему-то шёпотом:"
    $ meet('un','Голос')
    un "Спит?"
    me "Ага. Закономерное завершение истерики."
    un "Вы с ней не…"
    me "Да нет, не сильно ругались."
    "Я сделал вид, что не понял намёка."
    me "Просто умыл, накормил, а уснула она сама."
    un "Надо же…"
    "Голос казался удивлённым."
    un "На моей памяти ты первый, кто сумел приручить Двачевскую."
    me "Достаточно было просто перестать врать себе."
    un "Мне бы этому научиться…"
    window hide
    scene bg ext_house_of_mt_night_without_light with dissolve
    stop ambience fadeout 1
    show un_shade with dissolve:
        pos (931, 0)
    "Шезлонг опять заскрипел, на фоне звёздного неба силуэтом мелькнули четыре хвостика…"
    show un smile dress close with dissolve:
        pos (931, 0)
    hide un_shade with dissolve
    me "Лена?!"
    "Ахнул я."
    $ meet('un','Лена')
    un "А кого ты ожидал,"
    "Лукаво спросила она."
    un "уж не Ольгу ли Дмитриевну?"
    me "Может, и её. {w}А что ты тут делаешь?" 
    un "Медсестра попросила помочь в медпункте, раз уж я всё равно не танцую."
    un "Только что освободилась. Но если нужно валерьяночки…"
    me "Нет, спасибо."
    "Я поймал себя на мысли, что мне удивительно легко общаться с этой девочкой."
    "Будто не было нескольких дней взаимных смущений."
    "А ей, кажется, вдвое легче оттого, что лица прячет темнота."
    show un smile3 dress close with dissolve
    un "Береги её. Она сама не знает, какое она счастье. Не обижай."
    me "А как же…"
    if (herc or loki):
        un "Наша беседа перед турниром? Это наши внутренние отношения такие."
        show un laugh dress close with dissolve
        "Она рассмеялась."
        un "Выглядит шокирующе, понимаю."
    else:
        un "Что, смутила её манера поведения? Не обращай внимания."
        show un smile2 dress with dspr
        un "Это не она настоящая, я-то знаю."
    me "Да нет, я о другом. О её мании — она боится, что ты хочешь отобрать у неё все игрушки."
    "Я запнулся."
    me "В том числе…"
    show un smile dress close with dissolve
    un "Тебя?"
    "Она сидела близко-близко, я мог разглядеть каждую волосинку в её хвостиках, отблеск лунного света в глазах… Взгляд невольно притягивало к губам."
    un "И подраться с ней потом? {w}Ну она скажет, конечно. Ты — это только её."
    un "Это обоюдно, поэтому я даже лезть не стану. Спокойной ночи."
    hide un with dissolve
    "Как и Ульянка, она растаяла в темноте."
    show blinking with dspr
    "Я опять закрыл глаза и, кажется, уснул, потому что перехода между отключением сознания и началом неласковой такой тряски за плечо не ощутил."
    show mt smile dress with dissolve
    "Это была Ольга Дмитриевна, и она всеми силами пыталась выглядеть сердитой."
    "Что, впрочем, с довольным выражением лица сочеталось не очень. Платье на ней было — закачаешься"
    show mt normal dress at center with dissolve
    extend ", и я поневоле проследил за всеми изгибами и выпуклостями прежде, чем понял, что она мне что-то говорит."
    mt "…филоним коллективные мероприятия, да?"
    mt "Сидим непонятно где, занимаемся непонятно чем. Кстати, почему именно здесь?"
    me "Там Алиса."
    "Односложно ответил я."
    mt "И она…"
    me "Спит."
    show mt normal dress at center  with dissolve
    "Не знаю, какая мысль пришла в голову вожатой, но она вдруг смутилась и расспросы на время прекратила, поднялась на крыльцо, приоткрыла дверь и задумчиво посмотрела на спящую в моей кровати девочку."
    mt "Я не буду спрашивать о том, как она здесь оказалась."
    mt "Но придётся тебе сегодня ночевать на незастеленной кровати."
    hide mt with dissolve
    $ renpy.pause(1)
    show mt normal dress at center with dissolve2
    "Она снова сходила в домик и принесла оттуда связку ключей, один в один с той, что посеяла Славя, когда кормила меня."
    mt "На следующей «улице» обрати внимание на «бочку» с эмблемой возле двери, она ещё заметно так от земли оторвана."
    "Она сделала строгий вид."
    mt "Надеюсь, в первый и последний раз."
    me "Я тоже."
    "Вздохнул я."
    me "Мне нравится спать в своей кровати."
    show mt smile dress with dissolve
    "Ольга вдруг хихикнула:"
    mt "Представляю завтра её лицо с утра. Обязательно разбужу её лично."
    me "Вы страшный человек, Ольга Дмитриевна. {w}Так ведь и заикой можно стать!"
    mt "Ничего, меньше терять кавалеров будет."
    show mt smile dress with dissolve
    "Я покраснел."
    me "Да мы не…"
    mt "Да я знаю, чего вы не…"
    mt "Про то, как Сёма с Алиской обжимались у рукомойников, половина лагеря уже судачит. Сплетники."
    "Она сплюнула это слово как ругательное."
    mt "А я думаю, что главное, чтобы всё честно было."
    "Она многозначительно покосилась в мою сторону."
    mt "И по любви."
    "Я же только вздохнул, и, поднявшись, направился на своё временное место ночёвки."
    hide mt with dissolve
    window hide
    $ persistent.sprite_time = "night"
    scene anim_square_party with dissolve
    play ambience ambience_camp_center_night fadein 2
    sl "Семён! Семён! Погоди."
    show sl normal dress with dissolve
    "Я обернулся. {w}Славя, в руках какой-то куль."
    th "Очередная помощь страждущим?"
    me "Чего тебе?"
    sl "Да вот, услышала, что ты временно переезжаешь. Так я тебе подушку пока дам, а завтра уже всё нормально со склада выдам."
    sl "Ну и мыло с полотенцем."
    show sl smile dress with dissolve
    sl "Кстати сказать, сегодня баню растопили, так что если есть желание нормально помыться, могу занять очередь."
    "Прикинув, сколько времени я уже нормально не мылся, я согласно кивнул."
    me "Конечно, занимай. Спасибо."
    show sl smile dress with dspr
    sl "Хорошо."
    "Она улыбнулась и махнула рукой в сторону лесочка."
    sl "По тропинке иди мимо рощи, как раз и выйдешь. Не промахнёшься."
    "Она улыбнулась и, вручив мне подушку с банными принадлежностями, ушла."
    hide sl with dissolve
    window hide
    return
    
label alt_day3_dance_dance:
    scene anim_square_preparty with dissolve
    play ambience ambience_camp_center_evening fadeout 2
    play music lonesome_shepherd fadein 7

    "За время, пока мы набивали желудки, вожатая очень неплохо постаралась!" 
    "Не сказал бы, что площадь было не узнать, но в общем и целом выглядело всё это куда приличнее, чем я опасался."
    "Хотя к идее относительно танцев под открытым небом относился с известной долей скепсиса."
    "Я устроился на лавочке неподалёку от мурлыкающих «Пастуха» чёрных кубиков «Ломо» и насвистывал мелодию, когда ко мне подошли два оболтуса-кибенематика."
    show el normal pioneer at cleft
    show sh normal pioneer at cright
    with dissolve
    me "Поздорову."
    "Я пожал руки обоим теоретикам и вопросительно уставился на них."
    if alt_day3_technoquest_st1:
        "После всего добра, что я для них сделал, резонно было ожидать, что теперь они будут подходить ко мне по любому затруднению."
        th "Ну и ну! Кажется, меня считают второй Славей! Или вторым Славей? Чёртова диалектика."
    me "Чем могу?"
    el "Да мы пока сами не уверены."
    me "То есть?"
    sh "Да есть одно затруднение."
    me "И?"
    el "Мы не уверены, справимся ли сами, или нам потребуется твоя помощь."
    me "И вы хотите заручиться ей заранее?"
    th "Восхитительно."
    sh "В целом."
    "Шурик кивнул."
    show sh smile pioneer at cright with dspr
    me "Может, хотя бы в курс дела введёте?"
    "Они оба смущённо хихикнули."
    if alt_day3_technoquest_st1:
        el "Так ты в курсе. Про антенну."
        show el normal pioneer at cleft with dspr
    else:
        sh "Да радио хотим в лагере наладить. Оно незадолго до твоего приезда сломалось."
        show sh normal pioneer at cright with dissolve
    "Их смущение выглядело явно нездоровым. Такое ощущение, что они то ли оба высоты боятся — а работа на верхотуре неизбежна, если мы говорим о радио."
    "Либо…"
    me "А в чём загвоздка?"
    $ alt_day3_technoquest_st3 = 1
    show el upset pioneer at cleft with dspr
    show sh upset pioneer at cright with dspr
    el "Да тут, понимаешь…"
    "Глазки веснушчатого кибернетика забегали."
    me "Вы высоты боитесь."
    "Уверенно заключил я."
    show el serious pioneer at cleft with dspr
    el "И ничего мы не боимся, мы вообще!"
    if herc and alt_day3_technoquest_st1:
        me "Значит, сил не хватает затащить какую-нибудь железку наверх."
    "Они синхронно покачали головами."
    me "Что-то ещё?!"
    "Почему я не удивляюсь…"
    show sh serious pioneer at cright with dspr
    sh "Раньше радио играло в режиме воспроизведения."
    sh "А тут Ольга Дмитриевна сказала нам, что раз мы за технику отвечаем, то и объявления делать должны, и горн на нас, и программа музыки чтобы каждый день новая была."
    dreamgirl "Хы. Чувак, ничего не напоминает?"
    "Я призадумался."
    th "Фриланс? Или жадный начальник?"
    dreamgirl "Последнее. Не помнишь, как тебя на последнем месте хотели определить одновременно дизайнером, сисадмином и веб-мастером… И это не считая основной сферы деятельности."
    dreamgirl "А-ха-ха! Вот они, истоки вечного русского головотяпства у власть предержащих."
    me "Вы должны были отказаться. Диск-жокей — это серьёзная профессия, требующая полной отдачи."
    el "Во!{w} Может, тогда ты ей и растолкуешь?{w} А нам ещё стенгазету рисовать надо!"
    me "Ну да, посредником мне ещё выступать не приходилось. Фигушки, ребята. Ни за какие коврижки."
    sh "Да как так-то."
    "Шурик выглядел немного обескураженным моим отказом."
    show sh serious pioneer at cright with dspr
    sh "Значит, ты нам не поможешь?"
    $ volume(0.8, "music")
    if alt_day3_un_med_help == 1:
        "Я вспомнил о том, что мы с Леной вообще-то обещали в медпункте поработать."
        me "Не, ребят. Извините. В другой раз."
        "Они ещё немного помялись рядом, пока до них не дошло, что решения своего я не изменю."
        hide el
        hide sh
        with dissolve
    elif alt_day3_dv2_event:
        me "Нет, ребят, извините, у меня ещё дела."
        "Если я не явлюсь на мини-концерт Алисы, она вполне может обидеться."
        "Чего очень не хотелось бы."
        "Они недоумевающе посмотрели на меня, но я уже исчерпал лимит объяснений и просто ждал, когда они уйдут наконец." 
        "Наконец до них дошло, что я всё сказал, и они, помявшись, ушли."
        hide el
        hide sh
        with dissolve
    else:
        me "Я этого не говорил. Но я не собираюсь выступать вашим адвокатом или кем-то ещё."
        me "Я могу помочь с поисками радиоведущего или по технической части."
        me "Но только что-то одно."
        me "Интересует?"
        show sh normal pioneer at cright with dissolve
        if alt_day3_dv_dj or alt_day3_mi_dj:
            sh "Конечно!"
            show el normal pioneer at cleft with dspr
            show sh normal pioneer at cright
            with dissolve
            me "Вот и договорились. Я поищу вам диск-жокея, а вы пока ковыряйте аппаратуру."
            me "Да не забудьте место человеку отрядить! Надо же ему где-то работать!"
            if alt_day3_technoquest_st1 or alt_day3_technoquest_st2:
                el "Так ты помог сегодня каморку разобрать, туда и определим!"
            else:
                el "У нас, кажется, было свободное помещение."
                el "Только оттуда надо всё выбросить."
                "Он посмотрел на меня."
                el "Поможешь?"
                "Я пожал плечами."
                me "Сейчас?"
                el "Желательно бы!"
                me "А ведущего искать Шурика отрядим?"
                "С намёком спросил я, и от меня мгновенно отстали."
            el "Звукоизоляция там хорошая, в окно никто ломиться не будет."
            "Они быстро распрощались со мной и отправились по своим, несомненно, важным кибернетическим делам."
            hide el
            hide sh
            with dissolve
            "Я даже не стал спрашивать их, пойдут ли они танцевать — такие одиозные личности считают преступлением впустую тратить время, лупя каблуками доски."
            "Ну а на мне ответственное партийное поручение."
            "Вербовка кадра на нужды местной узковещательной сети."
            if alt_day3_mi_dj:
                "Честно сказать, подходить второй раз за день с одной и той же просьбой, было, мягко говоря, не слишком скромно."
                "Однако раз уж дал обещание…"
                "И я замахал проходящей мимо Мику:"
                show mi normal pioneer at center with dissolve
                me "Мику, погоди! Мику! Разговор есть!"
                "Она остановилась и вопросительно посмотрела на меня."
                mi "Да, Сёмочка. Ты насчёт костюма?"
                if alt_day3_mi_date:
                    me "Нет, вообще я тебя увидеть хотел."
                    me "Костюм-то никуда не убежит, а вот на тебя когда ещё налюбуешься."
                    show mi shy pioneer at center with dissolve
                    mi "Ну вот и где тут устоять несчастной девушке."
                    me "Но ведь это правда!"
                    mi "Ладно, говори, чего хотел?"
                    "До сей поры я отказывал Мику в прозорливости, поэтому ей удалось меня удивить."
                    "Я старательно набрал в грудь воздуха и секунд десять пытался сформулировать просьбу."
                else:
                    me "Костюма? Какого костюма?"
                    show mi laugh pioneer at center with dspr
                    mi "Для танцев, конечно же. Ты же не хочешь пойти в этом!"
                    "Я оглядел себя. Вроде, прилично выгляжу. Белый верх, чёрный низ. Правда, низ до колена, но это ничего."
                    me "Нет, я за другим."
                    mi "Может, всё же попробуем тебя одеть?"
                    me "Не надо меня одевать."
                    "Еле вклинился я в поток сознания."
                me "Я хотел предложить тебе постоянный пост диджея."
                mi "Серьёзно? Ой, как интересно."
                me "Ну, ты в ходе танцев посмотри, понравится ли тебе вся эта кухня, и если понравится — тогда определим  тебя радиоведущей в местную станцию."
                show mi serious pioneer with dissolve
                mi "А как же мой клуб?"
                "Я деликатно умолчал о том, что её клуб с трудом насчитывает одного человека, и продолжил уговаривать:"
                me "Так у тебя никто его не забирает! Если захочешь сыграть чего-нибудь — главное, выключи микрофон, чтобы неотрепетированные вещи в эфир не попали. И играй!"
                mi "Ну, ладно! Я подумаю, посмотрю как мне понравится сегодня на танцах."
                me "Спасибо!"
                "Я от души поблагодарил девочку."
                me "Ты просто чудо!"
                "Она улыбнулась мне и убежала. Видимо, по своим музыкальным делам."
                hide mi with dissolve
                "А вслед за ней поднялся и я."
                "Меня ждали дела."
                window hide
                scene bg ext_washstand2_day with dissolve
                pause(4)
                play sound sfx_open_water_sink
            
                "Забежав по дороге к умывальникам, я несколько минут ухал, подставляя тело ледяной воде, но по итогам удовлетворённо констатировал, что теперь хотя бы пахну прилично."
                "Вытираться было нечем — пришлось употребить рубашку несколько не по назначению."
                play sound sfx_close_water_sink fadein 6
                "Поэтому мокрая ткань дарила просто незабываемые ощущения, прилипая к телу."
                "Вынуждая ускоряться как только ноги позволяли!"
                window hide
                scene bg ext_house_of_mt_sunset with dissolve
                play sound sfx_open_dooor_campus_1
                window hide
                return
            if alt_day3_dv_dj:
                stop music fadeout 3
                window hide
                $ persistent.sprite_time = "sunset"
                scene bg ext_house_of_dv_day
                with dissolve
                "Поиски Алисы я решил начать опять с её домика, и не сильно прогадал."
                "В этот раз никто не мучал гитару, заставляя плакать несчастный инструмент."
                "Зато в домике горел свет и оттуда слышались голоса на повышенных тонах."
                "Я постучался и, дождавшись некоего невразумительного «бу-бу-бу», открыл дверь."
                window hide
                play sound sfx_close_door_campus_1
                scene bg int_house_of_dv_day with dissolve
                "Удивительно как преображает человека даже малая толика признания."
                "Куда только делись покатые плечи, ссутуленная спина, безучастное всеприятие в глазах вперемешку с высокомерным прищуром."
                "Ничего этого не было. Двачевская деятельно сидела на полу, обнимая ногами огромный квадратный короб с пластинками — готовила концертную программу, не иначе."
                "Периодически она вынимала одну пластинку из кейса и, вытряхнув винил из бумажного чехла, протягивала Ульяне, и та заводила проигрыватель."
                "Большая часть музыкальной коллекции уже была отложена — судя по небрежной стопке, в забракованное."
                "Напротив, кучка одобренной музыки была не столь велика и, по самым оптимистичным прикидкам, неспособна была обеспечить музыкой мероприятие целиком."
                show dv normal pioneer at cleft with dissolve
                show us smile dress at cright with dissolve
                dv "Это ты, заходи."
                "Она отрывисто кивнула мне и вернулась к своему увлекательному занятию."
                us "Ты что с моей подругой сделал, гад? Она теперь не балуется, не бесится — сидит и слушает музыку."
                "Она подозрительно сощурилась."
                us "Ты ей свою дуру байтную подарил, да?"
                show dv laugh pioneer at cleft with dspr
                me "Вот ещё. Да и не станет она такое слушать."
                dv "Ну, кое-что послушала бы. Например, про то, как лампа не горит. Хочется табулатуру подобрать."
                dv "Сможешь подключить свою игрушку к проигрывателю? Там пятипинки есть на вход."
                me "Почему бы и да."
                "Я добыл из кармана миниджек, и, немного помыкавшись, заставил проигрыватель играть Сплина."
                dv "Добротная вещь!"
                "Одобрительно кивнула Алиса."
                dv "Что за группа и как песня называется?"
                me "Песня называется «Романс», а вот исполнителя я забыл."
                "Не хватало ещё ляпнуть что-нибудь вроде «Он в вашем времени ещё не существует»."
                show dv grin pioneer at cleft with dspr
                "А Алиса так задумчиво-задумчиво на меня глядела… Я бы поставил на то, что я ей нравлюсь — если бы не был уверен в обратном!"
                "Двачевской может нравиться только насилие!"
                
                me "Алис, ты, это."
                me "Если будешь и дальше на меня так таращиться, я решу, что опять где-то накосячил."
                dv "Да я вот думаю, что будет, если я всё это барахло — "
                "Она щедрой рукой обвела виниловые залежи."
                dv "Брошу здесь, а танцы проведу с помощью твоей игрушки."
                "Прикинув перспективы профукать остатки зарядки плеера, я нешуточно испугался."
                me "Не получится. Заряда мало осталось. А заряжать негде."
                show dv smile pioneer at cleft with dissolve
                dv "Так мы кибернетиков привлечём! Пора бы им уже отрабатывать содержание."
                me "Боюсь, для кибернетиков техника слишком сложная. Это, блин, не самопальные усилки в купоросе травить."
                dv "То есть, ты откажешь девушке?"
                "Её глаза смеялись."
                me "Прости, но откажу. Не та вещь, которую я так легко могу дать."
                "И тут мне в голову пришла мысль."
                me "Впрочем… Мы можем заключить сделку."
                show dv normal pioneer at cleft with dissolve
                dv "Ну-ка! Какую сделку?"
                me "Я дам тебе плеер и даже объясню как с ним обращаться — и мы выберем несколько треков оттуда. Но только несколько! Дискотеку целиком заменять не будем."
                dv "Так, а взамен?"
                me "А взамен ты проведёшь один день на новом радио в качестве ведущего!"
                show dv shocked pioneer at cleft with dspr
                dv "Радио? Но это же на весь лагерь…"
                me "Ага. Почти то же самое, что и сегодня, только там придётся весь день так сидеть. Попробуешь?"
                dv "Целый день — это очень много."
                me "Короче, я не давлю. Сегодня вечерком посмотришь, как у тебя получается, и если понравится — будешь Элис Диджей."
                show dv normal pioneer at cleft with dissolve
                show us smile dress at cright with dissolve
                us "Звучит похоже на имя этой идиотки из книжки у тебя под…"
                show dv normal pioneer at center with moveinright
                "Ульянка не договорила — почему-то всполошившаяся Алиса мгновенно рванулась к подруге и зажала той рот!"
                dv "Молчать."
                "Я ничего не понял, наверное, это их какие-то внутренние разборки. Не знаю. Не моё дело."
                me "Так что, Алис? Мы договоримся?"
                "Она задумалась, так и не выпуская бешено вращающую глазами Ульяну."
                me "Подругу не выпустишь? Задохнётся."
                dv "Эта-то? Не… Её как таракана, и ломом не перешибёшь. Носом дышит."
                "Похоже, что её не слишком-то грела перспектива оказаться весь день взаперти накоротке с кибенематиками, но желание послушать ещё чего-нибудь эдакого победило."
                dv "Годится. Давай сюда свою игрушку."
                me "Не так быстро. Сначала инструктаж. Отпусти уже Ульяну, она не будет твои секреты разбалтывать."
                "Ульяна быстро-быстро закивала, а Алиса, недоверчиво прищурившись, всё же отпустила подругу."
                dv "Ладно. Так что куда тут нажимать надо?"
                "Я вздохнул и вывел на экранчик список песен."
                "Электронщина, саундтреки и лаунж отпадали автоматически — под это никто здесь танцевать не сможет."
                "Осталось минимум — пара дисков романтической коллекции, на случай, если вдруг стрельнёт послушать Морриконе или Ласта."
                "Собственно, оттуда и была основная выборка."
                "Слушатели попались благодарные и первоначально собирались определить в ротацию чуть ли не 100%% прослушиваемого материала, но я немного остудил их пыл, напомнив о соглашении и причинах, почему я такой жадный."
                show blinking
                "Короче говоря, спустя полчаса мы закончили составление танцевальной программы и выдвинулись в сторону площади, где уже ожидала аппаратура."
                "Ведущую на сегодняшний вечер и завтрашний день я обеспечил. Задание выполнено! Я заработал кучу опыта и теперь точно подниму уровень."
                hide dv
                hide us
                with dissolve
                window hide
            play music music_list["lightness"] fadein 5
            scene anim_square_party with dissolve
            voices "Танцы! Танцы!"
            "Скандировала толпа."
            show mt smile dress at center with dissolve
            "Ольга Дмитриевна взяла в руки микрофон и начала вечер:"
            mt "Добрый вечер, дорогие друзья!"
            mt "Сегодня вторник, а значит, вечер танцев."
            mt "И сегодня мы приготовили для вас нечто совершенно особенное."
            mt "Я хочу представить вам человека, под чью музыку вы сегодня будете танцевать."
            "Она простёрла руку в сторону пульта."
            if alt_day3_mi_dj: # кому она кивнула? Семён уже марафет наводить ушёл
                mt "Мику!"
                hide mt with dissolve
                "Японка только кивнула. На ней было красивое аквамариновое платье в тон волосам, и огромные наушники смотрелись на ней довольно забавно."
                show mi serious dress with dspr
                "Похоже не я один не удержал улыбки от её занятного вида, потому что вожатая вдруг нахмурилась."
                "Но человека сцены такой ерундой как смешки в зале не проймёшь. Она подняла руку и, улыбнувшись нам, помахала."
                mi "Всем привет! Первой песней я хотела бы поставить что-нибудь лёгкое и доброе, такое же, как сегодняшний вечер."
                "Она убрала руку, сдерживающую пластинку, и по вечернему воздуху поплыли ноты вступления к «Свету» — той самой композиции из клуба."
                "Все закричали и захлопали в ладоши — видимо, песня была здесь уже привычной и любимой."
                "Придерживая одной рукой наушники, она крутнулась вокруг собственной оси, прогибаясь, и тут уже не выдержала Ульяна — она кинулась под самые колонки и замахала руками в такт."
                show us laugh dress at right with dissolve
                us "Танцуем, люди!"
                "Вечер начался!"
                hide mi
            elif alt_day3_dv_dj:
                mt "Алиса!"
                "Пройдя к вертушкам, Алиса деловито кивнула."
                "Судя по её лицу, внимание публики для неё было не сказать чтобы совсем в диковинку, но — не самым частым переживанием в жизни точно."
                "Хотя смутить её было сложнее, чем казалось — она уже вытряхнула из черного конверта с пирамидкой-призмой, расщепляющей луч света, первый виниловый диск, поставила, плавно подбавила громкости."
                "Как только «Флойды» ушли на полную громкость, она мгновенно остановила вторую вертушку, и кинулась колдовать с плеером — мы договорились третьим треком поставить что-нибудь из моей коллекции."
                "Ульянка не дождалась подруги, и кинулась плясать."
            return
        sh "Смысла от ведущего не будет, если ему нечего будет вести."
        sh "Мы подойдём к тебе чуть позже, ладно?"
        sh "Хотим, чтобы народ подтянулся на танцы и нам никто не мешал."
        "Я пожал плечами."
        me "Как скажете."
        el "Тогда до скорого!"
        hide el
        hide sh
        with dissolve
    "Я слышал целую кучу мнений о таких вот самостийных танцульках. Мол, и стрёмно, и дурно, и бывает, что все стоят, по стенам жмутся."
    "Но мне всю жизнь везло на то, что либо я приходил тогда, когда уже кто-то выплясывал, набираясь инфразвука по самые ушки."
    "Либо я сам, набегавшись и напитавшись куража за день достаточно, с громкими криками кидался в открывающиеся двери и занимал своё любимое место неподалёку от кафедры-возвышения, где восседал ведущий со своими «Техниксами»."
    "Но если здесь будет играть то, что я думаю, дела мои совсем плохи."
    "То есть, я при желании и под «камаринскую» спляшу — от количества водки зависит."
    "Но под Юрия Антонова, например, мои конечности танцевать категорически отказываются!"
    "Наверное, воспитание такое.{w} Привык к тому, что музыку такого толка можно только слушать, а для танцев несколько другие направления."
    "В любом случае, до танцев есть ещё немного времени, которое вполне можно посвятить прогулке."
    $ persistent.sprite_time = "sunset"
    scene bg ext_house_of_mt_sunset with dissolve

    "Я добрался до домика и толкнул дверь."
    window hide
    play sound sfx_open_dooor_campus_1
    stop ambience fadeout 1
    return
    
label alt_day3_makeup:
    play music music_list["trapped_in_dreams"] fadein 3
    scene bg int_house_of_mt_sunset with fade
    $ renpy.pause (1)
    play ambience ambience_int_cabin_night fadein 1

    if alt_day3_dv2_event:
        "Помнится, мне Алиса обещала устроить концерт. Может, лучше к ней, чем тратить время на площади?"
    "Ох уж мне эти танцы."
    th "Вот опять, позориться будут они, а стрёмно будет мне."
    "Я отправился приводить себя в порядок."
    if not alt_day3_dv2_event:
        "Может, ну его?"
    if alt_day3_dv_date:
        "Я сгрузил припасы в домике и, встав перед зеркалом, критично оглядел себя."
    elif alt_day3_mi_invite2:
        "Мику повесила вешалку с костюмом на ручку двери, поэтому всё, что мне оставалось — это выглядеть понаряднее и чувствовать себя хорошо. Надеюсь, справлюсь."
        "Я переоделся и внимательно уставился на себя в зеркале."
    else:
        "Я подошёл к зеркалу и скептически уставился в глаза отражению."
    if alt_day3_mi_invite2:
        scene cg d3_me_mirror_bordo_7dl with dissolve
    else:
        scene cg d3_me_mirror_white_7dl with dissolve
    "Рост метр восемьдесят пять — неплохой."
    "Однако худоба и сутулость успешно скрадывают от пяти до десяти сантиметров, поэтому с той же ДваЧе, которая всего метр семьдесят пять, мы смотрим глаза в глаза."
    if herc:
        "Для подростка довольно широкие плечи — хотя по сравнению с тем, какими они стали в армии…"
    else:
        "Плечи узкие — фенотип, их расширению не способствовали даже турники. Однако одним летом мне довелось поработать на строительстве дома…"
    "Разумеется, сейчас ничего такого и в помине нет. Относительно нынешних лет, это сумрачное грядущее."
    "Подбородок узкий, безвольный. {w}Губы безвольные, чуть большая нижняя говорит о капризном и ветреном нраве. {w}Нос классический ровный."
    "Глаза… Их нет. Не запоминаются совершенно."
    "Я сделал два шага назад и сфокусировался на общей картинке, образе."
    "Вот это безликое нечто, не самой привлекательной внешности и харизмы, стремящейся к негативной — может ли оно внушить хоть какой-то интерес?"
    window hide
    with dissolve
    "Лично мне показалось, что не может. {w}Я себе не нравился, чётко и ярко выраженно.{w} Я бы, наверное, даже ненавидел себя — если бы это не требовало таких сил."
    if alt_day2_date == 132:
        "Ничего удивительно в том, что Лена не заинтересовалась эдаким персонажем, не было."
    window hide
    with dissolve
    me "Ты мне не нравишься, уродец. {w=.3}И это, безусловно, взаимно."
    play sound sfx_close_door_1
    "Сказал я отражению и, захлопнув дверцу, вышел на улицу."
    "Помнится, меня кое-куда приглашали."
    stop ambience fadeout 4
    if alt_day2_date == 132:
        window hide
        scene bg ext_house_of_mt_sunset with dissolve
        play ambience ambience_camp_center_evening fadein 6
    
        "Выйдя на улицу, я остановился в раздумьях."
        "На танцы меня абсолютно недвусмысленно приглашала Славя — хотя после дневных её откровений, относился я к ней с прохладцей."
        "И Мику — она очень хотела, чтобы я станцевал с ней."
        "Хотя всё это не так важно как то, что там будет…"
        "Будет там…"
        th "А может, действительно, ну его? Под лежачий камень, как говорится…"
        menu:
            "Идти на танцы":
                $ lp_un += 1
                $ alt_day3_un_fz_evening = True
                "Алиса Алисой, но я в своих желаниях разобрался."
                "Меня ждала вечерняя площадь, ВИА восьмидесятых и очень красивая зеленоглазка."
                "Я понятия не имел, как собираюсь танцевать подо всё это и собираюсь ли, но вечер уже начался, и пропускать его я не имел права."
                "Полон надежд, я направился в сторону площади, кивая в такт головой доносящимся басовым раскатам."
            "Идти на эстраду":
                $ lp_dv += 1
                "Я не знаю, сумею ли когда-нибудь добиться если не признания, то хотя бы спокойного ровного отношения со стороны Лены."
                "С другой стороны, с Двачевской всё понятно — мы соперники, партнёры по играм и просто люди, не стесняющиеся использовать друг друга."
                "Такая откровенность отношения несколько пугала, но, поразмыслив, я счёл её даже своего рода привлекательной."
                "А потому."
                "Двести метров по тропинке сквозь кусты — и я выберусь аккурат напротив сцены."
    window hide
    return
    
label alt_day3_choose:
    window hide
    scene anim_square_party with dissolve
    "Наконец вечер набрал обороты и диджей обрадовал присутствующих первой новинкой."
    "Что странно — медленной."
    menu:
        "Танцевать с Леной":
            call alt_day3_lp_checker(alt_dater = un)
            if alt_day2_sl_bf:
                $ lp_sl -= 1
            $ alt_day3_dancing = 1
            "Я не очень понимал, как можно танцевать под то, что крутит ведущий, но в чужой монастырь со своим уставом не лез."
            "Предпочитал забиться себе тихонечко в уголок и послушать музыку."
            "Тем более, что через полутёмную площадь, между сумерками, разгоняемыми единственно светом гирлянд и отблесками световой пушки, я периодически ловил на себе взгляд Лены."
            show un shy dress far with dspr
            "Не могу сказать, заинтересованный то был взгляд или он просто спотыкался на мне в рамках дежурного обшаривания окружения."
            "Но я чувствовал его на себе и от того что-то ёкало внутри."
            "Каким-то восторженным холодком не то предчувствия, не то нечаянной симпатии к красивой девочке с грустным взглядом."
            show un normal dress far with dspr
            th "Знала бы ты, милая, всю бронебойность твоего воздействия."
            "Я искоса поглядывал на неё, всеми силами избегая прямого контакта, и мне вся решимость моя потребовалась для того, чтобы сделать первый шаг по направлению к ней."
            "И вот, я шагаю — и её взгляд немедленно замирает на мне."
            show un serious dress with dspr
            "Шагаю дальше — и чувствую, как деревенеет спина и сутулятся плечи, как хрустит где-то в колене и неловко опускается стопа."
            "Как подбородок опускается слишком низко, и налетевший ветерок играет в не к месту встопорщившихся волосинках на макушке."
            "Мне уже очень давно не было так неловко, как во время этих десяти шагов."
            "А она сидела на скамеечке и с удовольствием слушала музыку, время от времени поглядывая на моё приближение."
            if alt_day2_date == 132:
                $ alt_day3_dancing = 132
                me "Извини пожалуйста. Можно пригласить тебя?"
                "С трудом вытолкнуло враз пересохшее горло."
                play music danceagain fadein 6
                show un serious dress with dspr
                "Она подняла на меня взгляд."
                un "Это снова ты."
                "Констатировала очевидное она."
                me "Я."
                un "Не хотела же идти…"
                "Прошептала она."
                me "Извини, я не слышу тебя."
                "Притворился глухим я."
                me "Так что, позволишь пригласить себя?"
                "Она уже начала отрицательное движение головой, а я умоляюще посмотрел на неё."
                th "Хватит уже и того, что ты меня сегодня целый день с грязью мешаешь. Может быть, я заслужил хотя бы чуть-чуть сладкого?"
                "Говорил мой взгляд."
                "«Надоел»."
                "Отвечал её."
                me "Пожалуйста. Всего один танец."
                "Девочка тяжело вздохнула."
                un "Всего один."
                "Она протянула мне ладонь, которую я с превеликим трепетом взял."
                "И всё время, что вёл её на танцпол, пытался понять, отчего чуть дрогнули её пальчики, когда я взял её за руку."
                "Было ли это от отвращения? Неприязни? Как бишь там — он готов положить весь мир к её ногам, но её вполне устроит просто его голова отдельно от тела."
                "Понимает ли она, что этот танец, возможно, последняя возможность стать кем-то ближе, чем просто знакомыми?"
                show un cry dress close at center with dissolve2
                "И вот, когда мы наконец вышли на танцпол, и она развернулась ко мне, я удивлённо попятился."
                "В её глазах стояли огромные слёзы."
                me "Лена? Что ты…"
                un "Нет."
                "Прошептала она."
                un "Я не могу."
                un "Прости."
                "Она оттолкнула меня в сторону и убежала, скрылась в неизвестном направлении."
                window hide
                scene anim_square_party with dissolve
                with vpunch
                me "Да твою мать!"
                "Рявкнул я."
                me "Что не так?!"
                "Проверил ещё раз — чистый, не пахну, одежда опрятная."
                th "Какого дьявола ей не по душе?!"
                "Ссутулившись, я прошаркал к скамейкам, вздрагивая и обжигаясь о взгляды — сочувствующие, любопытные, равнодушные, злорадствующие."
                "И хотелось поймать кого-нибудь за этот не в меру длинный взгляд, вытащить из толпы и дать по загривку как следует, наорать."
                "Да, свиньи праздные, мне только что отказала в танце девочка, которая значит преступно много!"
                "Усевшись, я со злостью саданул кулаком по безвинным доскам."
                th "Вот и потанцевали."
                "Горько усмехнулся я."
                th "Лучший танец в моей никчёмной жизни."
                "Опёршись локтями о колени, я сгорбился на своём месте, и до самого конца медляка не позволял себе двинуться с места."
                "В голове было пусто, не осталось там ни злости, ни обиды, ни моей любимой привычки жалеть себя."
                "А я поймал себя на мысли, что начинаю медленно, но верно сходить с ума."
                th "Пацан сказал — пацан сделал, верно? Обещал себе, что без Лены не продержусь в рядах вменяемых долго — получите, распишитесь."
                stop music fadeout 10
            else:
                me "Потанцуем?"
                "Я прищёлкнул воображаемыми каблуками, пряча за бесшабашной внешностью и неуверенность в себе, и боязнь весьма вероятного отказа."
                "То, что мы только что договорились о танце, дела не меняло совершенно."
                if alt_day3_un_invite == 2:
                    th "В конце концов, в бальную книжку меня так и не занесли."
                dreamgirl "Уймись, ретроград. И улыбнись девушке, а то решит, что ты её на похороны приглашаешь."
                play music music_list["silhouette_in_sunset"] fadein 3
                show un shy dress with dissolve
                "Наконец она решилась и, легонько кивнув, поднялась, держась за мою руку."
                "Я вёл её как ведут величайшее сокровище, бережно, трепетно, боясь разбить или повредить."
                "А она прошла в середину танцпола и, развернувшись, вопросительно посмотрела на меня."
                me "Что дальше? Дальше — танец!"
                "Руки сами ложатся как правильно, как нужно, и ей остаётся лишь принять правила и следовать им."
                "Первый шаг в такт, второй как получится."
                "Третий — вровень с дыханием."
                "А оно — всегда одно на двоих."
                "Она молчит, не столько смущаясь, сколько просто не зная — что говорить во время танца. {w} Что ж, похоже, что на мне сегодня вечер."
                me "У тебя потрясающее платье."
                "Начал издалека я."
                "Тем более, что и хозяйка ничуть не хуже."
                un "Правда?"
                me "Конечно. Я никогда не вру."
                dreamgirl "Разве что самую чуточку."
                if loki:
                    me "Но это вовсе не значит, что ты здесь самая красивая!"
                    me "Я тоже ничего!"
                elif herc:
                    me "Вообще, мне очень повезло!"
                    un "Что?"
                    me "Я успел пригласить тебя прежде, чем это успел сделать кто-то другой."
                show un smile dress with dissolve
                "Она несмело улыбнулась и кивнула, чуть-чуть расслабившись."
                "Во всяком случае, спина её под пальцами уже не такая окостеневшая."
                "Я приблизился на расстояние дыхания и доверительно шепнул:"
                me "Я тебе секрет открою. Вообще я не очень хорошо танцую."
                me "Но, похоже, что ты вдохновляешь меня забыть о том, чего я не умею."
                "Она тихо улыбнулась, не поднимая глаз."
                me "И танцы не очень жалую."
                me "Но сейчас мне почему-то очень нравится."
                me "Не хочешь повторить наш танцевальный опыт ещё раз?"
                if  alt_day3_un_med_help == 1:
                    un "Но мы же… В медпункт, помнишь?"
                    me "Подумаешь. Время-то ещё есть."
                    "Мне неловко напоминать ей о сценке на дворе склада, но она, кажется, поняла."
                    un "Если в этом  смысле… Я согласна."
                else:
                    un "Прости, но нет. Надо будет уйти."
                    "Я не скрывал огорчения."
                    me "А если подумать? Может, всё же есть возможность?"
                    if loki or herc:
                        un "Ну…"
                        "Она явно колебалась."
                        un "Разве что только ещё один танец."
                    else:
                       un "Прости. Нет."
                "Остальной танец прошёл во многозначительном молчании."
                "Том самом, которое возможно только между теми двумя, кому есть о чём молчать."
                "Музыка закончилась, и мы неохотно отпустили руки."
        "Танцевать с Мику":
            $ alt_day3_dancing = 4
            call alt_day3_lp_checker(alt_dater = mi)
            if alt_day2_sl_bf:
                $ lp_sl -= 1
            play music music_list["lightness"] fadein 5
            if alt_day3_mi_dj:
                "Полностью вжившись в роль диск-жокея, Мику стояла у вертушек, следя за звучанием, кое-где создавая даже некое подобие переходов и наложений, будто выступая предвестником будущих школ диск-жокеев."
                "Она смотрелась так замечательно-потешно — маленькая, хрупкая девочка с красивыми волосами, собранными в два длинных хвоста, и огромными наушниками."
                "И для меня вдруг стало очевидно — кого и что бы я ни искал раньше — я нашёл это здесь. Вот оно, стоит и подгоняет по скорости очередной шлягер к уже звучащему."
                me "Привет, красота! Танцуешь?"
                show mi normal dress with dissolve
                mi "Пытаюсь!"
                "Не снимая наушников прокричала она."
                "Я указал пальцами на наушники, и она, ойкнув, тут же стащила их с головы."
                show mi smile dress with dissolve
                mi "Ты чего-то хотел, Сенечка?"
                me "Хотел пригласить тебя танцевать."
                "Я уже смирился со всеми на свете «Сёмами» и «Сенечками» — лишь бы только она улыбалась."
                "Она расцвела."
                mi "Конечно, пошли!"
                "Она взяла меня под руку, и я вывел её на танцпол."
            else:
                "Хореография — даже дискотечная — в Японии всё-таки здорово отличается от того, к чему привыкли мы, на среднерусских просторах."
                "Будучи известными индивидуалистами, превыше всего ценящими зону комфорта, они даже танцуют так, что занимают больше места, чем, скажем, их коллега-танцор из Союза."
                "И уж точно они не станут делать так, как делают эти варвары-русские: вставать в кружок и, подбадривая друг друга задорным взглядом да вычурным коленцем, смотреть, кто во что горазд."
                "Кто горазд — тот пойдёт в круг, а кто не очень — чему научится."
                "Танцы Мику больше походили на некое подобие уличного балета — с определённой скидкой на местный колорит. Разобщённая работа рук и ног, много-много прыжков…"
                "В общем, мало удивительного было в том, что когда очередной голос заткнулся орать про высокие берега, и по бетонке поплыли басовитые нотки, воспевающие свет — она осталась одна."
                "Впрочем, недолго — я тут же подскочил к ней и резко севшим голосом предложил:"
                me "Потанцуем, красивая?"
                show mi normal dress with dissolve
                mi "Конечно!"
                "Она схватила меня за руку и потащила на танцпол."
            scene cg d3_mi_dance_close_7dl
            with dissolve
            me "Ты сегодня затмила всех — и танцами, и платьем."
            "Ничуть не покривил душой я."
            "Ведь так легко делать комплименты, когда они — чистая правда!"
            if alt_day3_un_invite == 1:
                "Хотя вряд ли этим комплиментам обрадовалась бы одна тихая девочка."
                "Но я предпочёл не думать об это."
            mi "Честно?"
            me "Честнее не бывает. Я как тебя увидел, сразу понял, с кем я сегодня танцую."
            "Она немного смущённо улыбнулась."
            if alt_day3_mi_invite:
                dreamgirl "Давай, а теперь спроси у неё, не хочет ли она после дискотеки сходить в клуб, и там в реквизиторской…"
                mi "Ты тоже выглядишь замечательно."
                me "Так твоими молитвами. Я ведь тебя так и не поблагодарил."
            else:
                mi "Жаль, ты не зашёл сегодня, так бы тебе тоже что-нибудь нарядное подобрали."
            if loki:
                me "Но это вовсе не значит, что ты здесь самая красивая!"
                me "Я тоже ничего!"
                "Она беззвучно расхохоталась."
            "Мы некоторое время танцевали, не говоря ни слова."
            "Лично я наслаждаюсь моментом — относительно неплохой музыкой, девочкой в руках…"
            mi "Сём…"
            "Прошептала она."
            mi "Я понимаю, что симпатична тебе и ценю оказываемое доверие — но чуть-чуть подальше, ладно?"
            if lp_mi >= 5:
                mi "А то я тебя уже изучила лучше, чем если бы голого видела."
                "Я расхохотался, даже и не думая отодвигаться. Хуже того — я её ещё и прижал к себе как следует."
                if alt_day2_date == 4:
                    me "Ну а что, после пляжа-то. Всё равно все вокруг уже всё подумали что хотели."
                    mi "То, что они подумали — их личное дело."
                mi "Меня твоя близость отвлекает! {w=.4}Чуть подальше. {w}Или на ногу тебе наступлю."
                "Какая страшная угроза."
                "Сделав испуганное лицо, я поспешно отступил на полшага."
            else:
                "Я мгновенно вспыхнул и, как мог, отодвинулся."
                me "Прости…"
                me "Я задумался…"
                mi "Ничего, мне даже понравилось. Но от танца отвлекает."
                "Она негромко хихикнула мне на ухо, и я понял, насколько своевременно отодвинулся."
            "Музыка закончилась и мы неохотно отпустили руки."
            scene anim_square_party
        "Танцевать со Славей":
            call alt_day3_lp_checker(alt_dater = sl)
            $ alt_day3_dancing = 2
            play music music_list["raindrops"] fadein 3
            show sl smile dress with dissolve
            "Славя напрыгалась от души во время всех этих странных мелодий, но для неё вечер не был бы достаточно томным без обязательного медленного танца."
            "Подозреваю, если бы я не успел первым, она объявила бы внеочередной «белый» танец."
            if alt_day3_un_invite == 1:
                "Хотя меня не отпускала мысль, что, возможно, этот танец стоило бы отдать кое-кому другому."
                "Но я предпочёл не думать об это."
            me "Потанцуем?"
            "Она ещё не восстановила дыхание после предыдущей пляски, поэтому казалось, что это она на меня так реагирует."
            "И оттого будто сами собой расправлялись плечи, и по спине вниз колоннами маршировали огромные мурашки."
            show sl smile2 dress with dissolve
            "На ней серо-голубое платьице, скорее, сарафанчик, до середины бедра с интересным орнаментом явно растительного происхождения."
            "Оно такое лёгкое с виду, что кажется прозрачным, и я поймал себя на мысли, что стараюсь посмотреть насквозь — а ну как что увижу?"
            me "Твоё платье — просто что-то!"
            "Признался я."
            me "Это хорошо, что ты в нём не каждый день ходишь."
            me "Иначе ни работать, ни отдыхать было бы невозможно. Только расслабишься — а тут ты, красивая."
            show sl laugh dress with dissolve
            if alt_day3_sl_day_event2:
                sl "Я обещала показать его — смотри!"
                "Улыбнулась девушка."
                sl "Но только, надеюсь, в этот раз ты никуда не убежишь и не бросишь девушку одну на танцполе."
                me "Туше. {w} Этот плохой ход слишком классический, слишком предсказуем."
                me "Другое дело, если бы убежала…"
                scene cg d3_sl_dance with dissolve
                sl "Даже и не мечтай!"
                "Она взяла мои руки в свои, и одну утвердила у себя на талии, а вторую схватила так, что нечего было и думать убежать."
                sl "Мы будем танцевать — я этого хочу!"
                th "И опять поди узнай, в каком смысле у неё эти желания работают."
                "В принципе, если хотя бы чуть-чуть знаешь человека, глаз уже привыкает к тому, как тот себя ведёт, какая у него мимика, какие реакции."
                "И любое проявление неестественности можно разглядеть — если быть достаточно наблюдательным."
                "Мимо. {w}Со Славей это просто не работает."
                "Она смотрела мне в глаза всё время танца."
                "И это было очень, очень нехорошо."
                "Потому что, в принципе, единственным способом низвести моё эго до уровня пола был такой вот взгляд."
                "То есть я вполне допускал, что есть люди, которым жизненно необходим прямой контакт глаз во время общения."
                "Но Славя до сих пор не показывала себя таким человеком."
                th "Неужели подстраивалась под меня?"
                "Наверное, только гипнотическим её воздействием можно было объяснить то, что спустя несколько секунд помрачения я обнаружил себя куда ближе к ней, чем раньше."
                "Исключая то, что руки не искали тепла на спине партнёрши, танец был всё тот же, к которому я уже успел привыкнуть за пару минут."
                "Разве что танцевали мы его на той дистанции, что была привычна мне по лагерям — а это расстояние совсем не «пионерское»."
                "И, наверное, этим несколько выделялись из толпы."
                "Всё то время, что я трусливо отводил глаза, позволяя взгляду прыгать по танцующим рядом пионерам, застывшей за вертушками Мику, Ольге Дмитриевне, улыбающейся и качающей головой в такт музыке…"
                "Я видел что не так что-то. {w}Что неправильно мы танцуем."
                "Но, наверное, поздновато уже было переигрывать?"
                "Тем более, что Славя уже успешно справилась с собой и перестала краснеть как маков цвет."
                "И где-то даже начала получать удовольствие. {w}Не знаю. Наверное."
                "Потому что уже в следующую секунду моё ухо опалило горячим дыханием:"
                sl "Семён, ау?"
                "Похоже, где-то что-то провернулось окончательно, и мы настроились на одну частоту."
                "Я, даже не имея возможности увидеть, {i}знал{/i}, что Славя улыбается."
                "И улыбка у неё — светлая-светлая."
                me "Да?"
                sl "Тебе не обязательно бояться меня."
                th "А, да есть причины, знаешь ли."
                "Несмотря на то, что я силой принуждал себя расслабляться и не костенеть, я всё же ощутимо нервничал."
                sl "Просто ты должен сказать мне, что тебя это нервирует, и я отстану."
                me "Да нет."
                th "Нервирует. {w}Но это не то, от чего я готов отказаться."
                "Секундой спустя я ощутил её голову себя на плече."
                window hide
                with fade
                "А через минуту и песня закончилась.{w} Но я, похоже, этого вовсе не заметил."
                "Славя всё так же грелась у меня на груди, несколькими миллиметрами дальше, чем вплотную."
                "Но вполне достаточно для того, чтобы ощущать биение жизни по ту сторону."
                "И только сейчас, когда я перестал циклиться сам на себе, я понял, как же быстро колотится её сердечко."
                stop music fadeout 3
                window hide
                $ persistent.sprite_time = 'night'
                $ night_time()
                play ambience ambience_camp_center_evening
                scene bg ext_square_night_party 
                show sl shy dress
                with dissolve
                "Быстро-быстро."
                "Девушка прикусила губу и, отстранившись, заглянула мне в глаза."
                "Снова."
                th "Да когда же ей надоест меня смущать?!"
                me "Спасибо за танец."
                "Я расцепил руки, запрещая себе думать о том, что может значить…"
                "Я сказал, запрещая себе думать!"
                "Мику переставила пластинку."
                show sl normal dress with dspr
                sl "Пойдёшь на следующем?"
                me "До следующего ещё надо дожить."
                "Уклончиво ответил я."
                me "Я пойду немного проветрюсь, ладно? {w}В голове бардак."
                "Славя кивнула и отпустила меня."
                hide sl with dissolve
                "И я, в поисках спасения от сошедшего с ума сердца и непрошенных мыслей, решил обратиться к первостихии."
                window hide
                scene bg ext_beach_night 
                with dissolve
                play ambience ambience_boat_station_night fadein 3
                "К воде."
                play music slavyas_fantazm fadein 3
                th "Почему всё происходит именно так?"
                if loki:
                    th "Неужели, одного единственного раза никогда не бывает достаточно?"
                    "Я как мотылёк, обжёгся о свечу так, что сгорел почти полностью."
                    "А испещрённое шрамами желание чуда, оказывается, и не сдохло вовсе."
                    "Вот же оно, чудо, в сарафане с Уроборосом, улыбается голубоглазо и поневоле веришь в его искренность и чистоту."
                else:
                    "Я сегодня весь день чувствую себя глупо, веду себя глупо и думаю только и исключительно глупости."
                    "Как будто мне и правда снова стукнуло семнадцать, и у кровати до утра не выключается ночник, и строчка за строчкой ложатся ровными рядами, рассказывая всему миру о том, почему он прекрасен."
                    "Настроение стихов, сказок и обязательного исполнения самых заветных желаний."
                    th "Мир. Ты прекрасен."
                    th "Потому что у тебя есть Славя."
                "Выдыхаю я во влажный ночной воздух, слушая бьющийся под ногами прибой."
                "Кажется, у меня так и не получилось придушить ненужное?"
                th "Ещё и танец этот, конечно. {w}Разумеется, Славя ничего не сказала и не оттолкнула — хотя следовало бы."
                th "Всё-таки пионерскую дистанцию придумали далеко не просто так."
                "Но её запах, её тепло, её голос."
                dreamgirl "Сдался и поплыл?"
                th "А что делать? {w}Тут поблизости же больше нигде не намечается дежурство по столовой, куда так удобно можно сбежать."
                dreamgirl "А сам уже — нет? Никак?"
                "Я горестно вздохнул."
                "Несмотря на то, что, похоже, это она все три дня бегала за мной и уговаривала не быть букой, похоже, очень скоро мы поменяемся ролями."
                "Если только не произойдёт нечто, что меня спасёт."
                dreamgirl "Спас-бросок по удаче! Оп!"
                th "Удачное спасение, ты где? Ау?"
                "За спиной раздался скрип песка под шагами, и на плечо мне легла рука."
                show el normal pioneer with dspr
                el "Доброночер."
                "Кивнул мне кибернетик."
                th "Ты-то меня и выручишь!"
                "Обрадовался я, тем не менее, пряча торжествующую улыбку."
                me "Эл, дружище! Какими судьбами?"
                "Мальчик вопросительно приподнял бровь — я так не умею — но послушно ответил:"
                el "Помнишь разговор после ужина?"
                me "Значит, понадобилась моя помощь?"
                "Он кивнул."
                me "Тогда веди!"
                stop music fadeout 3
                window hide
                return
            else:
                sl "Льстец!"
                me "Да ни в одном глазу. Говорю как есть — ты украшение танцпола."
                dreamgirl "Ага, а если представить, что под этим платьем ничего нет…"
                th "Гусары, молчать."
                show sl happy dress with dissolve
                sl "Вот уж не подумала бы, что ты так умеешь."
                "И это я ей ещё на ушко ничего не пошептал…"
                me "Что умею?"
                sl "Ай, не притворяйся. Я же знаю, что когда вы, мужчины, так делаете — это значит, хотите от девочки…"
                "Договорить, чего, по её мнению, мужчины могут хотеть от девочки, она не успела — я наконец добрался до её уха и внятно произнёс:"
                me "Ну что вы, барышня, мне от вас только танец, и тот…"
                "Здесь уже не договорил я — щекотка достигла нервных узлов, и Славя расхохоталась, чуть оттолкнув меня."
                if alt_day3_sl_invite:
                    me "Вообще, я жертва обстоятельств."
                    me "Я приехал в незнакомое место, а тут все такие добрые."
                    me "А ты ещё и красивая. Недолго и растеряться."
                else:
                    sl "Следующий танец мой! Дождись обязательно."
            if alt_day2_sl_bf:
                $ lp_sl += 1
            "Музыка закончилась и мы неохотно отпустили руки."
            hide sl with dissolve
        "Пригласить Алису" if alt_day3_dv_dj:
            call alt_day3_lp_checker(alt_dater = dv)
            if alt_day2_sl_bf:
                $ lp_sl -= 1
            show dv smile dress at center with dissolve
            "Я подошёл к зажигающей Алиске и вежливо постучал её по плечу."
            dv "ЧТО?!"
            "Весело прокричала она."
            "Я зашевелил губами, делая вид что что-то говорю, и она сняла наушники."
            me "Прекрасно, больше ты не привязана к этой шайтан-машинке. Пошли."
            "Я нетерпеливо протянул ей руку."
            dv "Куда? У меня тут программа ещё!"
            me "Танцевать! Я хочу, чтобы ты со мной станцевала!"
            "Раздельно повторил я."
            me "Пригласить тебя. Понимаешь?"
            show dv smile dress at center with dissolve
            dv "Нет!"
            "Она резко отдёрнула ладонь, которую я почти — почти! — схватил."
            me "Но почему?! Тебе жалко, что ли?"
            "Она спрятала руки за спину."
            dv "Не хочу и всё тут!"
            me "Я тебе настолько неприятен, что ты со мной танцевать не хо…"
            "Я ещё договаривал фразу, когда передо мной вдруг как будто зеркало повесили, и я объективно смог взглянуть на себя."
            "А вполне вероятно, кстати, что неприятен."
            "Лично я, будь я девушкой, с собой танцевать в жизни не стал бы."
            "Но Двачевская помотала головой!"
            "Значит, у меня есть шанс?"
            dv "Потому что я никогда не танцевала. Понятно?"
            "Внятно произнесла она."
            me "Ну и что? Будь ты девочкой, в конце концов, дай мне вести и ухаживать."
            "Она покачала головой."
            if alt_day3_un_invite == 1:
                dv "Зачем ты подошёл? Ты не знаешь, кого приглашать должен?"
                "Она посмотрела на меня так, что я проглотил все возражения."
            else:
                "Мы препирались ещё несколько минут, пока медляк не закончился."
                "Я сделал обиженное лицо, выпятил нижнюю губу и исподлобья посмотрел на злую Алиску!"
                me "Надеюсь, тебе хотя бы чуточку стыдно."
                "Укорил я. И ушёл. В следующий медляк ещё раз пойду звать!"
            $ alt_day3_dancing = 3
            hide dv with dissolve
        "Ни с кем не танцевать":
            $ alt_day3_dancing = 0
            "Музыка здесь, конечно, ничего. Но танцевать я всё равно не стану."
            "Что я, больной, что ли?"
            th "Одиночество — вот наше всё."
    if alt_day2_date == 132:
        scene cg d3_disco_no_un_7dl with dissolve
    else:
        scene cg d3_disco with dissolve
    "Дискотека набирала обороты!"
    "С некоторым удивлением я разглядел на танцполе и кибернетиков."
    "То, что они изображали, назвать танцем можно было лишь с ооочень большой натяжкой."
    th "Заболели, что ли?"
    if alt_day2_date == 132:
        "А вот Лены там больше не было."
    if alt_day3_dv_dj:
        "Алиса открыла в себе талант церемониймейстера, и отрывалась на полную катушку, буквально светясь от переполняющей её энергетики!"
        "Ещё чуть-чуть — и она сама пустится в пляс!"
        "Если песне не хватало ритма, она добавляла его с соседней пластинки, подгоняла скорости, где необходимо, добавляла нужного тембра — короче, вела себя так, что я лишний раз похвалил себя за то, что не проглядел талант!"
        "Я поймал её взгляд и показал большой палец."
        me "Ты молодец!"
        "Одними губами сказал я."
        dv "Сам дурак!"
        "Так же ответила она."
        "Двачевская стерва. Но — классная!"
    elif alt_day3_mi_dj:
        "Мику вела себя так, будто родилась с вертушкой в руках. Мама родная, что она с ней вытворяла!"
        "Я не особо силён во всей этой диджейской кухне, но такие очевидные вещи как мэшапы, например, гладкое сведение и игра с громкостью выполнялись её пальчиками безукоризненно, создавая дополнительный элемент шоу."
        "Впрочем, ничего другого от гениальной японской девочки я и не ожидал."
        "На какой-то миг она оторвалась от вертушек и, прижимая к правому уху наушник, пританцовывая, оглядела площадку."
        "Я поймал её взгляд и выставил вверх большой палец."
        me "Умница!"
        "Не пытаясь даже перекричать колонки, сказал я. {w}Прочитает по губам, я в ней не сомневаюсь."
        "Она в ответ подмигнула мне и вернулась к своей технике."
    "…"
    window hide
    stop music fadeout 3
    return

label alt_day3_dance_dance2:
    scene anim_square_party with dissolve
    if alt_day3_un_med_help == 1:
        show un smile dress at center with dissolve
        if alt_day_binder != 1:
            $ lp_un += 1
        "Лена тронула меня за плечо."
        un "Ну что, пошли?"
        if herc or loki:
            "Похоже, она опять растеряла те небольшие остатки смелости, что были в ней ещё в обед, когда она сбивчиво шептала своё странное признание."
        "Я улыбнулся и кивнул."
        hide un with dissolve
        window hide
        return
    elif alt_day3_us_bugs == 1  and alt_day2_date != 132:
        show us smile dress at center with dissolve
        play music genki fadein 3
        "Я был ещё весь во власти томного вечера, когда моё настроение крайне невежливо испортили — наступив на ногу."
        "Ульянка. Кто ж ещё."
        us "Ну что, пошалим?"
        "Усмехаясь, она подбросила в руке спичечный коробок."
        "И я вспомнил вдруг — что там!"
        return
    elif alt_day3_technoquest_st3 == 1:
        "Кто-то потрогал меня за плечо."
        show el normal pioneer at center with dspr
        el "Меня Шурик прислал, сказал, что всё готово для решающего рывка."
        if alt_day3_technoquest_st1 or alt_day3_technoquest_st2:
            me "Вы мне на шею-то не садитесь. Я вам помог уже."
            "Он пожал плечами."
            el "Извини, но, похоже, что без тебя никак."
        me "И под решающим рывком вы подразумеваете, что рваться придётся мне, а вы бездельничать будете?"
        "Он немного смутился."
        el "Ээээ… Немного не так, но…"
        if alt_day3_mi_dj or (lp_mi > 7):
            show mi normal dress at cleft with easeinleft
            mi "Простите, мальчики, но я забираю Семёна!"
            "Прощебетала Мику, уводя меня прочь."
            "Я даже возразить ничего не успел."
            scene anim_square_party
            return
    else:
        "Вскоре быстрая музыка опять сменилась перебором струн, и окружающие начали приглядываться друг к другу в поисках партнёра на танец."
        "И, разумеется, я тоже решил не отказывать себе в удовольствии потискать девочку под романтическую музыку."
    return

label alt_day3_dance_dance2_menu:
    menu:
        "Помочь им":
            $ alt_day3_technoquest_st3_help = True
            if alt_day3_dancing == 2:
                if alt_day3_sl_day_event:
                    $ lp_sl += 1
                sl "Правильное решение."
                show sl smile dress at right with moveinright
                "Улыбнулась незаметно подошедшая Славя."
                sl "Ты заставляешь уважать себя!"
                th "Я об этом пожалею…"
            elif alt_day3_dv_dj:
                me "Ребят, я вам и так помог. Но я Алису подводить не хочу, понимаете?"
                el "Понимаю."
                "Электроник пожал плечами, но, похоже, не был слишком удивлён."
            elif alt_day3_mi_dj:
                me "Извините, но я хочу убедиться, что у Мику всё в порядке, понимаешь?"
                el "Понимаю."
                "Электроник пожал плечами, но, похоже, не был слишком удивлён."
            me "Хватит. Пошли."
        "Остаться танцевать":
            if alt_day2_date == 132:
                hide el with dissolve
                "Электроник ушёл расстроенный, а я уселся на скамейку."
                "Быстрые танцы мне никогда не давались."
                "А единственная девушка, с которой мне хотелось танцевать медленные танцы, куда-то убежала в слезах."
                "Вздохнув и прикинув варианты, я поднялся и поплёлся вслед за кибернетикам."
                "Мой путь лежал в сторону клубов."
                th "Хотя бы при деле буду."
    return

label alt_day3_max_lp_dv:
    "Я смерил его взглядом."
    me "Чудной ты какой-то. Иди уже."
    el "Но ты…"
    "Запротестовал мальчик."
    me "Иди, я сказал. Поищи другого дурачка."
    $ alt_day3_lp_route = 3
    return
label alt_day3_max_lp_un:
    show un normal dress at right with dspr
    me "Вообще-то, я Лену пригласить собирался…"
    "Замялся я,  глядя на то, как она пробирается ко мне."
    un "А ты иди."
    "Похоже, она уловила конец разговора."
    me "Серьёзно?"
    show un smile dress at right with dspr
    un "Конечно."
    "Улыбнулась она."
    un "Всё равно в медпункте поработать надо."
    $ alt_day3_lp_route = 1
    "Я вздохнул и согласился с ней."
    me "Ваша взяла."
    "Кивнул Электронику я."
    me "Веди, куда ты там собирался."
    return
label alt_day3_max_lp_mi:
    me "Мне ещё Мику танцевать сегодня."
    $ alt_day3_lp_route = 4
    return
label alt_day3_max_lp_sl:
    me "Не получится, мне ещё Со Славей танцевать."
    $ alt_day3_lp_route = 2
    show sl sad dress at left with moveinleft
    sl "Семён! Как ты можешь говорить такое!"
    sl "Я была о тебе гораздо лучшего мнения, а ты товарищам помочь не хочешь!"
    me "Но как же наш танец… {w}Что там, горит у них?"
    show sl serious dress with dspr
    sl "Семён!"
    me "Ладно, ладно, мам, прости, уже иду."
    return
label alt_day3_max_lp_nobody:
    me "Танцевать буду."
    return


label alt_day3_choose3:
    scene anim_square_party with dissolve
    play music music_list["raindrops"] fadein 3
    menu:
        "Танцевать с Алисой" if alt_day3_dancing != 32:
            call alt_day3_lp_checker(alt_dater = dv)
            if alt_day3_dv_dj:
                if alt_day3_dancing == 3:
                    $ alt_day3_dancing = 31
                    "В этот раз я и слушать ничего не хочу!"
                    "Она на секунду отвлеклась от вертушек и бросила взгляд на танцпол."
                    "И, разумеется, пересеклась взглядами со мной!"
                    "Вздрогнув как от пощёчины, она попыталась было спрятаться — и куда только подевался весь задор — но в этот раз я не дал ей сбежать."
                    show dv normal dress far at center with dspr
                    "Я подошёл к ней и стащил с неё наушники."
                    show dv smile dress at center with dspr
                    me "Вечно за пультом не пропрячешься."
                    "Не знаю, что она расслышала в моём голосе, но почему-то вздрогнула."
                    "Нас будто сквозь строй пропускали — я ощущал десятки взглядов на себе и примерно столько же на ней."
                    "Проблема в том, что мы оба уже были в состоянии куража, когда поздно отыгрывать что-либо назад."
                    "Я мог бы сказать, что график обучаемости и приспосабливаемости в эти моменты приблизился к оптимуму."
                    "Ещё лучше он мог бы быть только если бы из колонок играла какая-нибудь пафосная вещь."
                    show dv smile dress with dissolve
                    "Но это был тривиальный медляк."
                    "Что ж, придётся работать с тем что есть."
                    "Она выглядела так, будто для неё в порядке вещей то, что её приглашают на танец."
                    "Идеальная спина, горящие глаза, румяные щёчки."
                    "Она чуть откинулась назад, закидывая руку мне на плечо."
                    "Ладонь легла в получившийся изгиб спины как влитая."
                    "Если бы не некая деревянная напряжённость плеча, я бы подумал, что для неё это и в самом деле момент тривиальный."
                    "Я наклонился к её ушку и наметил слова одним только дыханием."
                    window hide
                    scene cg d6_dv_dance_7dl with flash
                    me "Расслабься. Попробуем получить удовольствие от танца."
                    "Она серьёзно заглянула мне в глаза, видимо, пытаясь разобрать на их дне какой-то ответ."
                    "А я просто улыбнулся в ответ и привлёк её к себе поближе."
                    if alt_day3_mi_invite:
                        "Уроки Мику явно не прошли даром, я вёл девочку уверенно и без лишних волнений."
                    me "Зря ты в прошлый раз отказалась. Сейчас мы бы уже привыкли друг к другу и…"
                    me "Нет, ты, конечно, и так идеально ко мне подходишь. Мы как будто две детальки одного паззла."
                    me "Но к этому моменту, например, ты бы уже начала немного доверять мне."
                    me "Например, дыхание. Оно у нас разное. А должно быть — одно."
                    "Я свои 54 вдоха менять не собираюсь, но рассмотрю варианты гармоник."
                    "Я не сумел удержаться, и через две минуты после начала танца, пальцы уехали со ткани прямиком на разгорячённую кожу спины."
                    "Она тихо охнула, дёрнулась от щекотки, но под ладонями волнами уже разбегалось тепло, значит, не так уж оно и неприятно оказалось."
                    "Я так увлёкся впечатлениями, что за весь танец почти ничего не сказал."
                    "Ей пришлось самой снимать с себя мои руки и бежать к пульту ставить следующий трек."
                    "А я, прожжённый, циничный и невозмутимый я — вдруг понял, что если Алиска не будет моей — то можно заказывать мальчиков в белых халатах и аминазин прямо сейчас."
                    hide dv with dissolve
                else:
                    $ alt_day3_dancing = 30
                    "Прошлый раз потанцевать с Алисой не удалось, но, надеюсь, хотя бы в этот раз мне повезёт."
                    "Я подошёл к вертушкам и, дождавшись ответного взгляда, мотнул головой в сторону площадки."
                    show dv normal dress at center with dissolve
                    "Двачевская покраснела, кажется, до пяток, и отрицательно покачала головой."
                    "Но я не отставал! Я же если чего хочу…"
                    "Наконец она сдалась и, выйдя из-за пульта, вложила свою руку в мою ладонь."
                    dv "Веди. Ромео."
                    "Улыбнулась она."
                    window hide
                    scene cg d6_dv_dance_7dl with flash
                    "Мы промолчали весь танец, общаясь на уровне жестов, запахов, взглядов."
                    "Она отвечала на каждое моё легкое пожатие, и этот язык, знакомый мне как никому другому, заставлял тело покрываться сладкой ледяной истомой предвкушения."
                    "Танец закончился, а она, схватив меня за рубашку, притянула к себе и резко обозначила поцелуй где-то в области скулы."
                    "Потом рассмеялась и, воздев голову, прошествовала к пульту — подходило время следующей песни."
                    hide dv with dissolve
            else:
                $ alt_day3_dancing = 32
                "Алиска так и не пришла. Похоже, придётся мне либо искать другую кандидатуру, либо сычевать."
                jump alt_day3_choose3
        "Танцевать со Славей":
            call alt_day3_lp_checker(alt_dater = sl)
            if alt_day3_dancing == 2:
                $ alt_day3_dancing = 21
            else:
                $ alt_day3_dancing = 20
            if alt_day3_sl_day_event:
                "Между нами натянули струну."
                "Тайна, даже горькая, может сплотить людей — а может и разобщить."
                "Но я лелею надежду, что не разонравился Славе окончательно."
                "Ведь именно с ней я столкнулся в самой середине танцпола, придушив остатки нерешительности."
                "Этот танец мой по праву."
                sl "Потерял девушку, красавчик?"
                "Раздался насмешливый голос за плечом."
                if alt_day3_dancing == 2:
                    "Она была всё в том же платье, что и в наш прошлый танец. Но сидело оно на ней… Иначе?"
                show sl normal dress at center with dissolve
                "Может, обмялось, может, из-за разгорячённого влажного тела обтянуло будто перчатка — но взгляд было не отвести."
                me "Я потерял человека, с которым хотел танцевать."
                sl "Как же ты так?"
                "Её глаза отражали свет софитов и цветомузыки, но под влажной пеленой можно было разглядеть боль, участие и даже некий намёк на нежность."
                "Глаз в отрыве от мимики способен выразить что угодно. Достаточно просто отказаться от внутреннего монолога, перестать мыслить отдельными словами и переключиться на эмоции."
                me "Я не виноват."
                "Серьёзно сказал я, протягивая ей руку в приглашающем жесте."
                me "Я искал человека, а нашёл богиню."
                "Неземной смех — до мурашек и пропускающего удар мотора — был мне ответом."
                window hide
                scene cg d3_sl_dance
                with dissolve
            else:
                "Кажется, Славя не прочь потанцевать. Хотя мне, наверное, не хватило бы духу пригласить её в этот раз."
                "Но тут произошло так, что она сама подошла."
                show sl smile dress at center with dissolve
                sl "Потанцуем?"
                me "Потанцуем?"
                "Мы произнесли это одновременно и одновременно же рассмеялись."
                "Неизвестно откуда взявшийся мандраж, мучавший меня последние два часа, отступил, оставляя мне меня, ощущение музыки и неприкрытого интереса со стороны этой милой девочки с огромными голубыми глазами."
                me "Что?"
                with dissolve
                "Она вопросительно подняла глаза, сама вся пребывая где-то в другом месте."
                "А у неё платье такое… Такое. Оно как-то умудряется держаться на какой-то тоненькой верёвочке, и обладая нашей разницей в росте, можно наблюдать самые интересные ракурсы."
                "Она пожала плечами."
                sl "Да ничего."
                me "Точно?"
                sl "Безусловно."
                me "Я что-то сделал не так?"
                "Она рассмеялась."
                sl "Прекрати. Это мои личные проблемы, там твоей вины быть никак не может."
                "Я согласно кивнул, и до конца танца мы протанцевали молча."
            hide sl with dissolve
        "Танцевать с Мику":
            call alt_day3_lp_checker(alt_dater = mi)
            window hide
            scene anim_square_party:
            if alt_day3_dancing == 4:
                $ alt_day3_dancing = 41
            else:
                $ alt_day3_dancing = 40
            if alt_day3_mi_dj:
                show mi serious dress with dissolve
                me "Танцевать с самым красивым диджеем планеты — ну разве не прелесть."
                mi "По-моему, прелесть — это ты."
                show mi smile dress with dspr
                mi "И льстец."
                mi "Осталось только догадаться, чего ты добиваешься."
                mi "Такая идиома есть, не знаю, как по-русски. Когда мальчик девочку на горячие ключи приглашает."
                "Я понял."
                me "У нас это называется сверхблизкими отношениями."
                me "Интимными."
                window hide
                scene cg d3_mi_dance_afar_7dl
                with dissolve2
                mi "Насколько интимными?"
                "Она прижалась близко-близко."
                mi "Настолько?"
                "И лукаво улыбнулась тому, как у меня перехватило дыхание."
            else:
                show mi serious dress with dissolve
                "Сказать честно, я очень опасался, что Мику мне откажет. Она выглядела какой-то рассеянной и расстроенной."
                "Впрочем, она тут же приободрилась, стоило лишь моим рукам охватить её стан."
                mi "Чуть выше положи ладонь, пожалуйста."
                show mi normal dress with dissolve
                "Она довольно улыбнулась."
                mi "Да, именно сюда. Спасибо."
                "На то самое место, где у всех белых людей обычно приходится застежка от бюстгальтера."
                "Его отсутствие вызывало справедливые вопросы, а мне вдруг стукнуло в голову мыслью, что со мной сейчас танцует девочка, у которой под одеждой ничего — "
                "Она шевельнула бедром, отсекая сомнения."
                window hide
                scene cg d3_mi_dance_close_7dl
                "Ничего нет!"
                "Если она собиралась поиздеваться над бедным мной — у неё это здорово получилось!"
                "Я раза три собирался с духом, прочищал горло и прочими способами пытался преодолеть ступор."
                if alt_day2_rendezvous == 1 or alt_day2_rendezvous == 2 or alt_day2_rendezvous == 3: 
                    "Ох, как я это время завидовал тому гаду из своего вчерашнего сна!"
                    "С его решительностью, он чувствовал бы себя здесь в своей тарелке."
            window hide
            scene cg d3_mi_dance_afar_7dl with dissolve
            "Танец подобен беседе."
            "Мы знакомимся каждый по-разному — кому-то достаточно мимолётного взгляда, дёрнувшегося зрачка или приподнявшейся брови — всей этой микрожестикуляции — чтобы подойти и улыбнуться."
            "Кому-то необходимо нечто более материальное — как минимум, представиться, пожать руку, обменяться маркерами запаха."
            "Ну а для молодёжи всегда остаётся танец."
            "Между нами два-четыре слоя одежды, даже если один из партнёров в платье на голое тело, но расстояние — расстояние тепла, и я выдыхаю одновременно с ней, сердца бьются в едином ритме, и даже мысли сходятся."
            "Недаром же говорят, что танцевальные пары куда крепче, чем пары романтические — ибо им проще выстроить себя под знакомый и с детства любимый ритм танго, нежели пытаться лавировать в бурном море взаимного непонимания."
            "Я знаю, что в Японии совершенно собственные танцы, которые ничуть на наши не походят."
            "Я знаю, что поддевать что-либо под кимоно — это признак плохого вкуса."
            "Мику очень быстро адаптировалась под наши реалии и с огромным удовольствием пользовалась тем, что ей с детства не прививали зашоренности советского ребёнка, до восемнадцати лет по-ханжески считающего дружбу организмов чем-то постыдным."
            if not (herc or loki):
                "{i}Я познакомился с одной такой незашоренной просто — через интернет. В одном из чатиков, крутящих в фоновом режиме поток бесконечных «приветов», пока я набивал очередную простыню текста, глаз вдруг резануло ником.{/i}"
                "{i}Стрелядь.{/i}"
                "{i}Это звучало настолько похабно, что я не выдержал, рассмеялся — переключился на вкладку.{/i}"
                "{i}И да — к рыбе этот ник действительно не имел отношения, это был синтез «стервы» и… понятно, короче.{/i}"
                "{i}Потом предновогодний обмен асечными номерами, и — приглашение куда-нибудь выбраться.{/i}"
                "{i}А я не придумал ничего умнее, чем позвать её на дискотеку.{/i}"
                "{i}И всё, на несколько бесконечных часов я попросту пропал — начиная с момента, когда я встретил её с эскалатора на «Нарвской», и до самого утра, когда она очень неохотно отпустила мою руку.{/i}"
                "{i}Невысокая — ростом примерно с Мику — но предпочитающая короткое каре с бритвенно-острым нижним срезом, иссиня-чёрные волосы, зрачок, занимающий, казалось, всю дужку и экзотический разрез глаз.{/i}"
                "{i}Она запомнилась мне походочкой — чуточку семенящей, когда она о чём-то задумывалась или смущалась, или враспрыжку, когда нападало легкомысленное настроение.{/i}"
                "{i}И вот такой семенящий ребёнок подходит ко мне, безошибочно узнавая меня из толпы, суёт руку и бросает «Риоко».{/i}"
                "{i}За первые несколько часов мы не сказали друг другу и пары слов — и одновременно рассказали во много раз больше, чем иной раз сможет слово.{/i}"
                "{i}Я, пытающийся прийти в себя, она, пытающаяся понять, чего этому дылде вообще от неё надо… В асе-то мы болтали беспрестанно, постоянно находя всё новые и новые темы для бесед.{/i}"
                "{i}Собственно, разбежавшись с утра, мы встретились в окошке мессенджера тем же вечером, и опять болтали до рассвета, перемывая друг другу косточки и смеясь над глупым, неуклюжим свиданием. {/i}"
                "{i}Ещё пару месяцев пришлось изживать в ней дурацкую манеру приклеивать к моему имени хонорифики — хотя, казалось бы, учась в фотохудожке, в России, давно можно было бы привыкнуть к нашим формам обращения. Семён-сан, подумать только.{/i}"
                "{i}Стоит ли говорить, что мы решили дать другу ещё один шанс?{/i}"
                "{i}Март закончился, начался апрель, Риоко мёрзла у Ленэкспо, сжимая в кармане билетики с полустёршимся штемпелем — на выставку традиционного японского дизайна интерьера. {w}А меня в это время увозила скорая с температурой сорок.{/i}"
                "{i}В конце мая мы поменялись местами, и в этот раз нам повезло.{/i}"
                "Мику молчала, почти мурлыкая, улыбаясь загадочно и смотря так, что дух захватывало, затягивая в морские пучины этих смеющихся глаз."
                "Она была так близко, что могла потереться щекой о щёку как самая настоящая кошка — и несколько раз воспользовалась предоставившейся возможностью."
                "{i}Я перехватил её сразу после выписки и на день города увёз в Петергоф, где наша хрупкая дружба пошла прахом — ведь это так трудно, удержаться и не поцеловать девушку, когда она подставилась под фонтан и дрожит в руках, пытаясь согреться.{/i}"
                "{i}Наш первый. До сих пор я полагал, что это глупость, картон и просто касание кожи. Какое там таинство может быть, в каком-то банальном поцелуе, я вас умоляю.{/i}"
                "{i}Наша первая близость — и опять вместо ожидаемого бестолкового сплетения тел, нацеленного на единственно оргазм, получилось что-то непонятное… И волнующее.{/i}"
                "{i}Хотя мы оба негласно решили, что целоваться нам нравится куда больше. {w}Такие вот непонятные люди.{/i}"
            mi "О чём думаешь, Сенечка?"
            "Шепнула Мику."
            me "Сложно сказать… Наверное о том, что ты слишком красивая, а я слишком дурак."
            if not (herc or loki):
                "{i}К моему дню рождения Риоко стукнуло девятнадцать, и она перешла на последний курс.{/i}"
                "{i}Заканчивались нулевые, в Азии разыгралась очередная финансовая драма, и мы прощались, и всё никак не могли насытиться перед прощанием.{/i}"
                "{i}Правильно говорят — перед смертью не надышишься. {w}Мы обещали друг другу постоянно держать связь и не отпускать пальцев друг друга, она даже где-то тихонько всплакнула, хотя и не показывала мне этого.{/i}"
                "{i}И уехала.{/i}"
                "{i}Мы и в самом деле писали и созванивались каждый день, висели на линии часами, какие-то дикие счета за телефон оплачивали, прежде чем открыли для себя интернет-телефонию.{/i}"
                "{i}Риоко улыбалась в веб-камеру, махала мне рукой, слала фотки города, взамен требовала от меня того же.{/i}"
                "{i}Мы не сдержали обещания.{/i}"
            mi "Какие глупости ты говоришь, Сенечка. Ты не дурак!"
            mi "Ты большая умница и вообще симпатичный."
            me "Ума-то мне это не добавляет…"
            "Я бы развёл руками, если бы они не обнимали японку."
            if not (herc or loki):
                "{i}Сначала начали лимитировать общение.{/i}"
                "{i}Потом выходить на связь не каждую неделю.{/i}"
                "{i}И, мало-помалу, совсем перестали общаться. Вот так — тихо и скромно, без претензий и выяснений отношений.{/i}"
                "{i}Почему так случилось, я не знаю.{/i}"
                "{i}Но только благодаря Риоко придаю значение самому обычному танцу куда большее, чем это делает кто-то ещё.{/i}"
            window hide
            scene cg d6_dance_alt_7dl with flash
            "Мне казалась удивительной её раскрепощённость — воспитание, всё же, очень много значит в этом возрасте."
            "Но всё-таки она слишком спешит."
            "Поэтому я вежливо отвёл её к скамеечке и поспешил откланяться."
            if not (herc or loki):
                "Накатившими воспоминаниями похоронило всё настроение."
            "Может быть, завтра… Если она не будет против."
            if alt_day2_date == 4 and not alt_day2_mi_hyst:
                "Отчаянная Мику. Надеюсь, ей и правда хватит отчаяния не расценить моё поведение как отсутствие интереса."
            else:
                "А здесь… Вот эта её совершенно необъяснимая решительность в поступках…"
            "Как будто она знает что-то, дающее ей решимости и сил на то, чтобы переть асфальтовым катком по кратчайшему пути."
            dreamgirl "А что, тебе не нравится, что в этот раз окучивают тебя, а не ты?"
            dreamgirl "Попробуй расслабиться и получить удовольствие."
            th "Как будто у меня есть выбор."
            dreamgirl "Выбор есть всегда. Ты можешь просто отморозиться, как это ты обычно делаешь."
            dreamgirl "Просто подумай как следует — чего тебе на самом деле хочется?" 
        "Танцевать с Леной" if alt_day3_dancing != 12:
            if (herc or loki) and (alt_day3_un_med_help != 1):
                call alt_day3_lp_checker(alt_dater = un)
                if alt_day3_dancing == 1:
                    $ alt_day3_dancing = 11
                else:
                    $ alt_day3_dancing = 10
                if lp_un >= 5:
                    show un shy dress with dissolve
                    "Она всё начало танца то думала о чём-то, то поглядывала на часы."
                    me "Возможно, я не самый догадливый человек на Земле, но ты явно чего-то ожидаешь."
                    un "Скорее, опаздываю."
                    me "Куда?"
                    if alt_day3_un_med_help == 2:
                        un "Ты отказался помочь в медпункте, так что мне там придётся поработать."
                        un "Одной."
                    elif alt_day3_un_med_help == 0:
                        un "Понимаешь, меня Виола просила помочь ей что-то там в медпункте сделать."
                        un "Я всё равно не танцую, вот и согласилась."
                    show cg d3_un_dance with dissolve
                    "Она бросила на меня быстрый взгляд исподлобья."
                    un "А тут ты."
                    me "А что я?"
                    un "Танцуешь."
                    "Её голос совершенно стих, поэтому слово пришлось читать по губам."
                    "Я улыбнулся."
                    me "Я могу говорить только за себя. Но я сегодня занимаюсь тем, что мне очень нравится."
                    me "Если бы это была не ты — удовольствия и вполовину столько же не было бы."
                    "Она смутилась и, не зная куда девать глаза, прикрыла их."
                    "Впрочем, улыбка на её губах была самая что ни есть довольная."
                else:
                    "Весь танец она промолчала."
                    "Промолчал и я — сначала не зная что сказать."
                    "А потом — задохнувшись от её доверчивого жеста — на каком-то из поворотов она положила голову мне на плечо и так счастливо-счастливо вздохнула."
                    "Я могу говорить только за себя, но, кажется, это был тот самый момент, когда все объяснения и слова были бы лишними."
                    "На моё лёгкое пожатие руки она ответила своим."
                    "И с «пионерской» дистанции сделала шажок вперёд, оказавшись настолько близко, что, казалось, наши нервные системы превратились в одно целое."
                    "Во всяком случае, лёгкий мандражик у нас был один на двоих — это я знаю точно."
                    "А я вместо стылого ночного воздуха вдруг вдохнул её запах — терпкий, с еле уловимыми сладкими нотками, идущими от волос."
                    "Запах чисто-чисто отмытой кожи, которая только что разогрелась от активных действий."
                    "То, что возбуждает и сводит с ума любовников всего мира — если, разумеется, они успели принять душ прежде, чем перейти непосредственно к действиям."
                    "Впрочем, ничего гарантировать не могу — мои знания на этот счёт в большинстве своём чисто теоретические."
                hide un with dissolve
            elif alt_day3_dancing == 1:
                $ alt_day3_dancing = 12
                "Я думал пригласить Лену ещё раз, но как ни выглядывал её — нигде не мог найти. Нигде."
                th "Она, помнится, что-то про медпункт говорила. Неужели уже ушла?"
                "Пришлось смириться и поискать себе другую кандидатуру."
                jump alt_day3_choose3
        "Ни с кем не танцевать.":
            $ alt_day3_dancing = 5
            "В этот раз Славя одарила меня самым многообещающим взглядом. Потом встала и подошла ко мне."
            show sl smile dress with dissolve
            sl "Пошли, потанцуем?"
            "Она протянула мне руку."
            "Я отрицательно покачал головой."
            me "Не-а."
            sl "Но почему? "
            me "Ни-по-че-му."
            "Внятно ответил я."
            me "Не хочу и всё."
            "Она отвернулась и ушла."
            hide sl with easeoutleft
            "Похоже, обиделась."
            th "Бяда-бяда-огорчение…"
            "Сказал я сам себе голосом домовёнка Кузи."
            "Отстояв священное право на возможность забиться в тёмный угол, я немедля им воспользовался."
            "Проблема любых долбанных танцев в том, что дети на них не хотят музыку слышать."
            "Они хотят её СЛУШАТЬ. А для этого им нужен как минимум киловатт выходной мощности."
            "А с такой мощностью даже упомянутые сложности с басовой подачей переставали быть таковыми."
            "И спрятаться от местного сельского фольклора не удалось бы даже под тремя подушками с одеялами."
            "Потому что басы доставали даже там."
            "Картина маслом — Семён сидит в домике, держась за квадратную голову, а сверху ввинчивается саморез из какой-нибудь «Синей птицы» или чего-нибудь из раннего Макаревича."
            if alt_day3_dv_dj:
                "Хотя вкус у Алиски оказался неплохим, и втихаря она ставила вполне приличные вещи — всё же сто процентов ротации моим плейлистом забить было нельзя."
                "Я поймал её взгляд и выразительно показал, чтобы освобождала устройство — мне оно необходимо."
                "После вечера мучений мне требуется катарсис, а значит, без Мэрилина Мэнсона не обойтись никак."
            elif alt_day3_mi_dj:
                "Надежда была на Мику, что она из Японии хотя бы что-нибудь вменяемое привезла."
                "Напрасно."
                "Я как-то не обратил внимания, что на столике ведущего не было магнитофона, значит, все танцы шли полностью с пластинок. А такая девочка как Мику вряд ли стала бы забивать багаж винилом."
            "В горле встал горький комок, как и всегда, когда меня насильно пытаются подселить в толпу."
            "Я же, блин, вам не штамм, чтобы безболезненно подселяться!"
            "Я не хочу и буду активно противодействовать."
            "Наконец кошмар закончился, и вожатая со значением посмотрела на меня."
            show mt smile dress with dissolve
            mt "Это был прекрасный вечер, спасибо ведущему!"
            "Она вся светилась от удовольствия."
            if alt_day2_date != 6:
                th "Кобыла."
                th "Нагарцевалась."
            mt "Объявляется последний медленный танец. Белый танец."
            mt "Дамы приглашают кавалеров."
            show mt normal dress with dissolve
            "Твою ж дивизию."
            "Я наметил себе Генду в качестве надёжи и опоры."
            "Спрячусь за него и пережду. Обижать Славю ещё раз в мои планы не входит — а то вдруг до неё в прошлый раз не дошло."
            mt "Семён, не убегай. Будешь танцевать со мной."
            th "Она что, это на весь лагерь объявила?!"
            "Она отложила микрофон в сторону и как ледокол сквозь торосы направилась в моём направлении!"
            window hide
            "Мать! Мать! Мать!"
            dreamgirl "То, что называется «плавно слился», да?"
            "Она подошла ко мне и, взяв за плечо, развернула к танцполу."
            mt "Пошли… Семён."
            mt "Ты же не откажешь девушке в такой мелочи, как танец?"
            show mt smile dress close with dissolve
            if alt_dlc_active:
                call alt_day3_uvao_ch3
                pause(1)
                if alt_day3_uvao_spotted:
                    return
            mt "Особенно после всего случившегося."
            "ЧЕГО СЛУЧИВШЕГОСЯ?"
            me "Напоминаю, что в домик к себе подселили меня вы."
            me "А то, что с утра я с кровати сверзился — я в этом не виноват."
            mt "Сём…"
            mt "Ну ты дуру-то из меня не делай."
            mt "Я же вижу твои реакции. И я всё прекрасно помню."
            mt "Но если ты хочешь продолжать играть в невинного пионера…"
            "Она легонько корябнула когтями мне по рёбрам и меня передёрнуло всего от сладкой щекотки."
            mt "То мне и эта игра по нраву."
            dreamgirl "Чувак! Вот про эту твою эрогенную зону мне не было известно!"
            dreamgirl "Откуда она знает-то?!"
            th "А я знаю?!"
            if alt_day2_date == 6:
                mt "С внутренним болваном споришь?"
                "Я еле челюсть поймать успел, а она рассмеялась."
                mt "У тебя всегда в такие моменты так зрачки забавно расходятся, будто ты меня видеть перестаёшь."
                mt "Но ты прав, если я сейчас сделаю что хочу, поддерживать реноме идеальной вожатой будет трудновато."
            else:
                mt "Ты, главное, не кричи опять на весь лагерь о том, как к тебе вожатая пристаёт."
                mt "А то мало ли…"
            show mt smile dress with dissolve
            "Танец закончился, и я поспешил отойти от дикой вожатой."
            mt "Семён, что не так? Семён?"
            show mt shocked dress with dissolve
            "Потом до неё, кажется, дошло, так как она охнула и прижала ладошку ко рту."
            mt "Так ты что, ничего не помнишь?! Серьёзно?"
            mt "А я тут к тебе с откровениями. Кошмааааар!"
            if alt_day2_date == 6:
                show mt smile dress with dissolve
                mt "Ничего, скоро всё вспомнишь."
                mt "Кстати, спасибо за вчерашнюю помощь."
                "Коснувшись моей щеки кубами, она растворилась среди танцующих."
            else:
                "Она смутилась и поспешила раствориться в кричащей толпе."
            hide mt with dissolve
            "Мда… Вожатая со своими прохладными рассказами и орущие пионэры-дебилы."
            "Какое восхитительное сочетание."
            "Пока вечер не стал ещё более томным, я поспешил развернуться и спрятаться в собственном домике."
            stop ambience fadeout 4
    return

label alt_day3_disco_past_d2:
    "Дискотека подходила к плавному завершению."
    "Что я точно могу сказать — она удалась!"
    "Нет, возможно, я не показал себя лучшим танцором или кем-то в этом духе."
    "Но мне — внезапно — понравилось!"
    "Так, как не нравилось уже очень давно."
    "Неужели я действительно умудрился сбежать от этой взрослости и скучности, когда ты не понимаешь, зачем идёшь на танцпол?"
    "Хотелось бы верить. Хотя бы на то время, что я здесь."
    if alt_day3_dv_dj:
        show cg d3_dv_alice_dj80_7dl with dissolve
        "Алиска оказалась огромной умницей и прекрасно справилась с новой для себя работой."
        "Очень надеюсь, что она найдет в этом если не основное занятие, то хотя бы отдушину."
    elif alt_day3_mi_dj:
        "Мику не подвела. Я с первых минут знакомства поверил в эту девочку, и она оправдывает доверие."
        "Грею себя надеждой, что однажды встречу на прилавке пластиковый чехольчик с компакт-диском, украшенный изображением миниатюрной девочки с волосами необычного колера, уложенными в два хвоста."
        "И надписью «Мастер-микс от Мику»."
    "Сыграли последние аккорды, и микрофон взяла Ольга:"
    scene anim_square_party
    show mt smile dress
    with dissolve
    mt "Спасибо ведущему, спасибо всем вам за прекрасный вечер!"
    "Она не скрывала удовольствия."
    mt "Надеюсь, он будет не последним."
    mt "А по лагерю «Совёнок» объявляется…"
    show mt normal dress with dissolve
    "Все обиженно загудели, а Ольга рассмеялась."
    mt "Хорошо, хорошо. Последний танец. А потом по лагерю «Совёнок» объявляется отбой."
    hide mt with dissolve
    "Завопили все, я с удивлением обнаружил, что присоединил свой ликующий глас к хору других голосов."
    window hide
    "Мы танцевали наш последний танец так, будто это и правда был последний танец в нашей жизни."
    "Акклиматизация прошла успешно. Я стал частью лагеря."
    "Частью этих странных людей, верящих в светлое «завтра» и друг в друга."
    "Мне никогда не стать таким. Но я хотя бы могу примкнуть к их рядам."
    "И болеть себе со скамейки запасных."
    "Рядом остановилась Славя."
    show sl normal dress with dissolve
    sl "Ты не танцуешь."
    "Я кивнул."
    me "Я вдруг понял, насколько далёк ото всех вас."
    me "И насколько вам всем завидую."
    if not loki and alt_day2_walk == 2:
        sl "Я помню что ты вчера говорил про свой страшный мир."
        sl "Знаешь… Я думаю, куда важнее то, что внутри."
        sl "Даже в самом страшном мире."
        sl "И, знаешь… Я думаю, что если бы мы однажды встретились — там, на холодных улицах."
        sl "Я сделала бы неправдой тобой сказанное. Или хотя бы попыталась."
    "Я рассеянно улыбался ей, качая головой в такт."
    show sl smile dress
    sl "Ой, я совсем забыла."
    sl "У нас же баньку затопили. Если хочешь, могу тебе очередь занять."
    sl "Пойдёшь мыться?"
    me "Конечно! Спасибо огромное!"
    sl "Вот и хорошо. Как соберёшься, спроси у Ольги Дмитриевны, она объяснит как пройти."
    hide sl with dissolve
    "Она улыбнулась и растаяла в темноте."
    return
        

    
label alt_day3_technoquest3:
    if alt_day3_technoquest_st3 == 1:
        stop ambience
        stop music
        window hide
        scene bg ext_clubs_night with dissolve
        play ambience ambience_camp_center_night fadein 3
        show el normal pioneer at cleft
        show sh normal pioneer at cright
        with dissolve
        if alt_day3_sl_day_event2:
            "Электроник вывел меня короткой тропинкой к зданию клубов, где на крылечке уже мялся его друг."
        else:
            "Оба кибернетика уже стояли у двери, ожидая персонального мессию. Меня."
            "Оба же синхронно вздрогнули, когда я выступил из темноты в освещённый сектор перед крыльцом."
        me "Ну, братцы-кролики. Это должно быть что-то очень важное."
        me "Раз уж вы человека от танцев отрываете, значит, причина должна быть веской."
        if alt_day3_sl_day_event2:
            "Про то, что они меня по сути спасли от ненужных мне переживаний, я им сообщать не стал: настоящий мастер лишён гордыни."
        else:
            "Я не стал им говорить, что на танцах больше маялся, ожидая, когда все эти странные пляски под странную музыку закончатся."
            "Незачем им это знать."
        me "Ладно, господа, с чего начнём?"
        show el smile pioneer at cleft
        show sh surprise pioneer at cright
        with dissolve
        "Электроник толкнул друга локтем в бок:"
        el "Я же тебе говорил, он придёт!"
        "Шурик выглядел, мягко говоря, удивлённым."
        if alt_day3_technoquest_st1:
            "Видимо, решил, что после того, как я за них у них же в кладовке уберусь, я пошлю их далеко и надолго."
            "И он был не слишком далёк от истины."
            if alt_day3_technoquest_st2:
                "Кладовка… Лестница… Теперь, вот, на крышу лезть."
                "Апофеозом заботы о двух беспомощных пионерах станет только немедленное усыновление одного из них."
            "Я понятия не имел, что и где пошло не так, и каким образом это случилось, но похоже, что у меня в лагере и в самом деле сложилась крайне определённая репутация."
            "Из-за которой одна половина лагеря меня втихую презирает — ту же Двачевскую взять, например."
            "А другая норовит сесть на шею и ножки свесить. И здесь мы плавно выезжаем к чёртовым теоретикам."
        show sh normal pioneer with dspr
        me "Так вы обрисуете мне рабочий объект, или мы потрещать на крылечке собрались?"
        el "На крыше."
        me "Ага."
        if alt_day3_technoquest_st2:
            me "Ребят, один вопрос."
            me "Я спросить днём не успел, забегался."
            me "У вас тут что, нет чердака или другого выхода на крышу, что вы меня с лестницей бегать заставляете?"
            sh "Вообще-то, здесь и правда нет других выходов."
        else:
            me "И шансов сделать всё с чердака, разумеется, нет."
            sh "Нет. Но мы раздобыли лестницу."
            if not herc:
                "Он сходил в клуб и выволок оттуда лёгкую алюминиевую стремянку."
                sh "Вот."
                "Он поставил лестницу и сделал приглашающий жест."
            else:
                sh "Поможешь лестницу поставить?"
                th "О, ну да. Конечно. Помогу поставить лестницу для самого себя."
                th "Мне, наверное, следует поблагодарить, что меня самого не заставили этим заниматься."
                th "В одиночку."
                "Пыхтя и обливаясь потом, мы подняли-таки неподъёмную деревянную лестницу и приставили её к крыше."
                sh "Ну, удачи."
        me "Что, вот так прямо и лезть?"
        "Я не скрывал ужаса."
        me "А если я навернусь там в потёмках?!"
        show el grin pioneer at cleft with dspr
        el "Не навернёшься. Там два фонаря всё освещают."
        me "И что ж вы, умные, сами тогда не полезли?"
        "Переглянувшись, оба промолчали."
        th "Похоже, действительно высоты боятся. Или там осиное гнездо. Или… Не знаю."
        el "Погоди."
        "Электроник протянул мне зелёную брезентовую сумочку, в которой что-то бряцало."
        show el smile pioneer at cleft with dspr
        show sh normal pioneer at cright with dspr
        me "И что там?"
        "Я даже не собирался заглядывать внутрь. Лишний предмет на лестнице чреват риском слететь с оной."
        if alt_day3_technoquest_st1:
            me  "Весь хлам, что мы сегодня из каморки вымели?"
        else:
            me "Что-то, что не поможет, если я решу стать героем?"
        show el normal pioneer at cleft with dspr
        el "Там инструменты! Отвертки, ключи и прочее, если понадобится."
        me "И куда мне его?"
        show el upset pioneer at cleft with dspr
        el "Так это… к ремню пристегни, там карабин."
        show sh upset pioneer at cright with dspr
        sh "И постарайся там наверху их не растерять. Тут самое лучшее, что мы нашли."
        "Ох, как мне хотелось отправить их самих наверх. Чтобы сами за своим хламом следили!"
        me "Не бойся, не растеряю."
        "Сквозь зубы процедил я и, попробовав лестницу на прочность, сделал первый шаг."
        show el serious pioneer at cleft with dspr
        el "Я подержу!"
        if alt_day3_technoquest_st1:
            me "Сил-то хватит?"
            el "Это же лестница. Хватит!"
            "Он встал рядом, схватившись обеими руками за лестницу."
        "Немного устойчивости в самом деле не повредит."
        me "Хотя бы примерно можете сказать — что там?"
        sh "Неизвестно. Передатчик находится непосредственно на чердаке, так что вряд ли он повреждён. Остальное — надо смотреть."
        show sh serious pioneer at cright with dspr
        sh "Удачи."
        play music music_list["you_lost_me"] fadein 3
        me "Ну, понеслась, помолясь."
        "Я сделал второй шаг."
        dreamgirl "Удачи. Желаю тебе не свернуть голову во имя неизвестно чьего блага."
        th "Если бы ещё были здравые альтернативы."
        th "Разве что Славя. Славя-то без проблем полезет."
        if alt_day3_sl_day_event2:
            dreamgirl "Куда она полезет, дурака кусок? {w}Я тебе намекну, в кабинете начальника лагеря она на табуретку не полезла не потому, что боялась тебе нижнее бельё засветить."
            dreamgirl "Если бы ты чуть меньше распускал слюни, а наблюдал за обстановкой, понял бы, что у девочки явно выраженная фобия."
            dreamgirl "Боязнь высоты, паническая причём."
        dreamgirl "Так что ты герой. Да. {w}Будешь лежать весь переломанный и в гипсе, зато в лагере будет собственное радио."
        dreamgirl "И кто знает — вдруг тебе повезёт, и к тебе даже зайдёт Славя, сказать «спасибо, ты настоящий друг»."
        dreamgirl "ДрукЪ! А не сосиска."
        "Вот в таком уже привычном режиме внутреннего диалога, я не заметил, как сделал третий и четвёртый шаги — так, что руки уже могли схватиться за крышу."
        hide el
        hide sh
        with dissolve
        me "Надеюсь, это действительно стоит того."
        el "Стоит, конечно! У нас же собственное радио будет! Разве не волнительно?"
        me "Очень."
        play sound sfx_hiding_in_bush
        "Лестница поехала куда-то вбок, заскрежетала о сталь…"
        "И я вцепился в край, рявкнув:"
        me "Ну вы там, руки-крюки! Держите лестницу нормально!"
        "Ноги вдруг стали как ватные, а сердце заколотилось где-то у горла."
        el "Шурик, вставай с другой стороны!"
        "Кажется, Шурик послушался своего друга, потому что утерянный было баланс был восстановлен."
        sh "Держу!"
        me "«Это будет нетрудно» говорили они."
        if alt_day3_technoquest_st1:
            extend " «Просто убраться в кладовке» говорили они."
        else:
            extend " «Просто наладить радио» говорили они."
        "Если меня сейчас стошнит от страха, всё окажется на двух идиотах внизу."
        "Уняв немного сердцебиение, я сделал ещё один шаг, и…"
        "Зацепился за крышу! Забрался!"
        "Я сел прямо на влажное железо и несколько секунд глубоко-глубоко дышал, справляясь со страхом."
        "Ох уж мне этот долбанный экстрим."
        el "Семён, как ты там?!"
        me "Жив-здор…"
        "Я сглотнул."
        me "Здоров. Можете пока не держать лестницу."
        sh "Мы тут внизу, если что надо!"
        "Если — что? Всё, что мне сейчас надо, это поменяться с кем-нибудь местами."
        "Наконец я справился с колотящей тело трясучкой и очень осторожно поднялся."
        me "Куда мне идти?"
        el "На тот скат! Там слуховое окошко, и от него должен идти кабель."
        "Очень своевременная информация, спасибо."
        "Посекундно оскальзываясь, я двинулся наверх, немного задержался у конька крыши, выискивая упомянутое окошко, и направился туда."
        "Кабелей здесь и правда было с избытком."
        "Проложенные в некое грубое подобие кабель-канала, они толстым пучком уходили под окошко, при этом закрывая свободное пространство целиком, так что затопления во время дождя можно было не опасаться."
        "Антенну удалось найти не сразу, но и с этой задачей я справился."
        "Я проследил её направление взглядом и заметил, что примерно посередине крыши она придавлена толстой сухой веткой."
        me "Только не навернуться, только не навернуться."
        "Я предпочёл идти по коньку — тут хотя бы можно среагировать и быстро сесть. Тем более, что и до места аварии отсюда можно дотянуться."
        "Раскачав ветку, я оттолкнул её в сторону."
        me "Поберегись!"
        play sound sfx_hiding_in_bush
        "С жестяным шорохом проехав по крыше, ветка ухнула куда-то в темноту,"
        play sound sfx_bus_window_hit
        extend " где с грохотом и ударилась о землю."
        "Судя по всему, она собственным весом просто вырвала штекер из гнезда."
        "Ничего сложного, даже инструменты не понадобились."
        "Я поднял штекер с крыши и, загнав его в розетку, немного пошевелил, проверяя."
        me "Народ, идите проверяйте! Работает?"
        el "Сейчас!"
        "Под ногами раздался топот, " 
        play sound sfx_open_door_clubs fadein 0
        play sound phone_feedback fadein 0
        extend "грохот захлопнутой двери…"
        "Ближайший жестяной репродуктор вдруг зафонил, а я чуть не сверзился прямо с того места, где сидел."
        me "Вы меня инвалидом сделать хотите? Нельзя же так пугать!"
        "Под ногами опять раздался топот, и темнота голосом Электроника отозвалась:"
        el "Всё работает, Семён, спасибо! Можешь спускаться."
        th "Ага. А теперь самый весёлый этап."
        stop music fadeout 3
        "Стараясь не думать о том, как буду спускаться, я, косолапя, чтобы не скользить, двинулся к лестнице."
        "Впрочем, о спуске провидение уже позаботилось."
        "Под ногой тёмным пятном мелькнул незамеченный лист. Скользкий от вечерней росы."
        "И я, не успев даже испугаться, неловко всплеснул руками, утрачивая остатки равновесия."
        window hide
        scene bg ext_clubs_night:
            zoom 1.75 xalign 0.5 yalign 0.15
            linear 10.0 zoom 1.0 xalign 0.5 yalign 0.45
        play sound mpbt
        with flash
    
        "Время встало."
        "Я не успел удивиться или испугаться. Я даже выдохнуть не успел."
        "Я просто смотрел на то, как от меня удаляется край крыши."
        if herc:
            "Я не успел ни среагировать, ни расслабиться в ожидании удара."
        window hide
        play sound sfx_chair_fall
        with flash_red
        scene black
        $ alt_day3_technoquest_st3 = 2
        "Земля вышибла из меня дух."
        pause(1)
        $ renpy.pause(3, hard=True)
    else:
        pass
    return
        
label alt_day3_mt_scare:
    show us smile dress at center with dissolve
    us "Готов к мести за годы притеснений?"
    "Я расхохотался."
    me "Что делать-то надо, народоволец?"
    us "Надо, чтобы она взяла коробок."
    us "Была идея сделать медленный танец с огоньками, но у нас зажигалка только у Алиски."
    me "А если дать ей что-нибудь поджечь?"
    us "Свечки на торте? Последний торт я вчера съела, с тех пор о новых не слышала."
    "Я призадумался."
    me "Как вариант, можно просто оставить его где-нибудь на видном месте — там, где вожатая его точно найдёт!"
    me "На постаменте Генде, например."
    me "Или в туалетах на подоконнике — там попадание сто процентов. Но тогда половина народа шоу не увидит."
    "Мы ещё немного поразмышляли и пришли к выводу, что, действительно, самым оптимальным вариантом будет просто подбросить коробок."
    "Но как сделать так, чтобы его не перехватил кто-нибудь до нас?"
    me "Придётся мне немного подставляться."
    "Её глаза просияли."
    us "Ты чудо!"
    show us laugh dress at center with dissolve
    "Я пожал плечами и взял коробок."
    hide us with dissolve
    "Теперь самое важное — это сделать лицо посерьёзнее. Посложнее."
    "И медленный танец такой уместный. Хай думает, что молодой пионэр хочет пригласить вожатую и втихую провести сравнительный экспресс-анализ её топологии относительно топологий девочек отряда."
    "Я прошёл мимо памятника Генде, невзначай оставив там коробок — угрожающее шебуршание оттуда значительно сбавило в интенсивности, но всё ещё было слышно."
    "И подошёл к вожатой."
    show mt smile dress at center with dissolve
    play music music_list["eternal_longing"] fadein 5
    me "Танцуете, Ольга Дмитриевна?"
    "Она молча протянула мне руку, и во время выдвижения на центр танцпола, сопровождаемый одобрительным свистом и улюлюканьем, я поневоле среагировал вполне ожидаемо — распрямив последнюю треть позвоночника, расправив плечи и спину."
    "В результате с виду крупная вожатая оказалась чуть ниже меня ростом, и я мог видеть её макушку с забавной «звёздочкой», закрученной по часовой стрелке."
    th "Выдает во владельце человека ленивого, но не чуждого авантюрам — лишь бы те были не очень опасными."
    th "Любопытно, сочтёт ли она нынешний розыгрыш опасным?"
    "Я позволил себе улыбку и наклонился к ушку вожатой."
    me "Ольга Дмитриевна, а правда ли то, что на этот вечер планируется салют с фейерверками?"
    show mt smile dress with dissolve
    mt "Неправда. Опять, наверное, Ульянка где-нибудь петард добыла и хочет сделать это легально."
    me "Ох уж эта Ульянка."
    "Я вздохнул не сказать чтобы уж совсем притворно. В отличие от рыжей пакости, лично я сейчас здорово рисковал — в том числе и целостностью рассудка. Когда под предплечьями обнаруживаются такие вот формы, заключённые в самое упругое содержимое…"
    "Голова поневоле начинает работать в несколько другом режиме. И мысль о том, чтобы… кхм… развеселить обладательницу форм таким вот экстравагантным методом, уже не кажется такой уж хорошей."
    stop music fadeout 4
    dreamgirl "Сёма, выдохни и расслабь ягодицы. Это в тебе гормон гуляет."
    me "Значит то, что она что-то там у памятника делала со спичечным коробком, никем не санкционированно?"
    play music music_list["eat_some_trouble"] fadein 2
    mt "Спичечным коробком?! О чёрт. В прошлый раз у них с Алисой не получилось сделать порох потому, что сера была плохая. И если она у них теперь есть…"
    hide mt with dissolve
    "Она, не слушая моих протестов о том, что танец надо закончить, немедленно сняла с себя мои руки и быстрым шагом направилась в сторону постаментов."
    "Я широко осклабился, поймав взгляд Ульянки и оттопырил большой палец."
    "Через пятнадцать секунд в ночи раздался девичий крик… плавно переходящий в вопль ненависти."
    mt " {b}УЛЬЯНААААА!!!{/b} "
    show us grin dress with dissolve
    us "Чё стоишь? Бежим!"
    "И мы побежали!"
    hide us with moveoutright
    window hide
    stop ambience
    "Мы бежали так, будто за нами гонятся все черти с дьяволом во главе."
    "Впрочем, принимая во внимание всю силу гнева вожатой, угроза едва ли была меньшей."
    "Я несколько раз поскользнулся на вечерней росе и чуть не заработал себе асфальтовую болезнь — и спасся ценой ссаженных ладоней и перемазанной травой рубашки."
    window hide
    scene bg ext_boathouse_night with dissolve
    play ambience ambience_boat_station_night fadein 3
    play sound breath fadein 9
    "Выскочив на пристань, я остановился, переводя дух."
    "Ульянка уже упрыгала куда-то в темноте, а я побрёл по мосткам к дальнему углу понтонов — это место я облюбовал ещё в первый день."
    "Некоторое время я стоял в одиночестве, успокаивая дыхания и расслабляясь, а потом доски за спиной скрипнули, выдавая чьи-то шаги, и чей-то голос сказал:"
    stop sound fadeout 4 
    $ meet ('sl', 'Голос')
    sl "Опять балуетесь."
    "Судя по интонации, это было утверждение, а не вопрос."
    "А что поделать. Такова селява."
    "Я пожал плечами, не издавая ни звука."
    sl "Ладно, Ульянка баловница — ей по возрасту положено."
    sl "Но ты, Семён. Взрослый же мальчик."
    "Интонации были знакомые, но на голос не ложились."
    "Я развернулся."
    show sl smile dress with dissolve
    me "Славя."
    $ meet ('sl', 'Славя')
    "Та кивнула."
    sl "В отличие от Ольги Дмитриевны, я точно знаю, где тебя можно искать."
    "Она сказала это с таким намёком, что я поневоле смутился."
    me "Ты меня искала?"
    sl "Не то, чтобы… Просто я видела, как ты упал несколько раз. Подумала, что если не медицинская помощь, то баня тебе точно не повредит."
    me "Баня? Какая интересная мысль!"
    "Она улыбнулась."
    sl "Да. А то из-за того, что души поломались, помыться хорошо негде, разве что купаться постоянно."
    sl "Так ты пойдёшь?"
    "Я кивнул."
    sl "Очень хорошо, тогда я тебе очередь придержу пока. Иди собирайся."
    sl "От домика пойдёшь по тропинке в сторону вторых ворот, там и будет баня. Не промахнёшься."
    "Она помахала мне рукой и убежала."
    return
    
label alt_day3_med_un:
    play ambience ambience_camp_center_night fadein 3
    scene bg ext_aidpost_night with dissolve
    "После нескольких многообещающих взглядов со стороны Слави, я понял, что ближайшим белым танцем станет мой бенефис."
    "А танцевать ни с кем, кроме Лены не хотелось."
    "Поэтому её напоминанию я даже отчасти обрадовался."
    dreamgirl "Пионэр Семён угодил в общество голодных самочек и абсолютно нарасхват."
    dreamgirl "Ну что, Сёма, ощутил себя альфачом?"
    "На самом деле, мне более чем достаточно внимания всего одной девочки… Остальные в такой момент почему-то только раздражают."
    dreamgirl "Да правда, что ли? А у меня тут информация из твоих надпочечников — они утверждают, что ты врёшь."
    th "Я вру?!"
    dreamgirl "А как же. У колхозницы буфера больше, у японки у спинки прогиб такой — сразу видно, что позу «родео» она будет предпочитать все прочим. У Двачевской фигурка вообще эталон фитнеса, помнишь её мускулатуру?"
    th "Причём здесь буфера и фигура, похотливая ты тварь?"
    dreamgirl "Да притом, что с точки зрения производства потомства те девочки перспективнее. Вкуриваешь?"
    dreamgirl "А у этой замарашки кроме романтики  — ничего. Но тебе, похоже, и этого достаточно — вон, как слюни до пупа распустил."
    dreamgirl "Соберись!"
    "Я вздрогнул и последовал совету внутреннего голоса."
    stop music fadeout 3
    "Тем более, что мы почти добрались до места."
    window hide
    with dissolve
    play music music_list["goodbye_home_shores"] fadein 3
    with dissolve
    scene bg ext_aidpost_night
    with dissolve
    play ambience ambience_camp_center_night fadein 3
    "Приземистое здание медпункта почти ничем не освещалось в темноте, и только то, что я уже ходил сюда несколько раз раньше, не дало мне со всего маху стукнуться об угол."
    "Это, да ещё то, что Лена в последний момент успела поймать меня за руку, сберегло меня от новой травмы."
    "Похоже, она в темноте видела куда лучше моего, поэтому я медленно обернулся и сказал:"
    me "Знаешь, что. Иди-ка ты первой. Мне, конечно, приятна роль защитника и всё такое, но не тогда, когда я ничего не вижу."
    "Она сдержанно хихикнула, и я пошёл на звук."
    window hide
    scene black with fade
    play sound sfx_open_door_2
    "Впереди меня что-то несколько раз щёлкнуло — видимо, поворачивался ключ, а потом с тихим гудением разгорелась лампочка дневного света."
    "Наконец-то я могу оглядеться!"
    window hide
    $ persistent.sprite_time = "day"
    scene bg int_aidpost_night
    with flash
    play ambience ambience_medstation_inside_night fadein 3
    show un normal dress at center 
    with dissolve
    "Прямо от входа я бросил взгляд на столик-картотеку, за которым Виола работала в прошлый раз, когда я пришёл."
    "Она что-то говорила про верхний левый ящик?"
    "Про какое-то средство от головной боли?"
    if alt_day_binder == 1:
        "Я вдруг вспомнил, что именно лежит в ящичке."
        "Его же Мику открывала, и там…"
        "Я подавил смешок."
    else:
        "Но у меня не болит голова! И тогда не болела."
        "Ладно, сначала дела, всё остальное потом."
        "Лена, похоже, тоже вспомнила о словах медсестры, но настаивать не стала и, судя по вопросительному взгляду, предоставила мне инициативу."
    th "Что ж, значит, работаем."
    me "Итак, с чего начнём?"
    "Она молча указала на целый ворох коробок, абы как составленных по ту сторону медицинской ширмочки."
    "Мда… Когда речь шла о помощи, я предполагал несколько более скромные объёмы работы."
    me "А… куда?"
    un "Наверное, в шкаф."
    "Она показала на незамеченный ранее шкафчик с медикаментами."
    "Я нашёл, что трудно поверить в то, что содержимое сразу десятка больших коробок может поместиться в такой небольшой шкафчик."
    "Но Лене, кажется, было виднее."
    me "Не впервой, да?"
    show un smile dress with dissolve
    "Она улыбнулась и молча кивнула."
    me "Ну что ж, тогда… Погнали!"
    "Она протянула мне листок и ручку, но я демонстративно сложил руки на груди:"
    me "По части писанины будешь ты. С моим ужасным почерком я всё испорчу."
    "Она секунду посомневалась, будто не очень доверяя мне в таких вещах, и кивнула."
    "Полагаю, поверила в мои силы? Эх, мне бы теперь того же самого!"
    show un normal dress at center with dissolve
    "Я потрошил коробки, вытаскивал оттуда содержимое — все эти стерильные упаковки бинтов, ваты и марли, какие-то ампулы, вездесущий лейкопластырь, целая коробка бриллиантовой зелени и такое же количество йода."
    th "Понятное дело, что асфальтовой болезнью пионеры болеют чаще, чем запором, но куда такое количество?!"
    "Описанным йодом можно было в два слоя покрасить всех моих знакомых… и ещё на всякие глупости осталось бы."
    "Лена расписывала учётки в инвентарную книгу и указывала, куда разбрасывать уже описанное."
    "Львиная доля всего медицинского барахла прочно угнездилась в недрах шкафчика, оказавшегося поистине бездонным!"
    "Работа шла споро, и я стал понимать, что работай мы раздельно, нынешний объём мы бы не завершили и к полуночи."
    th "Кажется, Виолетта Церновна знает что-то, чего не знаю я."
    un "Семён…"
    show un normal dress at center with dissolve
    me "Да?"
    "Я немного отвлёкся от монотонного процесса и улыбнулся Лене."
    "Девочка не спешила отвечать. Вместо этого она как-то очень серьёзно уставилась мне в глаза, будто решая для себя что-то."
    th "После того, что она мне у склада наговорила, решать что-то уже поздно. Всё уже решено!"
    "Я же здесь. Неужели трудно догадаться."
    show un shy dress at center with dspr
    un "А что мы будем делать, когда закончим?"
    "Я кинул взгляд на часы — до отбоя было ещё порядочно времени, весь контингент сейчас на площади, пытается проломить бетонку пятками."
    "И это значит, что мы остались с ней совсем одни!"
    show un normal dress at center with dspr
    me "Не знаю."
    "Я пожал плечами."
    me "Есть какие-нибудь пожелания?"
    if alt_day3_duty:
        me "Можем забраться в столовку и украсть по мороженому. Ты как насчёт мороженого?"
        "Она покачала головой."
    un "М-может, погуляем?"
    me "Погуляем, конечно. Или ты думала, что после такого дня мы распрощаемся и разбежимся?"
    show un shy dress with dissolve
    un "Я не знаю."
    "Прошептала она."
    un "А чего тебе хочется?"
    "Выложить ей как на духу, как я сегодня после завтрака стоял у информационного стенда и прикидывал, где ещё, кроме библиотеки и собственного домика, можно найти её?"
    "А такие вещи разве можно говорить?"
    me "Я не знаю. Потанцевать мне не удалось, да и не слишком я это дело жалую. Так что силы ещё есть."
    return
label alt_day3_un_cards:
    "Немного помолчав, она вдруг оглянулась — как будто кто-то мог нас видеть."
    play sound sfx_close_cabinet
    "…Подошла к двери и заперла ту на ключ."
    "И, отодвинув верхний ящик у рабочего стола медсестры, сделала приглашающий жест."
    "Я подошёл и заглянул."
    "Ничего необычного — платки, бумажки, медицинские карточки… Стоп!"
    "Под бумажными завалами обнаружился прямоугольный предмет, на котором в самом вызывающем наряде и с самой вызывающей мордашкой была изображена горделиво стоящая мамзель."
    me "Карты."
    un "Да…"
    me "С бабами!"
    "Она покосилась на меня, и я поспешил поправиться:"
    me "То есть, с девушками."
    dreamgirl "Аааа, карты с голыми бабами!"
    me "Ты хочешь сыграть?"
    un "Не знаю. Я не очень хорошо в карты…{w} А ты?"
    me "Ну, я знаю пару игр. Тысчонку, там, холдем. Буру, опять же."
    un "А я только в дурака умею."
    un "Ну ещё в эту… Которую Электроник придумал."
    dreamgirl "Надеюсь, она понимает! Что {b}такими{/b} картами играют только на раздевание?!"
    th "Судя по взгляду, прекрасно понимает! Более того, её совершенно не пугает перспектива проигрыша."
    un "Будешь?"
    "Я прикинул оставшийся фронт работ и пожал плечами."
    "Дел и правда оставалось минут на десять, не больше. А судя по тому, как зажигали в уголке обслуживающего персонала, Виола явно не собиралась в скором времени возвращаться."
    "Но на всякий случай я решил внести ясность. Чтобы уж точно недосказанностей не оставалось."
    me "Ты же понимаешь, что такими картами на интерес не играют?"
    "Она опять зацвела как маков цвет."
    show un surprise dress at center with dspr
    un "Я бы сыграла…"
    "И тем не менее, продолжает настаивать!"
    me "Хорошо, как скажешь. Давай тогда закончим и сыграем."
    "Она улыбнулась и потупила глаза, пряча за ними азартный блеск."
    dreamgirl "Хе-хе, чувак! Кажется, тебя собираются раздеть!"
    th "Кто кого ещё разденет."
    window hide
    show blinking

    "В предвкушении игры, мы во мгновение ока разобрали оставшуюся коробку и, запершись изнутри, уселись на кушеточку."
    show un smile dress at center with dspr
    un "Ну что, азартный вьюнош, готов к поражению?"
    me "Конечно, готов. Жду не дождусь узреть тебя в самом что ни есть неглиже."
    "Она запрокинула голову и свободно расхохоталась, как будто остальные сдерживающие рамки, что ещё сохранялись, только что моргнули и утратили материальность."
    "Она прикрыла глаза и быстро-быстро сдала по шесть карт."
    "Игра началась!"
    "Мы немного поспорили из-за ставок, но решили, что для справедливости стоит установить определённый порядок без возможности отыграться."
    "Говоря проще — в плюс отыгрываться нельзя, игра идёт только в минус."
    "Кажется, нам обоим было интересно, что скрывает одежда другого."
    $ alt_day3_un_strip_pool_sp = 5
    $ alt_day3_un_strip_pool_un = 5
    play music music_list["eternal_longing"] fadein 5
    return

label alt_day3_un_strip_play:
    window hide
    menu:
        "Играть!":
            $ INVISIBLE = False
            python:
                dialogs = {
                                (0, 'win','jump'):'alt_day3_un_strip_play_win',
                                (0, 'fail','jump'):'alt_day3_un_strip_play_fail',
                                (0, 'draw','jump'):'alt_day3_un_strip_play_draw'
                            }
                generate_cards('bg int_aidpost_night', dialogs)
                rival = CardGameRivalUn(un_avatar_set, u"Лена, игра на раздевание")
            jump cards_gameloop
        "Сдаться!":
            $ alt_day3_un_strip_pool_sp = 0
            return
        "Посмотреть ставки":
            "Я быстро окинул взглядом себя и свою оппонентку."
            if alt_day3_un_strip_pool_sp == 5:
                "Дела шли более чем замечательно! У меня был полный комплект одежды."
            elif alt_day3_un_strip_pool_sp == 4:
                "В целом, дела идут неплохо. Я попрощался с рубашкой, но остальное ещё при мне."
            elif alt_day3_un_strip_pool_sp == 3:
                "Вслед за рубашкой ушла обувь, и у меня осталось всего три предмета одежды."
            elif alt_day3_un_strip_pool_sp == 2:
                "В спину дохнуло предчувствием проигрыша. С шортами пришлось попрощаться, остался один предмет."
            elif alt_day3_un_strip_pool_sp == 1:
                "Финита ля комедия. Я сидел напротив Лены в чём мать родила и старался делать вид, что не замечаю её взгляда."
            if alt_day3_un_strip_pool_un == 5:
                "Лена была полностью одета и с улыбкой наблюдала за мной."
            elif alt_day3_un_strip_pool_un == 4:
                "Лена пока отделалась только обувью."
            elif alt_day3_un_strip_pool_un == 3:
                "Интересный момент! Опасный, я бы даже сказал. Она поставила платье на кон и проиграла."
            elif alt_day3_un_strip_pool_un == 2:
                "Ух! От перспектив дух захватывает! Она осталась в одних трусиках и играла, прижав левую руку к груди."
            elif alt_day3_un_strip_pool_un == 1:
                "Ситуация явно складывалась не в пользу Лены. Она сидела передо мной совершенно обнажённая, а я млел и пускал слюни."
            window hide
            jump alt_day3_un_strip_play
            
label alt_day3_un_strip_play_fail:
    scene bg int_aidpost_night with dspr
    if alt_day3_un_strip_pool_sp == 5:
        $ alt_day3_un_strip_pool_sp = alt_day3_un_strip_pool_sp - 1
        "Я улыбнулся и без сожалений стащил с себя рубашку. Жалко, что ли?"
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_sp == 4:
        $ alt_day3_un_strip_pool_sp = alt_day3_un_strip_pool_sp - 1
        "Сандалии отправились вслед за рубашкой. Забирай!"
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_sp == 3:
        $ alt_day3_un_strip_pool_sp = alt_day3_un_strip_pool_sp - 1
        "Вот с шортами расставаться было трудно. Но карточный долг — дело святое, и я стащил с себя несчастные шорты."
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_sp == 2:
        $ alt_day3_un_strip_pool_sp = alt_day3_un_strip_pool_sp - 1
        "Нет, ну это надо как она меня раздела! Я покраснел как рак и прикрываясь чем мог, быстро стащил труселя, оставшись в чём мать родила."
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_sp == 1:
        $ alt_day3_un_strip_pool_sp = alt_day3_un_strip_pool_sp - 1
        jump alt_day3_un_strip_play
    else:
        return

label alt_day3_un_strip_play_draw:
    $ show_cards()
    "Никому не повезло, переигрываем."
    window hide
    jump alt_day3_un_strip_play
    
label alt_day3_un_strip_play_win:
    scene bg int_aidpost_night with dspr
    if alt_day3_un_strip_pool_un == 5:
        $ alt_day3_un_strip_pool_un = alt_day3_un_strip_pool_un - 1
        show un smile2 dress with dissolve
        "В угол полетели туфельки."
        "Кажется, поражение только раззадорило девушку."
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_un == 4:
        $ alt_day3_un_strip_pool_un = alt_day3_un_strip_pool_un - 1
        show un grin dress with dissolve
        "Немного поколебавшись, она завела за спину руку…"
        "И будто спохватившись, развернулась ко мне спиной."
        un "Расстегни, пожалуйста."
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_un == 3:
        $ alt_day3_un_strip_pool_un = alt_day3_un_strip_pool_un - 1
        "Кажется, она поняла, что я настроен серьёзно, потому что лукавства в глазах вдруг стало через край."
        un "Давай, победитель."
        "Она опять развернулась спиной."
        un "Забирай трофей."
        show un surprise body close with dissolve
        "Трясущимися пальцами я расстегнул застёжку, вспомнив вдруг, что в некоторых кругах считается особым шиком расстёгивать это кошмарное крючкасто-петлястое устройство одной рукой — за несколько секунд."
        "Она осталась в одних трусиках."
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_un == 2:
        $ alt_day3_un_strip_pool_un = alt_day3_un_strip_pool_un - 1
        show un sad body close with dissolve
        "Бросив на меня разгневанный взгляд, она чуть привстала, не отнимая левой руки от груди, одним неуловимым движением спустила последний предмет одежды."
        "…на бёдра…"
        "…села сама…"
        "И потянула дальше, между делом закидывая ногу на ногу."
        "…миновала колени, дошла до голеней."
        "Я судорожно вздохнул, и она, рассмеявшись, одним движением, сняла трусики и бросила их тут же на пол."
        un "У меня последний шанс отыграться."
        un "Я просто так не сдамся!"
        window hide
        jump alt_day3_un_strip_play
    elif alt_day3_un_strip_pool_un == 1:
        $ alt_day3_un_strip_pool_un = alt_day3_un_strip_pool_un - 1
        return
    
label alt_day3_card_lose:
    scene bg int_aidpost_night with dissolve
    show un smile dress with dissolve
    "Да, эта девочка раскатала меня как хотела."
    if (alt_day2_round3 != 0) and (alt_day2_f1 == 1):
        "Ничего другого от финалистки я и не ожидал, если честно."
    else:
        "Уж не знаю, почему вчера она показала невысокий класс игры — сегодня она зверствовала."
    "Она тихонько рассмеялась, а я в ответ смущённо улыбнулся."
    me "Поздравляю. Ты выиграла. Ты и правда выиграла."
    un "Чем дальше займёмся?"
    dreamgirl "О, у меня есть пара вариантов в духе дойче синема."
    dreamgirl "Не хочешь попробовать? Девочка, похоже, не против."
    th "Уймись, чудовище озабоченное."
    "Я улыбнулся Лене."
    me "Не знаю."
    "Она кинула ещё один взгляд на меня, будто сомневаясь… И бросила в меня формой."
    show un laugh dress with dissolve
    un "Одевайся уж… проигравший!"
    $ lp_un += 1
    return
    
label alt_day3_card_won:
    scene bg int_aidpost_night with dissolve
    show un sad body close with dissolve
    "Отыграться ей не удалось."
    "Да я бы и не позволил."
    "А она подумала и, рассмеявшись, отняла руки от груди."
    "Моя челюсть с лязганьем приземлилась на пол."
    show un grin body close with dissolve
    un "Ну что, насмотрелся?"
    "Немного нервным голосом сказала она."
    "Это она меня стесняется? Серьёзно?!"
    dreamgirl "Есть мнение, что она стесняется того, кто вдруг заглянет на огонёк. Дверь хоть и заперта, но окна…"
    un "Можно мне теперь одеваться?"
    me "А хочется?"
    "Она пожала плечами."
    un "Я тебя почему-то совсем не стесняюсь."
    dreamgirl "Сём, если у тебя есть непреодолимое желание обеспечить отдельно взятую демографическую вспышку, сейчас самое время."
    th "Ты дурак, да? Конечно, у меня есть непреодолимое желание. Но, чёрт возьми, я лет на девять её старше. К тому же, она несовершеннолетняя."
    dreamgirl "Тебе напомнить возраст согласия?"
    th "Не надо. Не дурак."
    if alt_day3_un_strip_pool_sp < 4:
        "Она немного продрогла, судя по тому, как зябко обнимала себя за плечи."
        me "Пойдёшь? Греться?"
        "Я с намёком кивнул себе на плечо."
        me "Или оденешься?"
        un "Пожалуй, оденусь."
        "Она собрала свои вещи и разворачивая платье, тихим голосом добавила:"
        un "Жаль, мы не у меня. Пошла бы…"
    return

label alt_day3_post_strip:
    hide un with dissolve
    if persistent.un_7dl_good_ussr or persistent.un_7dl_good_rf:
        "Я отдал ей одежду, а сам между делом решил заглянуть туда, куда нам советовала заглянуть сестра."
    else:
        "Последовательностью действий, я решил заглянуть туда, куда советовала заглянуть медсестра."
    "Левый верхний ящик конторки был полон серых бумажных пакетиков."
    "На той стороне, что была видна, кроме трёх букв, ничего больше не значилось."
    th "ОТК? Значит, Союз за качество отвечает?"
    "Я взял один пакетик, перевернул, и взгляд сам заскользил по буквам."
    "Под стилизованным изображением работающего завода значилось «Баковский республиканский завод резиновых изделий»."
    "Изделие №2. Гост 4645-49"
    show un shy dress with dspr
    if alt_day3_un_strip_pool_un < 5:
        "Лена уже привела себя в порядок и заглянула мне через плечо."
    else:
        "Она незаметно подошла и встала рядом, читая через плечо."
    un "Резиновое изделие номер два? Это же…"
    if loki:
        me "Ага, они самые. Молодец Виола, да?"
        me "Вот уж действительно половина головной боли долой."
    elif herc:
        me "Ну а чего ты ещё ожидала от нашей медсестры."
        me "Молодец хоть тюбики с лубрикантом не подсунула вкупе с весёлыми видео."
    elif not (herc or loki):
        un "От детей, да?"
        "Я молча кивнул, чувствуя как язык снова примерзает к гортани."
        "Сестра дура. Я, конечно, понимаю, что плотские отношения — суть любви, но, блин, должно же пройти какое-то время. И не два дня!"
        un "Я не знаю что сказать."
        "Я тоже."
        "Видно было, что ей и плакать и хохотать одновременно хочется от неловкости ситуации."
        me "Дурацкий врачебный юмор."
        me "Не держи зла на идиотку, она, видимо, родилась старой."
    "Ассоциации продолжить нетрудно. От «Янитакая» до закушенной губы и утробно-горлового «Здесь. Раздевайся!»."
    "Девочка распробовала эротический контекст, девочке понравилось."
    show un surprise dress at center with dspr
    un "А ты что думаешь о…"
    "Что я думаю? О, да так, о том, что кое-кому противопоказано лезть в личные дела пионеров."
    "Видимо, дисциплину половых отношений Виола почитает столь же тонким делом, как кнутобойство."
    me "У меня на латекс аллергия."
    "Автоматом сорвалось с языка."
    me "Хотя здесь резина же."
    "Она заинтересованно уставилась на меня."
    show un smile dress at center with dspr
    un "Так ты…"
    me "Эй-эй, я ничего такого не говорил!"
    "Я в панике замахал ладонями перед собой."
    un "Какие интересные подробности о нашем молчаливом Семёне."
    me "Да нет никаких подробностей. И вообще…"
    me "Это очень личное."
    show un laugh dress at center with dspr
    un "О, безусловно!"
    un "Но вот я, например, не знаю, есть ли у меня аллергия на латекс. А ты откуда знаешь?"
    show un normal dress at center with dspr
    if loki:
        "Я расхохотался."
        me "Примерял! Как Золушка туфельку."
        me "Вдруг пригодится!"
        un "И?"
        show un serious dress with dissolve
        "Я покачал головой."
        me "Не пригодилось."
    "Я спохватился, что до сих пор верчу несчастный пакетик в руках и поспешил убрать его обратно в ящик."
    "Лена проводила изделия заинтересованным взглядом. Похоже, она подумывала о том, чтобы взять парочку про запас, поэтому я поспешил среагировать:"
    me "Ну что, похоже, мы здесь закончили? Пошли?"
    show un smile dress at center with dspr
    un "Пошли."
    "Она подошла к двери и повернув ключ, открыла."
    "Всё-таки, не сильно она и соврала, когда сказала, что она деть."
    "Поиграла с идеей и отбросила, наигравшись."
    show un laugh dress at center 
    with dspr
    "Вдруг она засмеялась."
    me "Всё смеёшься…"
    un "Почему нет. У тебя лицо такое…"
    me "Какое?"
    un "Забавное."
    me "Да?…"
    show un smile dress at center with dspr
    un "Самый глупый вопрос из тех, что только можно задать. Ну, мы пошли?"
    me "Куда?"
    "Она заколебалась между желанием стукнуть меня по лбу и своей обычной реакцией на бестолковость окружающих, и я поспешил прийти ей на помощь."
    me "Ты о танцах?"
    show un shy dress at center with dspr
    un "А о чём. Но если ты не хочешь…"
    "Она мгновенно отморозилась и покраснела."
    dreamgirl "И правда! Ишь, хрень какую придумала. Танцы ей! Может, её ещё на руках нести. Раздевайся!"
    th "Достал. Ещё одна реплика в таком духе — и я брому обожрусь, понял? Я найду."
    dreamgirl "Всё, молчу-молчу."
    me "Помнишь первый день нашего знакомства? Я сказал тогда, что мне без разницы чем заниматься, если ты составишь мне компанию."
    me "Если это когда-нибудь изменится — я тебе скажу."
    th "Выкрутился, ага?"
    "Она так и стояла у двери, будто готовая смириться с моим решением."
    "Так что вполне логично было подойти и обнять её."
    me "Правда, в этот раз без музыки. Ничего?"
    show un normal dress at center with dspr
    stop music fadeout 3
    un "Ничего."
    window hide
    stop ambience fadeout 3
    $ persistent.sprite_time = "night"
    scene bg ext_aidpost_night
    with dissolve
    play ambience ambience_camp_center_night fadein 3
    "Мы вышли из здания медпункта и, закрыв его на ключ, остановились посовещаться — куда можно пойти, чтобы нас не нашли хотя бы некоторое время."
    me "Может, опять"
    if alt_day2_date == 1:
        extend " на спортивную площадку?"
    elif alt_day2_date == 11:
        extend " к твоему любимому месту?"
    else:
        extend " к складам?"
    "Она отрицательно покачала головой."
    un "Пошли на пристань? Там сейчас красиво очень."
    show un normal dress at center with dissolve
    "И никто туда просто так не сунется."
    "В логике этой девочке не откажешь!"
    me "Мысль разумная. Пошли."
    "Она тут же вцепилась мне в мизинец.{w} Кажется, её это почему-то радует до какого-то совершенно непонятного писка."
    "Мы решили немного срезать путь, обогнув площадь по тропинке через негустые заросли, чтобы уж точно никто не встретил."
    me "Мы теперь так будем ходить, да? Вот так, за мизинчик?"
    show un shy dress with dissolve
    un "Прости, тебе не нравится? П-прости…"
    play music music_list["confession_oboe"] fadein 3
    window hide
    scene cg d3_un_forest
    with dissolve
    un "Давай тогда по-взрослому, под ручку?"
    "Эта девочка и смешила, и умиляла."
    un "Будем как большие ходить, да?"
    "Я рассмеялся."
    me "Меньше дискретности, я просто спросил."
    me "Мне и за мизинчик удобно ходить — ты вон какая крохотная."
    un "Ничего я не крохотная! "
    play sound sfx_tree_branches
    extend "Ой, мамочки."
    "Она опять вцепилась мне в руку."
    me "Не крохотная, да."
    "Я остановился, и, повернувшись к ней, обнял."
    me "Вот я тебя обнял, и теперь ты будешь искать выход два дня."
    "Она чуть слышно рассмеялась."
    "А я расслышал плеск бьющей в берег воды."
    stop ambience fadeout 2
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_boathouse_night with dissolve
    play ambience ambience_boat_station_night fadein 3
    show un normal dress at center with dissolve
    "Я взял её за руку и вывел навстречу прекрасной ночи."
    "От неба под ноги нам простлалась серебряная лунная дорожка. {w} Будто софит, поймавший актёра на сцене, она сфокусировала внимание целого мира на нас."
    "На мне."
    "И девочке, прижавшейся в каком-то совершенно необъяснимом порыве, в сумасшедшем танце под шум волн."
    if herc or loki:
        "Как будто я и в самом деле могу отлистать решения назад и предпочесть ей что-то другое."
        "Да как я могу?"
        "Этому миру, в какую сторону его не верти, просто нечего предложить равноценного взамен этому вечеру, Луне и Лене, пританцовывающей в такт биению сердца и шороху прибоя."
    stop ambience fadeout 2
    show un smile dress at center with dspr
    un "Ты такой серьёзный сейчас. Иногда мне кажется, что я тебя совсем не знаю."
    un "И взгляд такой, как будто ты знаешь, что очень ненадолго здесь — вот и спешишь жить и дышать тем временем, что ещё осталось."
    "Она положила руку мне на плечо."
    "Нашла правой рукой мою руку и переплела пальцы."
    window hide
    scene cg d3_un_dance
    with dissolve
    play music music_list["a_promise_from_distant_days"] fadein 3
    "Скоро и я поддался очарованию момента, непроизвольно раскачиваясь в такт импровизированной музыке слов, пульса и очарования ночи."
    un "Ты сегодня целый день дурачился, вел себя как маленький ребёнок."
    un "А я вдруг поняла, что это не стыдно, не зазорно — вести себя именно так, как хочется."
    un "Если душа просит песни, скакать вприпрыжку или корчить рожицы окружающим — делай это, будь чуточку непосредственнее."
    me "Люди могут не понять."
    un "Но ведь это их проблемы, правда?"
    "Как меня эта однорукая поза достала."
    "Я не ответил и, освободив левую руку, положил её на спину девочке."
    "Мы будем танцевать так, как привычно мне… Так, как привычно моему сердцу."
    "Лена оказалась очень умной девочкой — и почти мгновенно приняла правила игры, уместив ладошки у меня на плечах, где они были наиболее уместны."
    "Как уместны они оказались несколько секунд спустя, сцепившись у меня на затылке, притягивая поближе."
    if loki:
        "Волшебство второго поцелуя в том, что ты до конца не веришь в реальность первого."
    "Я редко когда закрываю глаза, если целую любимого человека — во всяком случае, так было во время немногих моих романов."
    "И она не стала закрывать."
    "Мы смотрели друг на друга и общались касаниями губ, отстранившись только тогда, когда в ушах тонко запело от кислородного голодания."
    un "Хорошо…"
    "Я молча кивнул."
    "А она возобновила монолог."
    un "Самое притягательное и самое отвратительное в тебе то, что ты обязателен для того, чтобы мои мысли стали явью."
    un "Как с этими дурачествами. Я понимаю, что человек должен улыбаться хотя бы раз в сутки, чтобы не пустить себе пулю в висок."
    un "Я понимаю, что иногда надо просто давать выход эмоциям — в слезах, творчестве или смехе."
    un "Но я смотрю на равнодушные лица окружающих, и понимаю, что — не смогу."
    un "Даже если плохо и больно очень — не смогу. И когда запас горечи внутри достигнет контрольной отметки…"
    "Я молчал, кружа её в танце, и жутко было, и холодно оттого, насколько её мысли похожи на мои собственные."
    "Человек, который растормаживает, который служит той самой точкой приложения силы, само присутствие которого позволяет смеяться и дышать."
    me "Я не могу пообещать, что я и завтра буду. Я не гарантирую даже ближайших нескольких секунд."
    me "Но пока я здесь — я не хочу отпускать руки."
    me "Это всё, что я могу сказать с точностью, и всё, что я могу обещать."
    un "Этого уже достаточно."
    me "Для чего?"
    "Она положила голову мне на плечо и промолчала."
    dreamgirl "Небольшая тайна, мог бы и сам догадаться. Девочка просит хоть каких-нибудь гарантий взамен на вообще всё, что у неё есть или когда-нибудь будет."
    "Митчелл, Бернетт, Грин… Правильно, что они могли взрастить в молоденькой головушке, даже с поправкой на высокий коэффициент интеллекта."
    "Девочке хочется романтики, хочется абсолютов применительно к чувствам. А я… Блин, я даже сейчас не на все сто процентов уверен, что {i}и в самом деле{/i} чувствую что-то, а не притворяюсь перед самим собой."
    dreamgirl "Как и при любой другой болезни. Пока тебя не скрутит, ты будешь сидеть перед монитором, чувствуя себя последним симулянтом."
    "С неба скатилась звёздочка."
    me "Падающая звезда. Загадывай, пока падает."
    dreamgirl "Несколько граммов твёрдых веществ, раскалившихся в результате трения в плотных слоях атмосферы. Причём здесь желание?"
    th "Дешёвый кухонный цинизм всезнайки в майке и трениках с пузырями на коленях."
    th "Воплощение идиомы «потребляй, работай, сдохни»."
    th "Но ты не дохни пока. Ты пригодишься мне тогда, когда я размякну окончательно, и мне — по морде, по тупой! — выбросом обратно в мой ненаглядный мир."
    un "Мне кажется, что не было никакого лагеря или города до тебя. Что я всё это придумала, чтобы хоть как-то оправдать своё существование."
    me "А ещё ты меня нарисовала. А я не сумел перелистнуть страницу с сюжетом где ты. Рука не поднялась."
    "Она вжалась в меня, будто боясь, что я и правда сейчас пропаду."
    un "Нет прошлого, нет настоящего. Я не строю планов, моя психика их не приемлет."
    un "Но грустна и смешна паника о том, что я не знаю, как сейчас отпущу руки и несколько часов проведу без тебя."
    un "Я просто не знаю. Не представляю себе."
    "Я поцеловал её вместо ответа."
    th "В конце концов, возможен ведь и марафон не расцепляя рук. Сколько у меня ещё времени здесь? Часы? Недели?"
    "Я закрыл глаза."
    "А я вспоминаю собственные сделки с судьбой, где ставкой всегда становилось что-то крайне важное и нужное."
    "Двенадцать лет — первая влюблённость. Я называл её кошкой за её забавную мордочку с хитринкой в глазах."
    "Три года я кроил реальность, расплачиваясь временем и упущенными возможностями."
    "Такие дурацкие совпадения — два ребёнка на улицах мегаполиса, шанс повторной встречи равен нулю."
    "Нулю? Ага."
    "Мы познакомились на одном из детских концертов, откуда и началась вся эта катавасия."
    "Сначала выступление на одной сцене, потом совместное выступление, потом для дуэта — я так и не решился развить очевидный и безусловный обоюдный интерес во что-то большее."
    "Мы даже попали в один лагерь! И первую смену спали в разных корпусах, но всё равно виделись каждый день, а во вторую смену оказались в одном отряде."
    "Дошло до смешного — в результате мы спали через стеночку и при желании могли перестукиваться или перебрасываться записками романтического содержания через форточную почту."
    "Я так ни разу и не стукнул в ту стену."
    "Пятнадцать лет — влюблённость номер два, подробности истёрлись из памяти, она на год меня младше, и мы дружим, смеёмся и шутим друг над другом, что выдумали друг друга друг для друга."
    "Мы репетируем совместную песню, и мне не нужен микрофон для того, чтобы звучать вровень с её тихим мягким голосом."
    "А у меня впервые получаются рифмованные строчки — и половина из них пишется с её лицом перед глазами."
    "И я почему-то совершенно не удивился тому, что когда меня увезли на скорой с температурой под сорок, именно она была первой, кто примчался узнавать, не помер ли я часом."
    "Правда, выходя из больницы, она целовала человека, которого я всерьёз считал своим другом. Дело житейское."
    "На душе табличка «do not disturb»."
    "Не потому ли я дёрнулся сейчас как подсечённый, разглядев всесокрушающую эмоцию в огромных зелёных глазах?"
    "Идеальное ведь сочетание — мне всегда нравились брюнетки, и я всю жизнь питал слабость к зеленоглазым людям."
    "Возможно, это лебединая песня уставшего сердца. Дальше останется только брать, что дают. Или сычевать."
    show blinking
    stop music fadeout 3
    play ambience ambience_boat_station_night fadein 3
    me "Мне кажется, что я тебя…"
    mt "Семён! Семён, ты где?"
    "Дура. Ты мешаешь. Спрячься обратно в ту дыру, где находилась последние несколько часов."
    "Я неохотно открываю глаза."
    mt "Семён!"
    "Отрицательный показатель характеристики «Удача» в действии."
    "Мы стоим так плотно, что нас можно принять за один силуэт."
    th "Можно попробовать спрятаться."
    th "Хотя куда спрячешься на причале?"
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_boathouse_night
    with dissolve
    show un normal dress at center 
    with dissolve
    "Надеюсь, она не выйдет на берег. Не нужно это."
    "Силуэт прекрасно виден в лунном свете, и если он вдруг распадётся на двух отдельных людей…"
    me "Тихо… Может, пронесёт."
    "Лена молча кивнула, а глаза у неё такие напряжённо-радостные. Сумасшедшие."
    "Не знаю что на неё нашло, но я глазом моргнуть не успел, как её ладошки вовсю хозяйничали у меня под рубашкой."
    th "Что она творит?! Нам и так солоно придётся, если застукают!"
    th "А если ещё и в такой интересной ситуации…"
    "Впрочем, все свои яростные мысли я думаю, почему-то даже не двигаясь с места."
    show un smile dress with dissolve
    "Я вспыхнул как спичка, весь передёрнувшись от щекотной ласки."
    "Это не осталось незамеченным, так как девочка у меня в руках очаровательно прикусила уголок нижней губки и с удвоенным энтузиазмом принялась лазить кончиками пальцев и коготков там, где ей лазить не следовало бы!"
    show un normal dress at center with dissolve
    "И всё это под аккомпанемент воплей нашей дурной вожатой."
    me "Ты сумасшедшая. Ты в курсе?"
    stop ambience fadeout 3
    un "Да…"
    me "Подожди хотя бы, пусть вожатая уйдёт!"
    un "Так в этом самый смак!"
    window hide
    $ persistent.sprite_time = "night"
    with dissolve
    play ambience ambience_forest_night fadein 3
    show un normal dress at center with dissolve
    "И почему мне достаются только полоумные бабы?"
    "Почему не может быть тихони-скромницы, глазки в пол, крылышками бяк-бяк — мир и покой в доме?"
    "Я усмехнулся, вспомнив своё первое впечатление о Лене."
    "Собственно, ей и предполагалось быть тихоней. Но что-то пошло не так, и заверте…"
    "Вскоре голос вожатой стих в потёмках, и Лена, расхохотавшись, впилась мне в губы жадным требовательным поцелуем."
    "Тихоня, да. Скромница."
    "Она нащекотала мне бока своим безыскусным маникюром как хотела."
    "Она разогнала все запасы тепла из мышц табунами мурашек."
    "И только необъяснимо платоническая влюблённость — та самая, которая не дала мне соблазнить Лену в медпункте — позволяет мне не проявлять своего истинного отношения к тому, когда по живой коже движутся подушечки чьих-то пальцев."
    un "С тобой так приятно сходить с ума."
    "Она была молода, довольна жизнью и пьяна от любви."
    un "Мы должны делать это почаще."
    "Она поставила вслепую на незнакомого человека — и проиграла."
    "Лена. Сказала, что её зовут Лена."
    show un smile dress with dissolve
    un "Пойдём, я провожу тебя до твоего домика."
    un "Или, может, попросим Мику переночевать в клубе?"
    me "Прекрати. Просто прекрати."
    "Голова заболела. Кажется, местная реальность где-то перепрыгнула на совсем не те рельсы, что должна была."
    me "Даже лекарство в больших дозах яд. А пресыщение друг другом опаснее стократ."
    me "Я тебе надоем. Очень быстро. Тем более, что не слишком-то я интересный человек."
    "Она готова была оспорить каждую мою фразу, побуквенно, потезисно. Но промолчала."
    "Правильно, нужно же что-то оставить и на завтра."
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_house_of_mt_night_without_light
    play ambience ambience_camp_center_night fadein 2
    show un grin dress with dissolve
    play music music_list["a_promise_from_distant_days"] fadein 5
    "Вскоре мы добрались до моего домика и долго-долго не хотели расходиться."
    "Голодные касания рук, губ, взглядов — кажется, невозможно насытить эту прорву ничем…"
    "Помогли заскрипевшие за спиной доски и выглянувшая на огонёк Ольга."
    mt "Семён, ты?"
    "Поинтересовалась она."
    "Ей приходилось смотреть из освещённого места в потёмки, поэтому разглядеть меня не могла."
    me "Я."
    mt "Так заходи."
    "Она скрылась в домике, и мне пришлось силой уводить себя."
    me "Спокойной ночи."
    show un grin dress with dissolve
    un "Сладких снов."
    hide un with dissolve2
    "Лена улыбнулась мне на прощанье и убежала."
    return
    
label alt_day3_bath_voyeur:
    scene bg ext_bathhouse_night
    with dissolve
    play ambience ambience_forest_night fadein 3

    $ alt_day3_sl_bath_voy = True
    "Найти баню оказалась несложно. Выглядела она скорее как избушка, чем как место, где можно очистить тело и мысли."
    "В любом случае, густой дым, валящий из трубы, исключал двоякие толкования." 

    "Осталось разобраться в том, кто же там сейчас находится."
    "А то конфуза потом не оберёшься — будто мало мне глупостей, которые происходят со мной без моей воли, давайте теперь ещё и опростоволосимся, забравшись в занятую баню, да…"
    "Стёкла там, где горел свет, были запотевшими, но в них время от времени мелькал силуэт и я, придвинувшись, попытался разглядеть, кто это там. Если мальчик, тогда всё нормально, а если…"
    "Я с запозданием понял, что я вообще творю, когда конденсат на стёклах стёк вниз, и расплывчатые контуры сложились в девичью фигурку!"
    window hide
    stop ambience fadeout 2
    scene cg d3_sl_bath_unplaited_7dl with flash:
        xalign 0.5 yalign 0.5 zoom 1.15
        linear 0.5 zoom 1.0 xalign 0.5 yalign 0.5
    play music music_list["waltz_of_doubts"] fadein 3

    "Славя! Ох, мать!"
    "Я прикусил пальцы. Она, конечно, обещала мне придержать очередь, но, блин, я и не думал, что она проделает это таким образом!"
    dreamgirl "А каким образом она должна была? Сесть в предбаннике и никого не пускать?"
    "И, в строгом соответствии с законами жанра, голая. Ну, в бане других не бывает."
    "Я шумно сглотнул и уставился на неё. Это же… Это же…"
    dreamgirl "Самое настоящее пипшоу! Кладёшь 25 центов в желоб, и открывается шторка, а там…"
    if alt_day_binder != 1 and not alt_day1_sl_conv and (herc or loki):
        dreamgirl "Сначала мы наблюдаем нижнее белье, перешли непосредственно к стриптизу."
    else:
        dreamgirl "Ну и как оно, в целом? Подвинь мысли похотливые, плохо видно же!"
    dreamgirl "И позволь узнать, чего это ты уставился? Никогда голых баб-с не видел?"
    if alt_day2_date == 21:
        th "Да вчера вообще…"
    else:
        "Видел. Лет пять назад. И то тогда…"
    if alt_day3_dv_evening:
        dreamgirl "Ладно. Ну а что ты скажешь о том, что ты вообще-то с Алисой…"
        th "Алиса — это другое."
    elif alt_day3_un_date:
        dreamgirl "Ладно. Ну а что ты скажешь о том, что ты вообще-то с Леной…"
        th "Лена — это другое."
    elif alt_day3_mi_date:
        dreamgirl "Ладно. Ну а что ты скажешь о том, что ты вообще-то с Мику…"
        th "Мику — это другое."

    dreamgirl "А, ну да, понятно. Губа у тебя не дура, барин."
    dreamgirl "Может, тогда и зайдешь?" 
    dreamgirl "Потрёшь ей что-нибудь… Внутри или снаружи."
    "В голове раздался скабрезный смешок, а я попытался отвернуться — и не смог!"
    "Организм отозвался мгновенно! И единственное, на что он был согласен променять это зрелище — это на непосредственный тактильный контакт. Собственно, электричество уже прогулялось по телу, разбудив все необходимые механизмы, и если бы меня кто застал в том виде, в каком я сейчас был…"
    "Никого вокруг не было, и никто не мешал мне наслаждаться визуальным пиршеством."
    "Недолго."
    "В какой-то момент она, вздрогнув, замерла — будто натолкнувшись на мой взгляд."
    "И пугающе медленно стала разворачиваться…"
    "В сторону окна!"
    "Я в ужасе отпрянул."
    window hide
    with fade
    "Всё, пора идти."
    "Мне надо… Я сказал «надо»! Идти!"
    "Чуть ли не за волосы я утащил себя от окошка."
    window hide
    $ persistent.sprite_time = "night"
    scene bg ext_bathhouse_night with blind_l
    with dissolve
    play ambience ambience_forest_night fadein 3

    "Между тем, свет в помещении погас, и, судя по поскрипыванию досок, Славя закончила мытьё и вышла в предбанник."
    show sl normal dress with dissolve
    "Ещё через пять минут она вышла наружу и с удовольствием вдохнула ночной воздух."
    "На ней было интересное платье-сарафанчик, высоко открывающее её стройные ножки, судя по отсутствию лишних складочек, надетое прямиком на то, что мыли, косы были ослаблены, и я невольно засмотрелся, поняв, что никогда раньше не видел её без этой её вездесущей причёски."
    show sl smile dress with dissolve
    "Тут она заметила меня, сидящего на скамеечке."
    sl "Семён, ты давно здесь?"
    "Спрашиваешь, во всех ли ракурсах я тебя рассмотрел? Ответ положительный."
    me "Конкретно здесь — минут пять, может."
    "Честно признался я."
    me "С лёгким паром!"
    "Правильно, до этого я был в несколько другом месте."
    sl "Спасибо."
    "Она потянулась, и платье, надетое на влажную кожу, рельефно обрисовало то, что должно было обрисовать, а я поспешил уложить полотенце на колени."
    sl "Хорошо как, после баньки! У тебя есть чем мыться? Если нет, могу дать мочалку."

    "У меня опять перед глазами встала картина — где была мочалка и что она там видела — и я срочно приказал организму успокоиться."
    me "Н-нет, спасибо."
    "Поблагодарил я."
    me "У меня своя есть."
    sl "Семён, ты какой-то странный."
    me "К-какой?"
    "Сдавлено крякнул я."
    sl "Напряжённый, что ли…"
    me "Ничего я не…"
    show sl laugh dress with dissolve
    sl "Сём."
    "Я поднял глаза и встретился взглядом с её, полным какого-то бесовского лукавства."
    sl "Ты что, подглядывал?"
    "Кажется, её это совершенно не смущало."
    sl "Серьёзно?"
    "Краснеть дальше уже было некуда, и я всерьёз подумывал о том, чтобы провалиться сквозь землю."
    me "Ничего я не…"
    sl "Да? А у меня другие сведения."
    "Она подошла ближе и, протянув руку, положила её мне на плечо."
    "Она ещё немного постояла, о чём-то раздумывая, и потом качнула головой."
    me "Сладких снов."    
    sl "Доброй ночи."
    "Я проводил её взглядом, весь во власти того волшебного момента, когда она…"
    hide sl with dissolve
    "Зайдя в баню, я в первую очередь налил половину шаечки холодной воды и опрокинул на себя, в голос завопив. Ибо нефиг."
    if alt_day3_dv_evening:
        "И вообще, у меня Алиса есть."
    elif alt_day3_un_date:
       "И вообще, у меня Лена есть."
    elif alt_day3_mi_date:
        "И вообще, у меня Мику есть."
    "Помывшись, я уступил помещение Мику и Лене, подумал было о том, что… И чуть ли не силком уволок себя от бревенчатого здания. Надо спать. Спаааать!"
    return
    
label alt_day3_mi_7dl_init:
    window hide
    scene bg ext_admins_night_7dl with dissolve
    play ambience ambience_camp_center_night fadein 3
    play music music_list["a_promise_from_distant_days"] fadein 2
    "До здания администрации почти не доносилось присущего танцам гула басов."
    "Моё любимое состояние — когда ты уже почти не слышишь никого, но всё ещё чувствуешь…"
    dreamgirl "Чад кутежа?"
    "Услужливо предложил вариант внутренний обормот."
    "Хмыкнув, я согласился с предложенным вариантом."
    th "Жестковато, конечно, но, в целом, верно."
    "Побочный эффект, аура, внешнее настроение — это можно было называть как угодно, но что-то в моей жизни определённо потерялось, когда я перестал ездить в лагеря и угрюмо жаться по углам на дискотеках."
    "Самое скверное, пожалуй, здесь было то, что попытки повторить вылазку, например, в клубы моей Северной Столицы, не давали ровным счётом ничего."
    "На лагерном танцполе все друг друга знают, примелькались, виделись — в городе же толпа представляет собой разрозненный бульон из кучкующихся по знакомствам людей."
    "Потому не то, совершенно."
    "Скамеечка была уже немного влажной от вечерней росы, поэтому я потратил некоторое время, усаживаясь."
    "Глубоко вдохнул…"
    show blinking
    scene black
    pause(2)
    voice "Ты снова сидишь один."
    "Попенял мне чей-то голос."
    "Он звучал на пониженных тонах, опасно соседствуя с шёпотом, а потому я никак не мог подобрать возможного… хозяина? Хозяйку?"
    "Наверное, всё же хозяйку."
    "Парням обычно ничего не надо объяснять — право оставить танцпол уважается всеми."
    me "Есть такое."
    voice "Неужели тебе танцы не нравятся?"
    me "Нравятся."
    "Признание само сорвалось с языка."
    me "Но нравятся как представление, как спектакль, который мне надо смотреть."
    me "А участвовать… увольте."
    voice "Всё-таки, ты странный."
    "Меня хлопнули по плечу, а мгновением спустя я понял, что остался в одиночестве."
    "Ещё один безотказный инстинкт."
    "Все хвалят компьютеры за многозадачность, за способность управляться с сотней дел одновременно."
    "Но какой в этом смысл, если сам человек не слишком-то многозадачен?"
    "Иному умнику придёт в голову попрекать Цезарем и прочими любителями параллельно писать, болтать по телефону и иже с ними."
    "Но где у этих любителей место для сознательного осмысления чего-либо, кто скажет мне?"
    "Всё на поверхности, кругами по воде, отскоками гладыша-монетки."
    "А мне покоя Мику не даёт."
    "Хотя я и так за ней хвостом хожу второй день уже — скоро издеваться начнут."
    "Пальцем показывать."
    dreamgirl "Мальчик с девочкой дружил, мальчик дружбой дорожил…"
    dreamgirl "И не думал он влюбляться, и не знал до этих пор, что он будет называться глупым словом «ухажёр»!"
    dreamgirl "Помнишь мещанское? Скоро и тебя будут подкалывать в том же духе."
    "Нет, меня не очень пугало то, что кто-то станет показывать на меня пальцем."
    "Приятного мало, конечно."
    "Но ничего смертельного."
    "Другое дело, касаемо Мику…"
    if alt_spt > 0:
        "Она же и в самом деле не слишком приспособлена к жизни."
        "Подумать только — чтобы завоевать её безоговорочное доверие, достаточно было просто отнестись к ней как… к человеку?"
        "Не смотреть с восторгом, оценивающе или как-то ещё, как привычно абсолютно всем в моём старом мире."
        "Страшное таинство улыбки нравящемуся тебе человеку, никак не дававшееся мне, в результате свершилось."
        "Наверное, и правда — достаточно просто дождаться подходящего времени и человека."
        "Отчаянную Мику. Рядом с которой можно позволить себе вести чуточку смелее и честнее, чем позволял себе раньше."
    else:
        "Правильно говорят, в тихом омуте черти водятся."
        "Сейчас, в ретроспективе, я понимаю, что, строго говоря, именно она и была той самой встречающей — ибо именно её я увидел первой, когда открыл глаза."
        "Но то ли вид у «девушки-виденья» оказался слишком из ряда вон, то ли бедовая моя головушка не в состоянии была воспринимать поставляемые чудеса — а забыта она оказалась вплоть до момента, когда я пришёл к ней знакомиться."
        "И глядя на пустоголовую болтушку без какой-либо глубины мысли в прозрачном взгляде, как мог я предположить бушующие по ту сторону аквамариновых глаз страсти?"
        "Девочка. Симпатичная. Немного не в моём вкусе, но о них мы и не спорим."
        "Добрая, жизнерадостная и смешливая. Немного тараторит — простительный недостаток при первом знакомстве."
        "А заглянешь внутрь…"
        "У меня не очень большой опыт был по части дружбы, но почему-то захотелось стать для Мику тем, кому она хотя бы сможет доверять."
        "Никто не заслуживает такого одиночества."
    "От размышлений меня оторвали громкие голоса, доносящиеся со стороны медпункта."
    "Глубоко вздохнув, я поднялся и отправился на разведку."
    stop music fadeout 3
    window hide
    scene bg ext_aidpost_night
    with dissolve
    "Дискотека плавно катилась к завершению — а потому шанс наткнуться на досужих пионеров был минимален."
    "Но пару силуэтов, деловито спешащих мимо меня, я сумел разглядеть."
    "И кто бы мог подумать."
    show cs normal at left
    show us calml dress at right
    with dissolve
    "Ульяна Батьковна и Виолетта Церновна."
    me "Что-то случилось?"
    "Я вышел в освещённое пятно под фонарём."
    "Ульянка вздрогнула от неожиданности, а Виола бровью не повела."
    "Махнула рукой — мол, пристраивайся в кильватере — и продолжила движение."
    hide cs with dissolve
    show us normal dress at center with dissolve
    "Делать нечего — пришлось принимать предложение. Любопытно же!"
    me "Мелкая, что стряслось-то?"
    "Шёпотом спросил я."
    us "Ничего. Очкарик звезданулся откуда-то, ударился головой, мозг не пострадал."
    me "А?"
    "До меня не сразу дошло, что девочка пошутила."
    "В конце концов, откуда бы у четырнадцатилетки взяться тяге к чёрному юмору?!"
    show us smile dress with dspr
    us "Бэ! Швыдче, новичок, а то всё пропустишь."
    "Она прибавила шагу, поравнявшись с Виолой, давая понять, что разговор окончен."
    window hide
    scene bg ext_clubs_night
    with dissolve
    play music music_list["door_to_nightmare"] fadein 4
    "Наш путь лежал в сторону ворот."
    "Вернее сказать, к зданию кружков."
    "Когда мы подошли, там уже стояла стайка пионеров и пионерок, оживлённо что-то обсуждающих."
    "К крыше была прислонена невесть откуда взявшаяся лестница, и я мало-помалу начал догадываться."
    "А когда разглядел неловко раскидавшую руки фигуру…"
    if persistent.mi_7dl_good_star or persistent.mi_7dl_good_human:
        "Виною тому были читанные книжки или фильмов я пересмотрел, но от стойкого чувства дежавю избавиться не удавалось."
        "Я как со стороны наблюдал за происходящим, понимая на каком-то уровне, что в случившемся есть и моя вина."
        "А именно — мой несгибаемый эгоизм отчасти причиной тому, что Шурик лежит сейчас после кратковременного полёта с крыши."
        "Зачем он туда залез — вопрос отдельный."
        "Но по основным правилам игры, мне полагалось танцевать на дискотеке, после чего успешно отправиться к клубам и изобразить помощь."
        "Однако, вместо этого меня прямо из-под носа вербовщика ворует разухабиста япона красотка и тащит плясать дальше."
        "А потому и Виола, вместо того, чтобы мучить сороковой уровень «Ксоникса» и баловаться травяным чаем, сидит на коленях, бегло осматривая пребывающего без сознания очкарика."
    "В синем свете уличного фонаря кровь казалась чёрной, и я рассеянно подивился этому."
    "Пока до меня не дошло вдруг, что если это у него из головы натекло…"
    "А, нет."
    "Я пригляделся: кроме неудачного приземления, Шурик умудрился ещё и руку себе разрезать."
    "А Виолетта всё вытирала и вытирала ему лоб, что-то приговаривая."
    "Совершенно игнорируя рассечённую руку, совершенно наплевав на окружающих."
    "А между тем из пореза натекла уже порядочная лужа."
    ba "Так, пионеры, меня интересует только одна вещь."
    show ba normal uniform with dissolve
    "Физрук появился из ниоткуда — в своём неистребимом трико походящий на тоталитарную пародию на Супермена."
    "Почему-то никто не спрашивал, по какому праву он здесь ошивается и задаёт неудобные вопросы."
    ba "Кто здесь был с самого начала и видел всё?"
    th "Он… Опрашивает свидетелей?"
    dreamgirl "А что тебя удивляет?"
    dreamgirl "В СССР служба была не чета нашей — престииииж."
    dreamgirl "Так что ничего удивительного в том, что наш Бориска из какой-нибудь милиции, а сюда ездит в отпуск."
    ba "Успел ли Трофимов что-то сделать на крыше или…"
    cs "Борис, ты вообще, нормальный — такие вопросы задавать?"
    "Возмутилась Виолетта, враз стряхнув ступор."
    "Ещё раз окинула взглядом подопечного, ахнула, обнаружив порез."
    "Тут-то и пригодилась её сумка — медсестра захлопотала над раненым."
    show ba evil uniform with dspr
    ba "Занимайтесь своим делом, Виолетта Церновна. А я буду заниматься своим."
    "Отрезал физрук и снова оглядел присутствующих."
    show el normal pioneer at fleft with dspr
    el "Борис Александрович, я… Это. Я тут был с самого начала."
    $ meet('ba','Саныч')
    "Электроник протолкался вперёд и встал рядом со мной."
    "Посмотрел на натёкшую под товарищем кровяку и, гулко сглотнув, отвернулся."
    ba "Ты всё видел?"
    show el upset pioneer with dspr
    el "Вообще-то, я принимал участие."
    "Сбивчиво пояснил Электроник."
    el "Нам надо было проверить антенну на крыше — помните бурю, когда Ольга Дмитриевна в город уезжала?"
    "Дождавшись кивка, Электроник продолжил."
    show el normal pioneer with dspr
    el "Так вот, мы решили залезть наверх и проверить, если там можно что-то починить."
    show el upset pioneer with dspr
    el "Но я же не знал, что я так сильно высоты боюсь!"
    "Электроник покраснел, но упрямо продолжал рассказ:"
    el "Вот Саша и решил сам лезть."
    th "Почему у Виолы дрожат руки, интересно?"
    "Она накладывала повязку споро, не задумываясь — но пальцы всё равно мелко-мелко тряслись."
    "Едва заметно, если бы я не ожидал это разглядеть — то и не заметил бы вовсе."
    "Тем более, ночью."
    "Тем более, с полутора метров."
    show el normal pioneer with dspr
    el "А сделать он ничего не успел — только до самого края лестницы поднялся."
    el "Похоже, потерял равновесие, попытался схватиться за край крыши…"
    ba "Понято."
    "Саныч отступил на шаг."
    ba "Благодарю за службу, а мне надо сделать один звонок."
    us "Дядь Боря, а помочь?"
    "Удивилась Ульянка."
    ba "Чуть позже, малая."
    hide el
    hide ba
    with fade
    stop music fadeout 3 
    "Он скрылся в потёмках прежде, чем кто-то успел остановить его."
    play music music_list["just_think"] fadein 6
    "Медсестра только рукой махнула."
    "С её подачи Шурика уже перенесли на ровное место, но…"
    "Она продолжала делать что-то с раной очкарика — голову достаточно было просто стабилизировать."
    "Но тщетно."
    "Даже такой далёкий от медицины человек, как я, понимал: если кровь проступает сквозь повязку — кисло дело."
    "Мало-помалу пионеры начали рассасываться — не столько спать, сколько под напутственными пинками вожатых."
    "В итоге, у клубов остались только мы трое: Я, Виола и Шурик."
    cs "Оль, есть сигарета?"
    "Спросила в пустоту сестра."
    "А, ещё и Ольга."
    mt "Ты вообще, думаешь…"
    show cs normal with dspr
    cs "Думаю, Оль, думаю."
    cs "Иногда приходится действовать по обстоятельствам. {w}А они таковы, что мне не хватает компетенции что-то сделать с этой раной."
    show mt surprise dress at left with dissolve
    mt "Ты имеешь в виду…"
    cs "Да. К врачу. К тому, кто знает, что делать в таких вот случаях. А не…"
    "Она бросила на меня взгляд и осеклась."
    show mt normal dress with dspr
    mt "Семён, ты почему ещё здесь?"
    "Я пожал плечами:"
    me "Чувство у меня такое. Что я вот-вот пригожусь."
    "И Виола кивнула в такт со мной:"
    cs "Александра придётся везти до города. А одна я не справлюсь."
    mt "Может, лучше Бориса запряжём?"
    "Сестра отрицательно покачала головой:"
    cs "Нужен кто-то схожих габаритов — и не боящийся крови, как некоторые."
    cs "Короче, кандидатура есть, самоотвода нет."
    mt "Но мне же надо написать вам разрешение на выход…"
    "Тяжёлый вздох, очи горе:"
    cs "Напишешь задним числом. Ну? Пионер ждать не станет."
    "Моим мнением на этот счёт никто не заинтересовался."
    "Но разве мог я упустить возможность легально смотаться на машине в город? Правильно. Не мог."
    stop music fadeout 3
    "Правда, как оказалось, не мог не один я."
    show mi normal pioneer at fright with dissolve
    mi "Ой, Сенечка, а я тебя везде ищу, ты…"
    "Мику уже успела и переодеться, и в порядок себя привести."
    "Как будто и не было вчерашних откровений — всё тот же жизнерадостный ребёнок."
    "С повышенной степенью болтливости."
    show mi surprise pioneer with dspr
    "Она смолкла, заметив лежащего на траве Шурика."
    mi "А что здесь случилось?"
    "Певучим голоском поинтересовались неприятности."
    mt "Ничего, что тебе следовало бы знать."
    "Хмуро ответила вожатая, нарочито посмотрев на часы у себя на запястье."
    mt "Скоро отбой, отправляйся-ка ты спать."
    mi "Но я…"
    show mt smile dress with dspr
    mt "Хатсуне, ты плохо слышишь?"
    "Ласково улыбнулась вожатая."
    show mi upset pioneer with dspr
    mi "Я слышу хорошо. Даже очень, Оленька Дмитриевна. Хватит говорить со мной как с ребёнком."
    show mt normal dress with dspr
    mt "В таком случае, по-взрослому — ноги в руки и в койку марш!"
    "Гаркнула вожатая."
    cs "В принципе, мне не помешал бы ещё один помощник."
    "Задумчиво протянула за нашими спинами Виола."
    "Вожатая аж задохнулась от вероломства подруги."
    show mt angry dress with dspr
    mt "Я кому-то что-то непонятно сказала?"
    show mi dontlike pioneer with dspr
    mi "Вы же слышали, нужна помощь."
    mi "Хватит уже того, что вы меня одну в самом дальнем углу заперли!"
    me "Ольга Дмитриевна?"
    show mt rage dress with dspr
    mt "Я всё сказала."
    mt "Я не отпущу Хатсуне никуда!"
    mt "Я головой отвечаю перед комитетом и лично её отцом за жизнь и здоровье девочки."
    menu:
        "И за совесть тоже?" if herc:
            $ alt_day3_mi_donor = True
            $ alt_hpt += 1
            show mt normal dress with dspr
            mt "То есть?"
            me "Ольга Дмитриевна, вы только что прилюдно заявили, что жизнь и здоровье Шурика значат меньше, чем жизнь и здоровье Мику."
            "Я прищурился и в упор посмотрел ей в глаза."
            me "Вы и правда хотели это сказать?"
            show mt normal dress with dspr
            mt "Вы все прекрасно понимаете, что я хотела сказать."
            "Она торопливо скрестила руки на груди."
            mt "Нет."
            show mi angry pioneer with dspr
            mi "Семён прав. Я не могу позволить вам вести себя так, будто я какая-то особенная только потому, что вы кому-то что-то обещали."
            mi "Силой задерживать меня вы не имеете права. "
            "Она высоко вздёрнула голову."
            mi "Я еду!"
            hide mi with dissolve
            "Она отошла ко мне, мимолётом кивнув."
        "Может, позволите ей выбирать самой?":
            $ alt_day3_mi_donor = True
            $ alt_hpt += 1
            "Я удивился, когда услышал собственный голос."
            me "Возможно, вы не в курсе, но по законам собственной страны она считается достаточно взрослой."
            show mt normal dress with dspr
            mt "Достаточно взрослой для чего?"
            "Я смутился."
            me "Ну… Например, чтобы замуж выйти."
            show mt angry dress with dspr
            mt "Замуж пусть выходит, когда домой уедет!"
            mt "А пока я решаю…"
            show mi angry pioneer with dspr
            mi "Нет. Решаете не вы."
            "Японка выпрямилась во всем свои сто шестьдесят сантиметров и снизу вверх посмотрела на вожатую."
            mi "Простите мне моё неподчинение, Ольга Дмитриевна-сан."
            mi "Но мне и правда будет плохо, если…"
            "Её голос сломался, поэтому я скорее угадал, чем услышал:"
            mi "Если и здесь будет как дома."
            show mi rage pioneer with dspr
            mi "Я не хочу так больше."
            mi "Я вам человек, а не кукла какая-то!"
            hide mi with dissolve
            "Не слушая возражений, она подняла с земли оторвавшийся шеврон с рубашки Шурика и, спрятав его в карман, отошла ко мне."
            "Я понял вдруг, что безмерно горжусь этой девчонкой."
        "Ольга Дмитриевна, я пригляжу за ней." if not (herc or loki):
            $ alt_day3_mi_donor = True
            $ alt_hpt += 1
            "Пообещал я."
            me "Честное слово. Я посмотрю, чтобы всё было хорошо, чтобы никто не попал в беду."
            "Я заискивающе улыбнулся."
            me "Может, вы не верите Мику, но поверьте мне."
            show mi smile pioneer with dspr
            mi "Нам!"
            "Мику отважно спряталась за моей спиной, но я чувствовал на шее тепло её дыхания."
            "Улыбнулся и кивнул."
            "Психология переговоров: мы друзья, мы тепло относимся друг к другу. Давай же, улыбайся."
            "И Ольга улыбнулась."
            show mt grin dress with dspr
            mt "Но если хотя бы волосинка упадёт с её головы…"
            "Её улыбка стала ещё ласковее, и я поневоле сглотнул."
            "Но согласился и с этим."
            "Сейчас я был согласен с чем угодно — только бы нас отпустили!"
            "Сотое чутьё подсказывало мне, что делаем мы сейчас нечто крайне правильное и важное."
            "Минута — и мы расселись в салоне."
            "Шурик спал на заднем, рядом со мной — его зафиксировали так, чтобы не особо трясло во время езды."
            "Я устроился поудобнее и разрешил:"
            me "Отправляемся."
        "То есть мне ехать одному?":
            $ alt_spt += 1
            mt "Вдвоём. Ты и Виолетта — вполне справитесь."
            show mi angry pioneer with dspr
            mi "Но я хочу помочь!"
            "Неожиданно сверкнула глазами японка."
            mi "Вы не можете заставить меня пойти против моих принципов!"
            mt "Хатсуне!"
            mi "Жизнь и здоровье не могут быть важнее или не важнее! "
            show mt normal dress with dspr
            mt "Могут."
            "Спокойно ответила вожатая."
            mt "Чем большему количеству людей причинит боль мысль о том, что тебе плохо — тем важнее твоё здоровье."
            mt "У Шурика есть Виолетта Церновна, Семён. Они позаботятся о нём."
            mt "А представь, сколько людей расстроятся, если {b}с тобой{/b} что-то случится."
            "Прозвучало это так, будто наша поездка обещалась стать невесть какой опасной."
            mt "Пальцев хватит пересчитать?"
            "Голос — мягкий, вкрадчивый. Ласковый."
            "И нет негатива за этим голосом, только святая правота — да, действительно, многие, очень многие расстроятся, если с Мику произойдёт что-нибудь."
            "И когда до японки дошло это — она дрогнула."
            "Сломалась."
            show mi upset pioneer with dspr
            "Отступила."
            "Потупила взгляд в пол, покраснела и, мелко-мелко трясясь, сбивчиво зашептала:"
            show mi cry pioneer with dspr
            mi "Простите меня, пожалуйста, Ольга Дмитриевна-сан. Я дура, я такая дура."
            "Она всхлипнула."
            mi "Но мне так хотелось быть полезной!"
            mt "Ты будешь намного полезнее на своём месте. Кстати,"
            "Вожатая посмотрела на часы."
            mt "До отбоя ещё пятнадцать минут — успеешь технику убрать?"
            hide mi with dissolve
            "Мику, помедлив, кивнула и, волоча ноги, скрылась в потёмках."
            mt "А вас — не смею задерживать."
            "Кивнула Ольга."
            "После чего мне осталось только открыть дверь рядом с Виолой и забраться в душную утробу «Волги»."
    return
    
label alt_day3_sleeptime:
    play ambience ambience_camp_center_night fadein 1
    play music music_list["a_promise_from_distant_days_v2"] fadein 2
    if alt_day3_uvao_spotted:
        scene bg ext_house_of_mt_night_without_light with fade
        "Домик встретил меня тишиной и покоем. Ольги Дмитриевны не было ни внутри, ни на подходе."
        pause(1)
        stop ambience fadeout 1
        window hide
        play sound sfx_open_dooor_campus_1
        scene bg int_house_of_mt_night2 with fade
        "Я прилег на кровать и попробовал еще раз объединить все увиденное и понятое в более-менее цельную картинку."
        "Картинка не складывалась, но чувствовалось, что из всего происходящего торчат чьи-то кошачьи уши."
    elif alt_day3_dv_evening:
        window hide
        scene bg ext_house_of_el_night with dissolve
        pause(1)
        play sound sfx_open_dooor_campus_1
        scene bg int_extra_house_7dl with dissolve
        "Добравшись до места временного ночлега, я раскатал лежащую здесь же скатку, подложил любезно ссуженную Славей подушку и, вытянувшись во весь рост, позволил себе расслабиться."
    else:
        if lp_un >= 12 or (lp_un >= 11 and alt_day1_sl_conv):
            scene bg ext_house_of_mt_night_without_light with fade
            play ambience ambience_camp_center_night fadein 1
            "Это был прекрасный день, это был прекрасный вечер."
            "И как-то вдруг в моей жизни появился человечек, ради которого, вполне возможно, это всё и затевалось."
            "Ведь не просто же так, правда?"
            "Хотелось бы надеяться."
            "Нас связывало что-то с самого первого дня, что-то, заставляющее ловить взгляд в столовой, ждать подле неё же, и искать, всеми правдами и неправдами её компании."
            "И если…"
            "Моё внимание привлёк чей-то силуэт, мнущийся около входной двери."
            show sl normal pioneer with dspr
            "Славя?"
            me "Что ты здесь делаешь?"
            "Требовательно спросил я."
            me "И как давно ты здесь?"
            sl "Изрядно."
            "Разлепила губы девочка."
            sl "Я к тебе."
            dreamgirl "Ого! А не рановато ли? Для третьего дня знакомства?"
            me "Вот как? Что ж…{w} Чем обязан?"
            sl "Я недолго."
            th "Наверное, опять со своими танцами дурацкими."
            if alt_day2_sl_bf and not alt_day3_dancing == 2:
                "Надо было всё же пригласить её — пусть и обещал я не совсем по собственной воле."
            me "Слушаю."
            "Меня нельзя было обвинить в неприветливости — после насыщенного дня я ног под собой не чуял."
            sl "Я по поводу ваших с Леной отношений."
            th "Ага."
            th "Наверное, сейчас самое время закатывать глаза и рявкать, что это не её собачье дело?"
            th "Или «она активистка, ей можно»?"
            sl "Я видела, как вы с ней смотрите друг на друга."
            sl "И должна сказать, что полностью одобряю ваш выбор…"
            "Она споткнулась на полуслове, видимо, не найдя относительно безобидного эпитета."
            sl "Э… Я хотела сказать, что вы стоите друг друга."
            me "Спасибо. Это всё?"
            show sl shy pioneer with dspr
            sl "Не совсем."
            sl "Я пришла сказать вот что:"
            show sl serious pioneer with dspr
            sl "Лена может показаться забитой, тихой девочкой не от мира сего — и так оно и есть. {w}Но она не одна."
            me "Что это значит? У неё кто-то есть? Ты об этом рассказать хотела?"
            sl "Да нет же! Я говорю о том, что она не одинока — и мы, все, кто не первый год её уже знаем, сделаем всё, чтобы она не плакала.{w}Ты понял намёк?"
            me "Нет."
            if loki:
                me "Тебе вообще никто не говорил, что лезть со свечкой в чужие отношения не слишком-то этично?"
            sl "Я, в общем, всё сказала. Обидишь её — будешь иметь дело со мной и другими девочками."
            dreamgirl "Усраться можно как страшно."
            if loki:
                "Меня насмешила её трогательная детская воинственность."
                "Она почему-то напоминала наседку, вступающуюся за своих цыплят."
                "А раз так — то какой смысл объяснять ей, что после того, что случилось со мной, я буду беречь Лену как никогда и никого не берёг?"
                "Может быть, права та странная девочка из автобуса?"
                "Может быть, это и правда мой единственный шанс?"
            me "Теперь-то всё?"
            show sl smile pioneer with dspr
            sl "Да!"
            sl "Сладких тебе снов. И подумай о том, что я сказала."
            th "Не было печали."
            window hide
            stop ambience fadeout 1

        scene bg int_house_of_mt_night2 with fade
    
        "Добравшись до ставшего уже родным домика, я сбросил одежду."
        if alt_day3_mi_date:
            "Стараясь всё же не слишком комкать ни шикарную рубашку, ни очень неплохие брючки со стрелочкой."
    "Мысли текли лениво и размеренно."
    if alt_day3_duty:
        "… Моё сегодняшнее дежурство…"
    elif alt_day2_date == 132:
        th "Лучше бы я с утра не просыпался. Как ни крути"
        th "Да вот как ни крути — а кошмарнее дня мне уже очень давно проживать не доводилось."
        th "Увлекательная стори. И всё о том, как я облажался."
        "Завершившаяся феерическим унижением руками Двачевской."
        jump alt_day3_router_dv
            
    else:
        "С этой беготнёй по лагерю, со всеми этими делами, я уставал так, что…"

    if alt_day3_un_event:
        "Мои утренние проказы в библиотеке, с целым драконом и вражеским войском."
        if alt_day3_un_invite and (herc or loki):
            "Вылившиеся в послеобеденный поход с трогательным объяснением."
        if alt_day3_un_med_help == 1:
            "Таблетка от головной боли, оказавшаяся не тем, о чём я думал."
            "Наш танец."
            "… Лена…"
            jump alt_day3_router_un
        
    if alt_day3_sl_event:
        "Поход на пляж, обернувшийся возможностью узнать, что обо мне думают девочки."
        if alt_day3_sl_day_event2:
            "Трогательная история той, что становится мне всё ближе и роднее."
        if alt_day3_dancing == 2 and alt_day3_dancing == 20:
            "Наши танцы. Её — белый, и мой… {w}Когда я наконец решился."
            "И, конечно, тот эпический поход по ночному лагерю с ней на руках."
            "Как забавно оказалось, что она слышала наш с Женей диалог, и то, как улыбнулась, когда я целовал её в лоб."
            "Вряд ли у неё остались сомнения относительно меня и моего к ней отношения."
            jump alt_day3_router_sl

    if alt_day3_mi_event or alt_day3_dv_event and not alt_day3_mi_dj:
        "… Концерт Мику…"
        if alt_day3_dv_invite:
            "…её же, охотящуюся на меня…"
        if alt_day3_dv_evening:
            "… Наш с Двачевской феерический в своей глупости спектакль о том, кто кого чем и зачем…"
            "… Удивительная откровенность и открытость грустной девочки с фиолетовыми волосами…"
        if alt_day3_sl_bath_voy and not alt_day3_mi_dj:
            "… Сценка в бане…"
            jump alt_day3_router_dv
        
    if alt_day3_mi_date:
        "И глупая, и смешная ситуация в костюмерной."
        if alt_day3_dancing == 4 and not alt_day3_mi_dj:
            "Уроки танцев…"
        if alt_day3_dancing == 40:
            "…Вечерний танец."
        if alt_day3_mi_invite2:
            extend " Мы оба в великолепных нарядах, и невольная гордость за то, что она — такая красивая."
        else:
            extend ", куда более горячий."
        if alt_day3_mi_date:
            "Дурацкая сценка с вожатой, которая подумала ровно то, что должна была подумать."
        if alt_day3_mi_dj:
            "Японская девочка в огромных наушниках за вертушками."
        if alt_day3_sl_bath_voy:
            "Эротическое шоу в бане."
        jump alt_day3_router_mi
        
    if alt_day3_sl_invite:
        "Кошмар с этими колонками."
    if alt_day3_technoquest_st2:
        "Беготня с этим радио."
        "И лестницами."
    if alt_day3_dv_dj:
        "Мой новый возлюбленный диджей — Элис Диджей…"
    if alt_day3_mi_dj:
        "Японская девочка в огромных наушниках за вертушками."
    if alt_day3_dv_evening:
        "Вечер наперевес с гитарой."
    if alt_day3_us_bugs == 1:
        "Жуки, которых мы с Ульянкой наловили и подложили злюке-вожатой."
    if alt_day3_sl_bath_voy:
        "… Сценка в бане…"
    if alt_dlc_active:
        call alt_day3_uvao_ch4
        pause(1)
    "… Денёк вышел тот ещё…"
    "С последней мыслью я уснул."
    jump alt_day3_slots